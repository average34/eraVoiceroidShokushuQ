;口上用文字色関係の処理

;色変更対象の選択
@CHANGE_PT_KOJO_COLOR
;LOCAL
;0	LOOP
;1	主人公
;2	味方１
;3	味方２
;4	TEMP

DRAWLINE
PRINTL 誰の口上の文字色を変更しますか？
DRAWLINE

LOCAL:1 = 1
LOCAL:2 = FLAG:10
LOCAL:3 = FLAG:11

;PTメンバー表示
FOR LOCAL:0, 1, 4
	IF LOCAL:(LOCAL:0) > 0
		LOCAL:4 = CFLAG:(LOCAL:(LOCAL:0)):700
		PRINTFORM [{LOCAL:0}] %CALLNAME:(LOCAL:(LOCAL:0)),10,LEFT%  現在の文字色：
		SETCOLOR LOCAL:4
		PRINTFORML %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:4 / 0x10000} G:{(LOCAL:4 & 0xFF00) / 0x100} B:{LOCAL:4 & 0xFF})
		RESETCOLOR
	ENDIF
NEXT

PRINTL [0] 戻る

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT >= 1 && RESULT <= 3
	IF LOCAL:(RESULT) > 0
		CALL SET_KOJO_COLOR(LOCAL:(RESULT))
	ELSE
		REUSELASTLINE 入力値が不正です。再入力してください
		GOTO INPUT_LOOP
	ENDIF
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

RESTART



;指定キャラの文字色変更
;ARG:0	対象キャラID
@SET_KOJO_COLOR(ARG:0)
;LOCAL
;0	LOOP
;1	今の文字色
;2	デフォルトカラー

PRINTFORML 
PRINTFORML %CALLNAME:(ARG:0)% の口上文字色を設定します
LOCAL:1 = CFLAG:(ARG:0):700
CALL GET_KOJO_DEFAULT_COLOR(ARG:0)
LOCAL:2 = RESULT
SETCOLOR LOCAL:1
PRINTFORML 現在の文字色：%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
RESETCOLOR
PRINTFORML 
PRINTFORML 何色に変更しますか？
SETCOLOR LOCAL:2
PRINTFORML [1] 口上のデフォルト色 %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:2 / 0x10000} G:{(LOCAL:2 & 0xFF00) / 0x100} B:{LOCAL:2 & 0xFF})
RESETCOLOR
PRINTFORML [2] 直接色を指定：16進数（0xFFFFFF）形式で指定
PRINTFORML [3] 直接色を指定：RGBを10進数で指定
PRINTFORML [4] システムの標準色
PRINTFORML [5] サンプルカラーの中から選ぶ
PRINTFORML [0] キャンセル

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN
ELSEIF RESULT == 1
	CFLAG:(ARG:0):700 = LOCAL:2
ELSEIF RESULT == 2
	CALL INPUT_FULL_COLOR
	IF RESULT == 0
		;キャンセルされているので、何もしない
		RETURN
	ENDIF
	CFLAG:(ARG:0):700 = RESULT
ELSEIF RESULT == 3
	CALL INPUT_RGB_COLOR
	IF RESULT == 0
		;キャンセルされているので、何もしない
		RETURN
	ENDIF
	CFLAG:(ARG:0):700 = RESULT
ELSEIF RESULT == 4
	RESETCOLOR
	CFLAG:(ARG:0):700 = GETCOLOR()
ELSEIF RESULT == 5
	CALL INPUT_SAMPLE_COLOR
	IF RESULT == 0
		;キャンセルされているので、何もしない
		RETURN
	ENDIF
	CFLAG:(ARG:0):700 = RESULT
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

SETCOLOR CFLAG:(ARG:0):700
PRINTFORMW %CALLNAME:(ARG:0)% の口上文字色を %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{CFLAG:(ARG:0):700 / 0x10000} G:{(CFLAG:(ARG:0):700 & 0xFF00) / 0x100} B:{CFLAG:(ARG:0):700 & 0xFF}) に設定しました
RESETCOLOR

RETURN



;16進数による文字色指定
@INPUT_FULL_COLOR
;LOCAL
;1	TEMP

PRINTFORML 
PRINTFORML 文字色を16進数（0xFFFFFF）形式で指定してください

INPUTS

LOCAL:1 = TOINT(RESULTS)

IF LOCAL:1 == 0 || LOCAL:1 > 0xFFFFFF || LOCAL:1 < 0
	;本当に0（真っ黒）を入力された場合も認識できない扱いになるけど、気にしない
	PRINTFORML 入力された内容が認識できなかったため、システムカラー（0xC0C0C0）を利用します
	LOCAL:1 = 0xC0C0C0
ENDIF

SETCOLOR LOCAL:1
PRINTFORML %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
RESETCOLOR
PRINTFORML この色でよろしいですか？
PRINTFORML [0] はい
PRINTFORML [1] いいえ
PRINTFORML [2] 変更をキャンセルする

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN LOCAL:1
ELSEIF RESULT == 1
	RESTART
ELSEIF RESULT == 2
	PRINTFORMW 色の変更をキャンセルしました
	RETURN 0
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF



;10進数RGBによる文字色指定
@INPUT_RGB_COLOR
;LOCAL
;1	TEMP
;2	R
;3	G
;4	B

PRINTFORML 
PRINTFORML 文字色のRGBを10進数で指定します

PRINTFORML R(赤色) の割合を 0～255 で指定してください

$INPUT_LOOP_R
INPUT

IF RESULT < 0 || RESULT > 255
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP_R
ENDIF

LOCAL:2 = RESULT

PRINTFORML G(緑色) の割合を 0～255 で指定してください

$INPUT_LOOP_G
INPUT

IF RESULT < 0 || RESULT > 255
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP_G
ENDIF

LOCAL:3 = RESULT

PRINTFORML B(青色) の割合を 0～255 で指定してください

$INPUT_LOOP_B
INPUT

IF RESULT < 0 || RESULT > 255
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP_B
ENDIF

LOCAL:4 = RESULT

LOCAL:1 = CONVERT_RGB(LOCAL:2, LOCAL:3, LOCAL:4)

IF LOCAL:1 == 0
	PRINTFORML 真っ黒は利用できないため、システムカラー（0xC0C0C0）を利用します
	LOCAL:1 = 0xC0C0C0
ENDIF

SETCOLOR LOCAL:1
PRINTFORML %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
RESETCOLOR
PRINTFORML この色でよろしいですか？
PRINTFORML [0] はい
PRINTFORML [1] いいえ
PRINTFORML [2] 変更をキャンセルする

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN LOCAL:1
ELSEIF RESULT == 1
	RESTART
ELSEIF RESULT == 2
	PRINTFORMW 色の変更をキャンセルしました
	RETURN 0
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF



;サンプル色選択による文字色指定
@INPUT_SAMPLE_COLOR
;LOCAL
;1	TEMP

PRINTFORML 
PRINTFORML 文字色を以下のサンプル色の中から選んでください

LOCAL:1 = 0xCF0125
SETCOLOR LOCAL:1
PRINTFORML [ 1] %"チェリーレッド", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0xEB5381
SETCOLOR LOCAL:1
PRINTFORML [ 2] %"カーネーションピンク", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0xF5A0BD
SETCOLOR LOCAL:1
PRINTFORML [ 3] %"フクシアピンク", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0xF79101
SETCOLOR LOCAL:1
PRINTFORML [ 4] %"ゴールデンオレンジ", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0xE49E61
SETCOLOR LOCAL:1
PRINTFORML [ 5] %"小麦色", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0xFFF352
SETCOLOR LOCAL:1
PRINTFORML [ 6] %"レモンイエロー", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0x7FFF00
SETCOLOR LOCAL:1
PRINTFORML [ 7] %"シャルトリューズ", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0x2CB232
SETCOLOR LOCAL:1
PRINTFORML [ 8] %"パラキートグリーン", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0x00A6AF
SETCOLOR LOCAL:1
PRINTFORML [ 9] %"薄浅葱", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0x49BDF0
SETCOLOR LOCAL:1
PRINTFORML [10] %"パステルブルー", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0x4169E1
SETCOLOR LOCAL:1
PRINTFORML [11] %"ロイヤルブルー", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0x0063A7
SETCOLOR LOCAL:1
PRINTFORML [12] %"シアンブルー", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0x7F1184
SETCOLOR LOCAL:1
PRINTFORML [13] %"ロイヤルパープル", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0xBC64A4
SETCOLOR LOCAL:1
PRINTFORML [14] %"若紫", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0x626063
SETCOLOR LOCAL:1
PRINTFORML [15] %"スレートグレイ", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
LOCAL:1 = 0x55461B
SETCOLOR LOCAL:1
PRINTFORML [16] %"ガンメタル", 20, LEFT% %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{LOCAL:1 / 0x10000} G:{(LOCAL:1 & 0xFF00) / 0x100} B:{LOCAL:1 & 0xFF})
RESETCOLOR
PRINTFORML [ 0] キャンセル

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 1
	RETURN 0xCF0125
ELSEIF RESULT == 2
	RETURN 0xEB5381
ELSEIF RESULT == 3
	RETURN 0xF5A0BD
ELSEIF RESULT == 4
	RETURN 0xF79101
ELSEIF RESULT == 5
	RETURN 0xE49E61
ELSEIF RESULT == 6
	RETURN 0xFFF352
ELSEIF RESULT == 7
	RETURN 0x7FFF00
ELSEIF RESULT == 8
	RETURN 0x2CB232
ELSEIF RESULT == 9
	RETURN 0x00A6AF
ELSEIF RESULT == 10
	RETURN 0x49BDF0
ELSEIF RESULT == 11
	RETURN 0x4169E1
ELSEIF RESULT == 12
	RETURN 0x0063A7
ELSEIF RESULT == 13
	RETURN 0x7F1184
ELSEIF RESULT == 14
	RETURN 0xBC64A4
ELSEIF RESULT == 15
	RETURN 0x626063
ELSEIF RESULT == 16
	RETURN 0x55461B
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF




;指定キャラのデフォルト文字色取得
;ARG:0	対象キャラID
@GET_KOJO_DEFAULT_COLOR(ARG:0)
TARGET = ARG:0
TRYCCALLFORM KOJO_DEFAULT_COLOR_{NO:(ARG:0)}_{CFLAG:(ARG:0):701}
CATCH
	;デフォルト色取得関数が無い場合、標準の文字色
	RESULT = 0xC0C0C0
ENDCATCH

IF RESULT == 0
	;0が帰ってきている場合は、標準の文字色を使う
	RESULT = 0xC0C0C0
ENDIF

RETURN RESULT


;全員の文字色初期設定
@INIT_KOJO_DEFAULT_COLOR
#DIM LCOUNT

	FOR LCOUNT, 1, CHARANUM
		CALL INIT_KOJO_DEFAULT_COLOR_SINGLE(LCOUNT)
	NEXT

;文字色初期設定 キャラクタ単体版
@INIT_KOJO_DEFAULT_COLOR_SINGLE(CHARA_ID)
#DIM CHARA_ID

	;グローバルセーブから取得　念のためロードしなおしておく
	LOADGLOBAL

	CFLAG:CHARA_ID:701 = GLOBAL:(NO:CHARA_ID + 300)
	IF CFLAG:CHARA_ID:700 == 0
		CALL GET_KOJO_DEFAULT_COLOR(CHARA_ID)
		CFLAG:CHARA_ID:700 = RESULT
	ENDIF
			
;!!! ぱにめーしょんが標準搭載になったので、多分こっちは不要
;RGB形式の文字色指定を16進数に変換
;ARG:0	R
;ARG:1	G
;ARG:2	B
@CONVERT_RGB(ARG:0, ARG:1, ARG:2)
#FUNCTION

LOCAL:0 = (ARG:0 * 0x10000) + (ARG:1 * 0x100) + ARG:2

RETURNF LOCAL:0



