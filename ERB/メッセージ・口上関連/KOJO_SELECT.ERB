;口上詳細設定
@KOJO_SELECT
PRINTFORML 各キャラクターの口上について、表示/非表示や、どの口上を使うかを設定します
PRINTFORML 誰の口上を設定しますか？

FOR LOCAL:0, 1, CHARANUM
	IF CFLAG:(LOCAL:0):104 == 0
		LOCALS:0 = %CALLNAME:(LOCAL:0)%の口上
		PRINTFORM [{LOCAL:0, 2}] %LOCALS:0, 25, LEFT%
		IF CFLAG:(LOCAL:0):701 < 0
			RESULTS = 表示しない
		ELSE
			TRYCCALLFORM KOJO_TITLE_{NO:(LOCAL:0)}_{CFLAG:(LOCAL:0):701}
			CATCH
				;タイトルが取得できない場合、本体があるかどうか確認
				TRYCCALLFORM KOJO_{NO:(LOCAL:0)}_{CFLAG:(LOCAL:0):701}(0, 0)
					;口上本体がある場合、汎用タイトルを表示
					RESULTS = %CALLNAME:(LOCAL:0)% 口上その{CFLAG:(LOCAL:0):701 + 1}
				CATCH
					;口上本体が無い場合、表示状態のみ
					RESULTS = 表示する
				ENDCATCH
			ENDCATCH
		ENDIF
		PRINTFORML %RESULTS%
	ENDIF
NEXT
DRAWLINEFORM -
PRINTFORML [97] %"口上がないキャラに汎用口上を利用する", 40, LEFT%\@(FLAG:595 == 0) ? 利用する # 利用しない\@
SIF FLAG:595 == -1
	SETCOLOR 0X333333
PRINTFORML [\@(FLAG:595 == 0) ? 98 # --\@] %"汎用口上に自動で色を付ける", 40, LEFT%\@(FLAG:596 == 0) ? 利用する # 利用しない\@
RESETCOLOR
PRINTFORML [99] 全OFF
PRINTFORML [ 0] 決定

$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN
ELSEIF RESULT == 97
	FLAG:595 = (FLAG:595 == 0) ? -1 # 0
	RESTART
ELSEIF RESULT == 98
	FLAG:596 = (FLAG:596 == 0) ? -1 # 0
	RESTART
ELSEIF RESULT == 99
	FOR LOCAL:0, 1, CHARANUM
		CFLAG:(LOCAL:0):701 = -1
	NEXT
	FLAG:595 = -1
	RESTART
ELSEIF RESULT > 0 && RESULT < CHARANUM && CFLAG:(RESULT):104 == 0
	CALL KOJO_SELECT_2(RESULT)
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

RESTART


;個別の口上設定
;ARG:0	対象キャラID
@KOJO_SELECT_2(ARG:0)
#DIM PREV_KOJO_COLOR
TARGET = ARG:0
VARSET LOCAL, 0, 1, 11

PRINTFORML %CALLNAME:(ARG:0)%の口上としてどの口上を利用するか選択してください
IF CFLAG:(ARG:0):701 < 0
	PRINTFORML 現在は口上非表示に設定されています
ELSE
	PRINTFORML 現在は{CFLAG:(ARG:0):701 + 1}番目の口上を利用するように設定されています
ENDIF

FOR LOCAL:0, 1, 11
	TRYCCALLFORM KOJO_{NO:(ARG:0)}_{LOCAL:0 - 1}(0, 0)
		;口上本体がある場合、項目を表示
		LOCAL:(LOCAL:0) = 1
	CATCH
		;口上本体が無い場合、表示を飛ばす
		LOCAL:(LOCAL:0) = 0
		CONTINUE
	ENDCATCH
	
	PRINTFORM [{LOCAL:0, 2}] 
	TRYCCALLFORM KOJO_TITLE_{NO:(ARG:0)}_{LOCAL:0 - 1}
		PRINTFORML %RESULTS%
	CATCH
		;タイトルが取得できない場合、汎用タイトルを表示
		PRINTFORML %CALLNAME:(ARG:0)% 口上その{LOCAL:0}
	ENDCATCH
NEXT
IF LOCAL:1 == 0
	;デフォルト口上が存在しない場合、「表示する」を足しておく
	PRINTFORML [ 1] 口上を表示する
ENDIF
PRINTFORML [-1] 口上を表示しない
PRINTFORML [ 0] キャンセル

$INPUT_LOOP
INPUT

IF RESULT == -1
	PRINTFORMW %CALLNAME:(ARG:0)%の口上を表示しないようにしました
	CFLAG:(ARG:0):701 = -1
ELSEIF RESULT == 0
	RETURN
ELSEIF RESULT > 0 && RESULT < 11 && LOCAL:(RESULT) == 1
	LOCAL:0 = RESULT
	TRYCCALLFORM KOJO_DETAIL_{NO:(ARG:0)}_{LOCAL:0 - 1}
	CATCH
		;詳細表示ができない場合、デフォルト文章を表示
		PRINTFORML この口上に詳細な説明はありません
	ENDCATCH
	PRINTFORML 
	PRINTFORML この口上でよろしいですか？
	CALL ASK_YN
	IF RESULT == 0
		;口上文字色の変更確認で使う情報を保存
		PREV_KOJO_COLOR = CFLAG:(ARG:0):700
		
		PRINTFORMW %CALLNAME:(ARG:0)%の口上を変更しました
		CFLAG:(ARG:0):701 = LOCAL:0 - 1
		
		;現在の文字色と変更後の文字色が同じ場合も変化が起きない為、選択処理をスキップ
		CALL GET_KOJO_DEFAULT_COLOR(ARG:0)
		IF PREV_KOJO_COLOR == RESULT
			CFLAG:(ARG:0):700 = RESULT
			
		;口上色選択
		ELSE
			PRINTFORML
			PRINTFORML 利用する口上文字色を選択して下さい
			
			SETCOLOR PREV_KOJO_COLOR
			PRINTFORML [0] 現在の文字色：%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{PREV_KOJO_COLOR / 0x10000} G:{(PREV_KOJO_COLOR & 0xFF00) / 0x100} B:{PREV_KOJO_COLOR & 0xFF})
			RESETCOLOR
			
			CALL GET_KOJO_DEFAULT_COLOR(ARG:0)
			SETCOLOR RESULT
			PRINTFORML [1] 変更後口上のデフォルト色 %UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%%UNICODE(0x2586)%　(R:{RESULT / 0x10000} G:{(RESULT & 0xFF00) / 0x100} B:{RESULT & 0xFF})
			RESETCOLOR
			
			$KOJO_COLOR_SELECT
			INPUT
			
			SELECTCASE RESULT
			CASE 0
				CFLAG:(ARG:0):700 = PREV_KOJO_COLOR
				PRINTFORMW %CALLNAME:(ARG:0)%の口上色を決定しました
			CASE 1
				CALL GET_KOJO_DEFAULT_COLOR(ARG:0)
				CFLAG:(ARG:0):700 = RESULT
				PRINTFORMW %CALLNAME:(ARG:0)%の口上色を決定しました
			CASEELSE
				REUSELASTLINE 入力値が不正です。再入力してください
				GOTO KOJO_COLOR_SELECT
			ENDSELECT
		ENDIF
	ELSE
		RESTART
	ENDIF
ELSEIF RESULT == 1
	;デフォルト口上無しで「表示する」を選択した場合
	PRINTFORMW %CALLNAME:(ARG:0)%の口上を表示するようにしました
	CFLAG:(ARG:0):701 = 0
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

;ここまで来た場合、設定が変わっているのでグローバルにも保存
LOADGLOBAL
GLOBAL:(NO:(ARG:0) + 300) = CFLAG:(ARG:0):701
SAVEGLOBAL

