;敵データ設定
;ARG:0	設定場所
;ARG:1	元キャラのID
@SET_ENEMY_DATA_69(ARG:0, ARG:1)

;簡易悪堕ちだった場合
IF ARG:1 > 0 && FLAG:2000 == 1
	DA:(ARG:0):1 = ABL:(ARG:1):98 * 100 + 8000
	DA:(ARG:0):2 = ABL:(ARG:1):98 * 100 + 8000
	DA:(ARG:0):3 = 1
	DA:(ARG:0):5 = 50 + ABL:(ARG:1):98 / 3
	DA:(ARG:0):9 = 100 + ABL:(ARG:1):97 / 2
	;●入れ替わりパッチ追加START
	DA:(ARG:0):49 = CFLAG:(ARG:1):104
	;●END
	;念のため元キャラのIDを残しておく
	DA:(ARG:0):46 = ARG:1
	;転生組である旨を記録
	DA:(FLAG:1):99=2
	SAVESTR:(ARG:0) = ネコマタ%CALLNAME:(ARG:1)%
	;元キャラがダミーデータの場合パス　バグ対策
	SIF ARG:1 == 0 || FLAG:997 == 81
		RETURN
;通常湧きの場合
ELSE
	DA:(ARG:0):1 = 6000
	DA:(ARG:0):2 = 6000
	DA:(ARG:0):3 = 1
	DA:(ARG:0):5 = 50
	DA:(ARG:0):9 = 100
	SAVESTR:(ARG:0) = %ENEMY_NAME(69)%
ENDIF

DA:(ARG:0):15 = 70
DA:(ARG:0):16 = 0
DA:(ARG:0):17 = 30
DA:(ARG:0):18 = 90
DA:(ARG:0):19 = 10


;図鑑用触手データ
DA:(ARG:0):85 = 5
DA:(ARG:0):86 = 3
DA:(ARG:0):89 = 5



;戦闘中の行動決定
;ARG:0	設定場所
;ARG:1	対象キャラ
@SELECT_ENEMY_ACTION_69(ARG:0, ARG:1)
;LOCAL
;0	LOOP
;1	拘束フラグ
;2	選択コマンド
;3	確率計算用

LOCAL:2 = 0

;誰かを拘束しているかチェック
LOCAL:1 = 0
IF DA:(ARG:0):20 == 1
	;１人目拘束
	LOCAL:1 = 1
ELSEIF FLAG:10 && DA:(ARG:0):20 == FLAG:10
	;２人目拘束
	LOCAL:1 = FLAG:10
ELSEIF FLAG:11 && DA:(ARG:0):20 == FLAG:11
	;３人目拘束
	LOCAL:1 = FLAG:11
ELSEIF DA:(ARG:0):20 > 0 || DA:(ARG:0):10 > 0
	;PT外メンバー拘束
	LOCAL:1 = -1
ENDIF


;賢い敵なら相手の状態に合わせてコマンド選んだりさせたい
;頭が悪そうな敵も多いので、基本はランダムで良いと思うけど

IF LOCAL:1 > 0
	;PTメンバーを拘束している場合
	;拘束相手には強性攻撃・寄生攻撃・戦意喪失済みなら解放　が選択肢
	;それ以外の相手には拘束攻撃以外
	IF ARG:1 == LOCAL:1
		;拘束している相手を攻撃
		;既に戦意喪失しても離してくれない
		LOCAL:2 = 1
		LOCAL:3 = RAND:19
		SELECTCASE LOCAL:3
			CASE 0 TO 1
				LOCAL:2 = 763
			CASE 2 TO 3
				LOCAL:2 = 764
			CASE 4 TO 7
				LOCAL:2 = 765
			CASE 8 TO 9
				LOCAL:2 = 1013
			CASE 10 TO 12
				LOCAL:2 = 1018
			CASE 13 TO 15
				LOCAL:2 = 1019
			CASE 16
				LOCAL:2 = 1024
			CASEELSE
				LOCAL:2 = 870
		ENDSELECT
	ELSE
		;キャラを拘束中だが、別のキャラを攻撃
		LOCAL:2 = 1915
	ENDIF
ELSEIF LOCAL:1 < 0
	;PT外メンバーを拘束している場合
	;PTには拘束攻撃以外
	LOCAL:3 = RAND:27
	SELECTCASE LOCAL:3
		CASE 0 TO 20
			LOCAL:2 = 184
		CASEELSE
			LOCAL:2 = 264
		ENDSELECT
ELSE
	;通常時
	;色々
	IF CFLAG:(ARG:1):10 || CFLAG:(ARG:1):20 || (DA:(ARG:0):10 && DA:(ARG:0):20) || (CFLAG:(ARG:1):29 && RAND:100 < 70)
		LOCAL:3 = RAND:27
	ELSE
		IF BASE:(ARG:1):0 < MAXBASE:(ARG:1):0 / 2 || BASE:(ARG:1):2 < MAXBASE:(ARG:1):2 * 2 / 3
			LOCAL:3 = RAND:38
		ELSE
			LOCAL:3 = RAND:33
		ENDIF
	ENDIF
	SELECTCASE LOCAL:3
		CASE 0 TO 18
			LOCAL:2 = 184
		CASE 19 TO 24
			LOCAL:2 = 264
		CASEELSE
			LOCAL:2 = 576
	ENDSELECT
ENDIF

;ここまで来たら確定
DA:(ARG:0):50 = LOCAL:2
DA:(ARG:0):51 = ARG:1
DA:(ARG:0):52 = 0



