;敵データ設定
;ARG:0	設定場所
;ARG:1	Reserved
@SET_ENEMY_DATA_93(ARG:0, ARG:1)

DA:(ARG:0):1 = 6000
DA:(ARG:0):2 = 6000
DA:(ARG:0):3 = 1
DA:(ARG:0):5 = 50
DA:(ARG:0):9 = 100

DA:(ARG:0):15 = 0
DA:(ARG:0):16 = 20
DA:(ARG:0):17 = 80
DA:(ARG:0):18 = 80
DA:(ARG:0):19 = 10


;図鑑用触手データ
DA:(ARG:0):85 = 5
DA:(ARG:0):86 = 4
DA:(ARG:0):89 = 11

IF RAND:2 == 0
	DA:(ARG:0):88 = 2
ENDIF


SAVESTR:(ARG:0) = %ENEMY_NAME(93)%

;戦闘中の行動決定
;ARG:0	設定場所
;ARG:1	対象キャラ
@SELECT_ENEMY_ACTION_93(ARG:0, ARG:1)
;LOCAL
;0	LOOP
;1	拘束フラグ
;2	選択コマンド
;3	確率計算用

LOCAL:2 = 0

;誰かを拘束しているかチェック
LOCAL:1 = 0
IF DA:(ARG:0):20 == 1
	;１人目拘束
	LOCAL:1 = 1
ELSEIF FLAG:10 && DA:(ARG:0):20 == FLAG:10
	;２人目拘束
	LOCAL:1 = FLAG:10
ELSEIF FLAG:11 && DA:(ARG:0):20 == FLAG:11
	;３人目拘束
	LOCAL:1 = FLAG:11
ELSEIF DA:(ARG:0):20 > 0 || DA:(ARG:0):10 > 0
	;PT外メンバー拘束
	LOCAL:1 = -1
ENDIF


;賢い敵なら相手の状態に合わせてコマンド選んだりさせたい
;頭が悪そうな敵も多いので、基本はランダムで良いと思うけど

IF LOCAL:1 > 0
	;PTメンバーを拘束している場合
	;拘束相手には強性攻撃・寄生攻撃・戦意喪失済みなら解放　が選択肢
	;それ以外の相手には拘束攻撃以外
	IF ARG:1 == LOCAL:1
		;拘束している相手を攻撃
		IF CFLAG:(ARG:1):29 && RAND:100 < 40
			;既に戦意喪失している場合、一定確率で解放する
			LOCAL:2 = 1
		ELSE
			LOCAL:3 = RAND:14
			SELECTCASE LOCAL:3
				CASE 0 TO 1
					LOCAL:2 = 611
				CASE 2 TO 3
					LOCAL:2 = 634
				CASE 4 TO 7
					LOCAL:2 = 660
				CASE 8 TO 10
					LOCAL:2 = 674
				CASE 11 TO 12
					LOCAL:2 = 1003
				CASEELSE
					LOCAL:2 = 815
			ENDSELECT
		ENDIF
	ELSE
		;キャラを拘束中だが、別のキャラを攻撃
		LOCAL:2 = 1915
	ENDIF
ELSEIF LOCAL:1 < 0
	;PT外メンバーを拘束している場合
	;PTには拘束攻撃以外
	LOCAL:3 = RAND:27
	SELECTCASE LOCAL:3
		CASE 0 TO 5
			LOCAL:2 = 611
		CASE 6 TO 13
			LOCAL:2 = 634
		CASE 14 TO 22
			LOCAL:2 = 660
		CASEELSE
			LOCAL:2 = 674
		ENDSELECT
ELSE
	;通常時
	;色々
	IF CFLAG:(ARG:1):10 || CFLAG:(ARG:1):20 || (DA:(ARG:0):10 && DA:(ARG:0):20) || (CFLAG:(ARG:1):29 && RAND:100 < 70)
		LOCAL:3 = RAND:27
	ELSE
		IF BASE:(ARG:1):0 < MAXBASE:(ARG:1):0 / 2 || BASE:(ARG:1):2 < MAXBASE:(ARG:1):2 * 2 / 3
			LOCAL:3 = RAND:38
		ELSE
			LOCAL:3 = RAND:33
		ENDIF
	ENDIF
	SELECTCASE LOCAL:3
		CASE 0 TO 24
			LOCAL:2 = 1915
		CASEELSE
			LOCAL:2 = 540
	ENDSELECT
ENDIF

;ここまで来たら確定
DA:(ARG:0):50 = LOCAL:2
DA:(ARG:0):51 = ARG:1
DA:(ARG:0):52 = 0



