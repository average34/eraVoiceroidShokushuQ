;危機回避

;指定されたレベルに必要な経験取得
;ARG:0	調べたいレベル（要するに１～３を指定することになる）
@GET_NEED_EXP_5(ARG:0)
LOCAL:0 = 0

IF ARG:0 == 1
	LOCAL:0 = 3000
ELSEIF ARG:0 == 2
	LOCAL:0 = 15000
ELSEIF ARG:0 == 3
	LOCAL:0 = 50000
ELSEIF ARG:0 > 3
	LOCAL:0 = -1
ENDIF

IF LOCAL:0 > 0
	;素質などによる補正
ENDIF

RETURN LOCAL:0



;経験値獲得
;ARG:0	獲得したキャラ（IDを指定　つまり1～10）
;ARG:1	獲得量
@ADD_ABL_EXP_5(ARG:0, ARG:1)
LOCAL:0 = ARG:1
;ソースコード変更量を抑えるために、ABLNOを入れておく
LOCAL:1 = 5


;素質などによる補正
IF ABL:(ARG:0):98 < 50
	LOCAL:0 = LOCAL:0 * (250 - ABL:(ARG:0):98 * 3) / 100
ELSEIF ABL:(ARG:0):98 < 100
	LOCAL:0 = LOCAL:0 * (150 - ABL:(ARG:0):98) / 100
ELSE
	LOCAL:0 = LOCAL:0 / 2
ENDIF

IF TALENT:(ARG:0):51
	LOCAL:0 = LOCAL:0 * 15 / 10
ENDIF



IF FLAG:501 == 0 && FLAG:20 == 0 && CFLAG:(ARG:0):4 == CFLAG:1:4
	PRINTFORML %CALLNAME:(ARG:0), 10, LEFT%：%EXPNAME:(LOCAL:1), 8, LEFT% EXP +{LOCAL:0}
ENDIF

EXP:(ARG:0):(LOCAL:1) += LOCAL:0

CALLFORM GET_NEED_EXP_{LOCAL:1}(ABL:(ARG:0):(LOCAL:1) + 1)
LOCAL:2 = RESULT
IF (RESULT > 0) && (EXP:(ARG:0):(LOCAL:1) >= RESULT)
	ABL:(ARG:0):(LOCAL:1) += 1
	IF FLAG:20 == 0
		PRINTFORML %CALLNAME:(ARG:0)%の%ABLNAME:(LOCAL:1)%がLv{ABL:(ARG:0):(LOCAL:1)}になった！
	ENDIF
ENDIF



