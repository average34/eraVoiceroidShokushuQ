;キャラボーナス
;「戦闘素質＋速度」がリビングバイン（80）以下であるキャラが対象
@EVENTFIRST_CHARABONUS
SELECTCASE NO:1
	;仲間発見系
	CASE 24, 37, 38, 57, 64, 72, 100, 109
		CALL EVENTFIRST_CHARABONUS_FRIEND
	;敵発見系
	CASE 25, 45, 46, 47, 55, 77, 78, 88, 89
		CALL EVENTFIRST_CHARABONUS_ENEMY
	;武器もらえる系
	CASE 5, 39, 41, 79
		CALL EVENTFIRST_CHARABONUS_WEAPON
	;アイテムもらえる系
	CASE 91, 102
		CALL EVENTFIRST_CHARABONUS_ITEM
	;アクセもらえる系
	CASE 7, 12
		CALL EVENTFIRST_CHARABONUS_ACCE
	;仲間が自己位置にスポーンする系
	CASE 4, 15, 48, 49, 50, 56, 99
		CALL EVENTFIRST_CHARABONUS_SPAWN
	;てゐ専用
	CASE 27
		CALL EVENTFIRST_CHARABONUS_TEWI
	;ひがしかぜたにはやなえ専用
	CASE 42
		CALL EVENTFIRST_CHARABONUS_SANAE
ENDSELECT

@EVENTFIRST_CHARABONUS_FRIEND
#DIM LCOUNT
PRINTFORML
PRINTFORML %CALLNAME:1%のカンにより、迷宮内の仲間の位置が分かった！
DRAWLINE
FOR LCOUNT, 1, CHARANUM
	PRINTFORML %CALLNAME:LCOUNT,15,LEFT% 居場所：{CFLAG:LCOUNT:4}
NEXT
DRAWLINE
PRINTFORMW

@EVENTFIRST_CHARABONUS_ENEMY
#DIM LCOUNT
PRINTFORML
PRINTFORML %CALLNAME:1%のカンにより、迷宮内の敵の位置が分かった！
DRAWLINE
FOR LCOUNT, 1, 99
	SIF DA:LCOUNT:4
		PRINTFORML %SAVESTR:LCOUNT,15,LEFT% 居場所：{DA:LCOUNT:4}
NEXT
DRAWLINE
PRINTFORMW


@EVENTFIRST_CHARABONUS_WEAPON
SELECTCASE RAND:100
	CASE 0 TO 29
		LOCAL = 1
	CASE 30 TO 60
		LOCAL = 2
	CASE 61 TO 80
		LOCAL = 3
	CASE 81 TO 90
		LOCAL = 4
	CASEELSE
		LOCAL = 8
ENDSELECT
PRINTFORML
PRINTFORMW 偶然にも、%CALLNAME:1%は%ITEMNAME:LOCAL%を見つけた！
CALL ADD_ITEM(LOCAL,1)

@EVENTFIRST_CHARABONUS_ITEM
SELECTCASE RAND:100
	CASE 0 TO 29
		LOCAL = 51
	CASE 30 TO 60
		LOCAL = 53
	CASE 61 TO 80
		LOCAL = 54
	CASE 81 TO 90
		LOCAL = 58
	CASEELSE
		LOCAL = 57
ENDSELECT
PRINTFORML
PRINTFORMW 偶然にも、%CALLNAME:1%は%ITEMNAME:LOCAL%を見つけた！
CALL ADD_ITEM(LOCAL,1)

@EVENTFIRST_CHARABONUS_ACCE
SELECTCASE RAND:100
	CASE 0 TO 29
		LOCAL = 21
	CASE 30 TO 60
		LOCAL = 25
	CASE 61 TO 80
		LOCAL = 29
	CASE 81 TO 90
		LOCAL = 35
	CASEELSE
		LOCAL = 39
ENDSELECT
PRINTFORML
PRINTFORMW 偶然にも、%CALLNAME:1%は%ITEMNAME:LOCAL%を見つけた！
CALL ADD_ITEM(LOCAL,1)

@EVENTFIRST_CHARABONUS_SPAWN
;LSET	主人公と一緒の位置に迷いこむキャラのID
;LCOUNT	ループ用
;LRAND	主人公と一緒の位置に迷いこむキャラのID判定用
#DIM LSET
#DIM LCOUNT
#DIM LRAND, 2
LRAND:1 = 0
LSET = 0
FOR LCOUNT, 2, CHARANUM
	LRAND:0 = RAND:100
	IF LRAND:0 >= LRAND:1
		LSET = LCOUNT
		LRAND:1 = LRAND:0
	ENDIF
NEXT
IF LSET
	CFLAG:LSET:4 = CFLAG:1:4
	PRINTFORML
	PRINTFORMW 偶然にも、%CALLNAME:LSET%も%CALLNAME:1%と同じ位置に迷い込んだようだ！
ENDIF

@EVENTFIRST_CHARABONUS_TEWI
#DIM LCOUNT
FOR LCOUNT, 0, 5
	SELECTCASE RAND:100
		CASE 0 TO 29
			LOCAL = 21
		CASE 30 TO 60
			LOCAL = 25
		CASE 61 TO 80
			LOCAL = 29
		CASE 81 TO 90
			LOCAL = 35
		CASEELSE
			LOCAL = 39
	ENDSELECT
	PRINTFORML
	PRINTFORMW 偶然にも、%CALLNAME:1%は%ITEMNAME:LOCAL%を見つけた！
	CALL ADD_ITEM(LOCAL,1)
NEXT


@EVENTFIRST_CHARABONUS_SANAE
#DIM LCOUNT
DRAWLINE
PRINTFORML 東風谷早苗の許されざるおみくじへようこそ！
PRINTFORML 引いていきますよね？
PRINTFORML [1] - はい
$INPUT_SANAE
INPUT
IF RESULT != 1
	PRINTFORML は？？？？？？？？？？
	GOTO INPUT_SANAE
ENDIF

PRINTFORML たいへんよろしい！
PRINTFORMW ではいきますよ……
PRINTFORMW
SELECTCASE RAND:100
	CASE 0 TO 29
		PRINTFORML アイテムがでてきた！
		CALL EVENTFIRST_CHARABONUS_ITEM
	CASE 30 TO 60
		PRINTFORML なんと、仲間の位置が分かった！
		CALL EVENTFIRST_CHARABONUS_FRIEND
	CASE 61 TO 80
		PRINTFORML なんと、敵の位置が分かった！
		CALL EVENTFIRST_CHARABONUS_ENEMY
	CASE 81 TO 90
		PRINTFORML *おおっと*
		PRINTFORMW 大凶だ……！
		CALL CREATE_ENEMY(7, CFLAG:1:4)
		CALL CREATE_ENEMY(9, CFLAG:1:4)
		CALL CREATE_ENEMY(7, CFLAG:1:4)
	CASEELSE
		PRINTFORMW 『ウッサーマウッサーーマ！！ギャアアアアア！！！！！』
		CALL EVENTFIRST_CHARABONUS_SANAE_STATUS
		PRINTFORMW 『おっと、今日はここまでだぴょん、ケケケ』
		PRINTFORMW うさくまちゃんは風のように去って行った....さようならまた会う日まで
ENDSELECT

@EVENTFIRST_CHARABONUS_SANAE_STATUS
SELECTCASE RAND:100
	CASE 0 TO 35
		PRINTFORML なんと、早苗の戦闘素質が５上がった！
		ABL:1:98 += 5
	CASE 36 TO 70
		PRINTFORML なんと、早苗の速度が５上がった！
		ABL:1:97 += 5
	CASEELSE
		PRINTFORML なんと、早苗の戦闘素質と速度が半減した！
		ABL:1:97 /= 2
		ABL:1:98 /= 2
ENDSELECT