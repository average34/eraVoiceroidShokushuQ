;各種初期化処理

;全キャラの位置などを初期化
@CHARA_INITIALIZE
#DIM LCOUNT

	;キャラ初期設定
	FOR LCOUNT, 1, CHARANUM
		CALL CHARA_INITIALIZE_SINGLE(LCOUNT)
	NEXT

	;主人公の位置を初期位置とする
	FLAG:5 = CFLAG:1:4

	;初期部屋の侵入経験をありにする
	FLAG:(600 + FLAG:5) = 1

	;初期の浄化力を設定
	FLAG:6 = 100

@CHARA_INITIALIZE_SINGLE(CHARA_ID)
#DIM CHARA_ID
	
	;TP設定
	CALL GET_BASIC_TP(CHARA_ID)
	BASE:CHARA_ID:4 = RESULT
	MAXBASE:CHARA_ID:4 = 200

	;NP設定
	BASE:CHARA_ID:6 = 0
	MAXBASE:CHARA_ID:6 = 100
	
	;補正済み戦闘素質計算
	CFLAG:CHARA_ID:1 = GET_BATTLE_POW(CHARA_ID)
	
	;初期位置決定
	CFLAG:CHARA_ID:4 = GET_CENTER_ROOM()
	
	;主人公との相性を初期有効度に設定
	;CSVで未設定の場合初期値は100とする
	CFLAG:CHARA_ID:5 = RELATION:CHARA_ID:(NO:1)
	SIF CFLAG:CHARA_ID:5 == 0
		CFLAG:CHARA_ID:5 = 100
		
	;難易度追加パッチ EXTRAモード補正 初期メンバーにのみ適用
	IF FLAG:999 && DAY == 1 && TIME == 0
		;ステータス補正
		BASE:CHARA_ID:0 = MAXBASE:CHARA_ID:0 / 2
		BASE:CHARA_ID:0 -= RAND:25 + RAND:12 + RAND:6 + RAND:3 + RAND:2
		MAXBASE:CHARA_ID:0 -= 50 + RAND:25 + RAND:12 + RAND:6 + RAND:3 + RAND:2
		BASE:CHARA_ID:1 -= 200 + RAND:100 + RAND:50 + RAND:25 + RAND:12 + RAND:6 + RAND:3 + RAND:2
		BASE:CHARA_ID:2 = MAXBASE:CHARA_ID:2 / 2
		BASE:CHARA_ID:2 -= RAND:25 + RAND:12 + RAND:6 + RAND:3 + RAND:2
		BASE:CHARA_ID:3 = MAXBASE:CHARA_ID:3 / 2
		BASE:CHARA_ID:3 -= RAND:25 + RAND:12 + RAND:6 + RAND:3 + RAND:2
		BASE:CHARA_ID:5 += 30 + RAND:6 + RAND:3 + RAND:2
		
		;レベル補正 感覚MAX、触手中毒2
		ABL:CHARA_ID:6 = 3
		ABL:CHARA_ID:7 = 3
		ABL:CHARA_ID:8 = 3
		ABL:CHARA_ID:9 = 3
		ABL:CHARA_ID:11 = 2
		
		;主人公専用処理
		IF CHARA_ID == 1
			;探索技能・気配察知・話術+2、危機回避+1
			ABL:CHARA_ID:0 += 2
			ABL:CHARA_ID:1 += 2
			ABL:CHARA_ID:2 += 2
			ABL:CHARA_ID:5 += 1
			
			;状態異常付与
			IF RAND:3 == 0
				CFLAG:CHARA_ID:28 = 1
			ELSEIF RAND:2 == 0
				CALL PARASITE_TYPE(CHARA_ID, 0, 5, 4 + RAND:5 + RAND:5 + RAND:3, 0)
			ELSE
				CALL PARASITE_TYPE(CHARA_ID, 1, 5, 8 + LIMIT(RAND:4 + RAND:4 + RAND:4 + RAND:4, 1, 10), 2)
			ENDIF
			
		;PT用処理
		ELSE
			;探索技能+1
			ABL:CHARA_ID:0 += 1
			
			;一部の味方が捕まっている（拘束具で囚われている）
			IF (FLAG:999 == 2 && RAND:3 < 2) || (FLAG:999 == 1 && RAND:2 == 0)
				CFLAG:CHARA_ID:504 = 1
				;拘束はさすがにHPとMP満タンじゃないと死ねる
				BASE:CHARA_ID:0 = MAXBASE:CHARA_ID:0
				BASE:CHARA_ID:1 = MAXBASE:CHARA_ID:1
			ENDIF

			;状態異常かHP0
			IF RAND:4 == 0
				CFLAG:CHARA_ID:28 = 1
			ELSEIF RAND:3 == 0
				CALL PARASITE_TYPE(CHARA_ID, 0, 5, 4 + RAND:5 + RAND:5 + RAND:3, 0)
			ELSEIF RAND:2 == 0
				CALL PARASITE_TYPE(CHARA_ID, 0, 5, 8 + LIMIT(RAND:4 + RAND:4 + RAND:4 + RAND:4, 1, 10), 2)
			ELSE
				BASE:CHARA_ID:0 = 0
			ENDIF
		ENDIF
	ENDIF
	
	;ランダム発情パッチ 最終発情日 初期化
	CALL RANDOM_ESTRUS_INITIALIZE(CHARA_ID)

	;入れ替わりパッチ 体／世代関連最初期情報設定
	CFLAG:(CHARA_ID):110 = NO:(CHARA_ID)
	CFLAG:(CHARA_ID):111 = 0
	CFLAG:(CHARA_ID):114 = NO:(CHARA_ID)
	CSTR:(CHARA_ID):1 = %NAME:(CHARA_ID)%
	CSTR:(CHARA_ID):2 = %CALLNAME:(CHARA_ID)%
	
	;同一キャラへのランダム能力化 触手宮のモブを流用
	IF EXIST_CHARA(NO:CHARA_ID)
		IF (FLAG:537 == 2 && CHARA_ID != 1) || FLAG:537 == 3
			CALL MOB_RESET(CHARA_ID)
			CALL MOB_CUSTOMIZE(CHARA_ID)
		ENDIF
	ENDIF

	;キャラの初期レベルと経験値が見合っていない部分を補正
	CALL CHARA_EXP_INIT_SINGLE(CHARA_ID)
	
	; キャラ口上設定をグローバルセーブに合わせて設定しておく
	CALL INIT_KOJO_DEFAULT_COLOR_SINGLE(CHARA_ID)

;コンフィグ適用後の処理
@CHARA_INITIALIZE_2
;爆乳化
IF FLAG:545 == 1 && (FLAG:521 == 0 || FLAG:521 == 3)
	FOR LOCAL:1, 1, CHARANUM
		IF FLAG:521 == 0 || (FLAG:521 == 3 && TALENT:(LOCAL:1):122 == 0)
			TALENT:(LOCAL:1):107 = 0
			TALENT:(LOCAL:1):108 = 1
			TALENT:(LOCAL:1):109 = 0
			TALENT:(LOCAL:1):110 = 0
		ENDIF
	NEXT
ELSEIF FLAG:545 == 2 && (FLAG:521 == 0 || FLAG:521 == 3)
	FOR LOCAL:1, 1, CHARANUM
		IF FLAG:521 == 0 || (FLAG:521 == 3 && TALENT:(LOCAL:1):122 == 0)
			TALENT:(LOCAL:1):107 = 0
			TALENT:(LOCAL:1):108 = 1
			TALENT:(LOCAL:1):109 = 0
			TALENT:(LOCAL:1):110 = 0
			TALENT:(LOCAL:1):130 = 1
		ENDIF
	NEXT
ENDIF

;ふたなり化
IF FLAG:546 == 1 && FLAG:520 > 0
	IF FLAG:520 == 1
		FOR LOCAL:1, 1, CHARANUM
			CFLAG:(LOCAL:1):42 += 100
		NEXT
	ELSEIF FLAG:520 == 2
		CFLAG:1:42 += 100
	ELSEIF FLAG:520 == 3
		FOR LOCAL:1, 2, CHARANUM
			CFLAG:(LOCAL:1):42 += 100
		NEXT
	ENDIF
ELSEIF FLAG:546 == 2 && FLAG:520 > 0
	IF FLAG:520 == 1
		FOR LOCAL:1, 1, CHARANUM
			TALENT:(LOCAL:1):121 = 1
		NEXT
	ELSEIF FLAG:520 == 2
		TALENT:1:121 = 1
	ELSEIF FLAG:520 == 3
		FOR LOCAL:1, 2, CHARANUM
			TALENT:(LOCAL:1):121 = 1
		NEXT
	
	ENDIF
ELSEIF FLAG:546 == 3
	FOR LOCAL:1, 2, CHARANUM
		IF RAND:2 == 0
			TALENT:(LOCAL:1):121 = 1
		ENDIF
	NEXT
ENDIF
;全部位過敏
IF FLAG:547 == 1
	FOR LOCAL:1, 1, CHARANUM
		TALENT:(LOCAL:1):101 = 3
		TALENT:(LOCAL:1):102 = 3
		TALENT:(LOCAL:1):103 = 3
		TALENT:(LOCAL:1):104 = 3
		TALENT:(LOCAL:1):105 = 3
		TALENT:(LOCAL:1):106 = 3
	NEXT
ENDIF
;小柄体型
IF FLAG:548 == 1
	FOR LOCAL:1, 1, CHARANUM
		TALENT:(LOCAL:1):100 = 1
	NEXT
ENDIF
;ふたなり細分化素質
IF FLAG:520 > 0 && FLAG:997 != 81
	CALL FUTANARI_CUSTOMIZE
ENDIF
;陥没乳首入手処理
IF FLAG:531 == 1
	FOR LOCAL:1, 1, CHARANUM
		TALENT:(LOCAL:1):159 = 1
	NEXT
ELSEIF FLAG:531 == 2
	FOR LOCAL:1, 1, CHARANUM
		SIF RAND:2 == 0
			TALENT:(LOCAL:1):159 = 1
	NEXT
ENDIF
;排卵日処理
FOR LOCAL:1, 1, CHARANUM
	CFLAG:(LOCAL:1):150 = RAND:30
NEXT
;ふたなりカスタマイズ
FOR LOCAL:0, 1, CHARANUM
	IF FLAG:590 == 2
		CALL FUTANARI_RANDOM_GET_2(LOCAL:0)
	ELSEIF FLAG:590 == 1
		
	ELSE
		CALL FUTANARI_CUSTOMIZE_GET_2(LOCAL:0)
	ENDIF
NEXT

;MAP関係初期化
@MAP_INITIALIZE
;LOCAL
;0	LOOP

;イベントを配置

;ナイフ入手は必ず主人公の初期配置と同じ場所
FLAG:100 = FLAG:5

FOR LOCAL:0, 101, 300
	FLAG:(LOCAL:0) = RAND:30 + 1
NEXT

;重要な回復ポイントや中毒をわざと増やすのに必要なイベントは四隅を避ける
FLAG:133 = GET_CENTER_ROOM()
FLAG:139 = GET_CENTER_ROOM()
FLAG:160 = GET_CENTER_ROOM()
FLAG:177 = GET_CENTER_ROOM()
FLAG:178 = GET_CENTER_ROOM()

;一部の固定イベントは他のイベントの位置に影響を受ける
DO
	FLAG:134 = GET_CENTER_ROOM()
LOOP (FLAG:134 == (FLAG:133 % 100))
DO
	FLAG:135 = RAND:30 + 1
LOOP (FLAG:135 == (FLAG:133 % 100) || FLAG:135 == (FLAG:134 % 100))

DO
	FLAG:202 = RAND:30 + 1
LOOP (FLAG:202 == (FLAG:201 % 100))
DO
	FLAG:203 = RAND:30 + 1
LOOP (FLAG:203 == (FLAG:201 % 100) || FLAG:203 == (FLAG:202 % 100))
DO
	FLAG:204 = RAND:30 + 1
LOOP (FLAG:204 == (FLAG:201 % 100) || FLAG:204 == (FLAG:202 % 100) || FLAG:204 == (FLAG:203 % 100))
DO
	FLAG:205 = RAND:30 + 1
LOOP (FLAG:205 == (FLAG:201 % 100) || FLAG:205 == (FLAG:202 % 100) || FLAG:205 == (FLAG:203 % 100) || FLAG:205 == (FLAG:204 % 100))
DO
	FLAG:207 = RAND:30 + 1
LOOP (FLAG:207 == (FLAG:201 % 100) || FLAG:207 == (FLAG:202 % 100) || FLAG:207 == (FLAG:203 % 100) || FLAG:207 == (FLAG:204 % 100) || FLAG:207 == (FLAG:205 % 100))

;遺品イベントは初期配置しない
FOR LOCAL:0, 190, 201
	FLAG:(LOCAL:0) = 0
NEXT

;エンディングで入れ直すのでリセット
FLAG:213 = 0
FLAG:214 = 0

PRINTFORML マップはどちらを使いますか？
PRINTFORML [1] 半固定マップ
PRINTFORML [2] ランダムマップ
$INPUT_LOOP
INPUT
IF RESULT != 1 && RESULT != 2
	GOTO INPUT_LOOP
ELSEIF RESULT == 1
	PRINTFORMW 触手宮オリジナルのものを使用します
	CALL INITIALIZE_SET_ROOT_ORIGINAL
ELSE
	PRINTFORMW ランダムマップを使用します
	CALL INITIALIZE_SET_ROOT_RANDOM
ENDIF

;全部屋の初期汚染度を10に設定
FOR LOCAL:0, 51, 81
	FLAG:(LOCAL:0) = 10
NEXT



;敵関係初期化
@ENEMY_INITIALIZE
;LOCAL
;0	LOOP
;1	ラスボス位置
;2	中ボス位置
;3	TEMP

;DA初期化
VARSET DA:0:0

;★EX系にクイーン、ガーディアン、バイオプラントはいない

IF FLAG:997 == 0 && FLAG:999 == 0

	;ラスボスとお供を４隅のどこかに配置
	LOCAL:1 = RAND:4
	IF LOCAL:1 == 0
		LOCAL:1 = 1
	ELSEIF LOCAL:1 == 1
		LOCAL:1 = 5
	ELSEIF LOCAL:1 == 2
		LOCAL:1 = 26
	ELSE
		LOCAL:1 = 30
	ENDIF

	;中ボス配置
	LOCAL:2 = RAND:3
	IF LOCAL:2 == 0
		LOCAL:2 = 1
	ELSEIF LOCAL:2 == 1
		LOCAL:2 = 5
	ELSE
		LOCAL:2 = 26
	ENDIF
	;ラスボスと位置が被っていたら30に移動　（要するにラスボス以外の４隅から３択となる）
	IF LOCAL:2 == LOCAL:1
		LOCAL:2 = 30
	ENDIF
ENDIF

;敵キャラデータ作成
;!!!先頭はダミーデータになるので、適当なキャラを入れておくこと

CALL CREATE_ENEMY(99, 0, FLAG:5)

IF FLAG:997 == 0 && FLAG:999 == 0
	IF FLAG:4 >= 0
		CALL CREATE_ENEMY(1, LOCAL:1, FLAG:5)
		CALL CREATE_ENEMY(2, LOCAL:1, FLAG:5)
		CALL CREATE_ENEMY(2, LOCAL:1, FLAG:5)
	ENDIF
	CALL CREATE_ENEMY(3, LOCAL:2, FLAG:5)

	IF FLAG:998 == 0
		;バイオプラントは移動しないので、ボスと被らないように、4隅を避ける
		CALL CREATE_ENEMY(4, GET_CENTER_ROOM(), FLAG:5)
		CALL CREATE_ENEMY(4, GET_CENTER_ROOM(), FLAG:5)
	ENDIF
ENDIF

;以下、雑魚キャラ追加
;★EXモードだと数は減るが、上級モンスターやヒュプノが初期状態から闊歩する
IF FLAG:999
	CALL CREATE_ENEMY(6, 0, FLAG:5)
	CALL CREATE_ENEMY(8, 0, FLAG:5)
	CALL CREATE_ENEMY(45, 0, FLAG:5)
	CALL CREATE_ENEMY(10, 0, FLAG:5)
	CALL CREATE_ENEMY(12, 0, FLAG:5)
	CALL CREATE_ENEMY(14, 0, FLAG:5)
	CALL CREATE_ENEMY(15, 0, FLAG:5)
	CALL CREATE_ENEMY(16, 0, FLAG:5)
	CALL CREATE_ENEMY(17, 0, FLAG:5)
	CALL CREATE_ENEMY(98, 0, FLAG:5)
;★ここまで
ELSEIF FLAG:998
	FOR LOCAL:0, 0, 13
		CALL CREATE_ENEMY(FLAG:998, 0, FLAG:5)
	NEXT
;トラップモードはシェイプシフターも最初からの敵もなし
ELSEIF FLAG:997 == 0
;パッチにより大量に敵が追加+種類により湧きの偏りのコンフィグが可能なため初期配置はシェイプシフターのみで残りはREPOPで処理
	CALL CREATE_ENEMY(17, 0, FLAG:5)
ENDIF



IF FLAG:4 >= 2
	;HARD以上ならドッペルを初期配置
	CALL CREATE_ENEMY(18, 0, FLAG:5)
ENDIF


