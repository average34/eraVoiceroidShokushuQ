;寄生・妊娠・A寄生・B寄生・C寄生処理関数
;産み付けられた時の処理で、成長や出産はEND_PHASE
;対応するCFLAGに数値を入れるだけの関数

@PARASITE_TYPE(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6)
;ARG
;0	対象キャラID
;1	0:寄生 1:妊娠 2:A寄生 3:B寄生 4:仮の子ども（妊娠をした場合に生まれるキャラ決定） 5:C寄生 6:C妊娠
;2	孕ませた敵番号
;3	タイムリミット
;4	妊娠の状態	0：普通の寄生　1：流産する妊娠　2：妊娠・判明前　3：妊娠・判明後
;5	ふたなり竿親に妊娠させられた場合の竿親ID
;6	デバッグスイッチ

;すでに状態異常中ならスルー

;妊娠寄生フィルターありだと媚薬爆弾に差し替え
IF (FLAG:515 == 1 || (FLAG:515 == 2 && RAND:10 < 3))
	IF CFLAG:(ARG:0):158 == 0
		CFLAG:(ARG:0):158 = ARG:3
	ENDIF
	
	;変数をリセットする しないとバグの元
	IF CFLAG:(ARG:0):22 == 0
		CFLAG:(ARG:0):23 = 0
		CFLAG:(ARG:0):36 = 0
	ENDIF
	IF CFLAG:(ARG:0):152 == 0
		CFLAG:(ARG:0):151 = 0
	ENDIF
	IF CFLAG:(ARG:0):162 == 0
		CFLAG:(ARG:0):161 = 0
	ENDIF
	IF CFLAG:(ARG:0):172 == 0
		CFLAG:(ARG:0):171 = 0
	ENDIF
	IF CFLAG:(ARG:0):174 == 0
		CFLAG:(ARG:0):173 = 0
	ENDIF
	
	;ランダム寄生の処理はしない
	RETURN
;寄生
ELSEIF ARG:1 == 0 && CFLAG:(ARG:0):22 == 0
	CFLAG:(ARG:0):22 = ARG:2
	CFLAG:(ARG:0):23 = ARG:3
;妊娠
ELSEIF ARG:1 == 1 && CFLAG:(ARG:0):22 == 0
	CFLAG:(ARG:0):22 = ARG:2
	CFLAG:(ARG:0):23 = ARG:3
	CFLAG:(ARG:0):36 = ARG:4
	IF ARG:5
		CFLAG:(ARG:0):109 = ARG:5
	ENDIF
;A寄生
ELSEIF ARG:1 == 2 && CFLAG:(ARG:0):151 == 0
	CFLAG:(ARG:0):151 = ARG:2
	CFLAG:(ARG:0):152 = ARG:3
;B寄生
ELSEIF ARG:1 == 3 && CFLAG:(ARG:0):161 == 0 && FLAG:516 == 1 && (TALENT:(ARG:0):162 >= 1 || ARG:6 == 1)
	CFLAG:(ARG:0):161 = ARG:2
	CFLAG:(ARG:0):162 = ARG:3
;C寄生
ELSEIF ARG:1 == 5 && CFLAG:(ARG:0):171 == 0 && FLAG:518 == 1 && (GET_PENIS(ARG:0) || ARG:6 == 1)
	CFLAG:(ARG:0):171 = ARG:2
	CFLAG:(ARG:0):172 = ARG:3
;C妊娠
ELSEIF ARG:1 == 6 && FLAG:518 == 1 && (GET_PENIS(ARG:0) || ARG:6 == 1)
	CFLAG:(ARG:0):173 = ARG:2
	CFLAG:(ARG:0):174 = ARG:3
ENDIF


;ボスなどのランダム寄生を差し替え
;不意打ちやREPOPから流用
;LOCAL
;4	孕む敵の番号
;5	ループ回数
IF TFLAG:93
	$REPOP_LOOP
	LOCAL:4 = 0
	LOCAL:5 += 1
	;無限ループを避けるため200回まで
	IF LOCAL:5 < 200
		;自然湧きする敵の範囲内でLOCAL:4を決める
		LOCAL:4 = 5 + RAND:91
		TRYCCALLFORM SET_ENEMY_DATA_{LOCAL:4}(FLAG:1)
		CATCH
			GOTO REPOP_LOOP
		ENDCATCH

		;娘とドッペルは処理が特殊なのでパスする
		SIF LOCAL:4 == 18 || LOCAL:4 == 19
			GOTO REPOP_LOOP
		;コンフィグ設定している場合は敵の系統で湧き制御
		;その系統の敵を出す・その系統の敵を弾く・一度会った敵でフィルターを掛けたものを弾く
		IF FLAG:527 > 0 && (DA:(FLAG:1):85 & FLAG:527) == 0
			GOTO REPOP_LOOP
		ELSEIF FLAG:536 > 0 && (DA:(FLAG:1):85 & FLAG:536) && FLAG:536 != 63
			GOTO REPOP_LOOP
		ELSEIF (FLAG:512 & 1) == 0 && (DA:(FLAG:1):85 & 128)
			GOTO REPOP_LOOP
		ENDIF
	ENDIF
	;フラグリセット
	FOR LOCAL:0, 0, VARSIZE("DA", 1)
		DA:(FLAG:1):(LOCAL:0) = 0
	NEXT
	;何もでなければバイオプラント
	IF LOCAL:4 == 0
		LOCAL:4 = 4
	ENDIF
	
	IF ARG:1 == 0 || ARG:1 == 1
		CFLAG:(ARG:0):22 = LOCAL:4
	ELSEIF ARG:1 == 2
		CFLAG:(ARG:0):151 = LOCAL:4
	ELSEIF ARG:1 == 3 && (TALENT:(ARG:0):162 >= 1 || ARG:6 == 1)
		CFLAG:(ARG:0):161 = LOCAL:4
	ELSEIF ARG:1 == 4
		CFLAG:(ARG:0):57 = LOCAL:4
	ELSEIF ARG:1 == 5 && (GET_PENIS(ARG:0) || ARG:6 == 1)
		CFLAG:(ARG:0):171 = LOCAL:4
	ELSEIF ARG:1 == 6 && (GET_PENIS(ARG:0) || ARG:6 == 1)
		CFLAG:(ARG:0):173 = LOCAL:4
	ENDIF
ENDIF

