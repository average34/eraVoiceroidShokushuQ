;自然回復処理
@AUTO_HEAL_PT(CHARA_ID)
#DIM CHARA_ID

	SIF CHARA_ID > CHARANUM - 1
		RETURN

	;リタイア・復活中・捕食中・戦意喪失中は自然回復しない（拘束具使用中は回復）
	SIF CFLAG:CHARA_ID:2 || CFLAG:CHARA_ID:3 || (CFLAG:CHARA_ID:10 && CFLAG:CHARA_ID:504 == 0 ) || ( CFLAG:CHARA_ID:29 && CFLAG:CHARA_ID:504 == 0)
		RETURN

	;好感度変動
	SIF CHECK_MEMBER_FROM_ID(CHARA_ID)
		CALL ADD_FEELING(CHARA_ID, 0)

	;HP回復
	CALL AUTO_HEAL_PT_HP(CHARA_ID)
	
	;MP回復
	CALL AUTO_HEAL_PT_MP(CHARA_ID)

	;EP回復
	CALL AUTO_HEAL_PT_EP(CHARA_ID)

	;DP変動
	CALL AUTO_HEAL_PT_DP(CHARA_ID)

	;NP変動
	CALL AUTO_HEAL_PT_NP(CHARA_ID)
	
	;CP変動
	CALL AUTO_HEAL_PT_CP(CHARA_ID)
	
	;勃起度変動
	CALL AUTO_HEAL_PT_ERECTION(CHARA_ID)
	
RETURN


;自然回復処理 HP
@AUTO_HEAL_PT_HP(CHARA_ID)
#DIM CHARA_ID
#DIM RECOVERY_VALUE

	;毎ターン最大HPの1/16回復が基本
	RECOVERY_VALUE = MAXBASE:CHARA_ID:HP / 16

	;素質補正
	;主に回復系素質
	;現在時間によって回復量が変化する種族や特性も
	SIF TALENT:CHARA_ID:111
		TIMES RECOVERY_VALUE , 1.5
	SIF TALENT:CHARA_ID:112
		TIMES RECOVERY_VALUE , 0.8
	IF TALENT:CHARA_ID:114
		IF TIME:0 == 0 || TIME:0 == 1
			TIMES RECOVERY_VALUE , 0.5
		ELSE
			TIMES RECOVERY_VALUE , 3.0
		ENDIF
	ENDIF
	IF TALENT:CHARA_ID:115
		SIF TIME:0 == 0 || TIME:0 == 1
			TIMES RECOVERY_VALUE , 2.0
	ENDIF
	IF TALENT:CHARA_ID:116
		SIF TIME:0 == 2 || TIME:0 == 3
			TIMES RECOVERY_VALUE , 2.0
	ENDIF

	;汚染度補正
	SELECTCASE FLAG:(CFLAG:CHARA_ID:4 + 50)
	CASE IS < 10
		TIMES RECOVERY_VALUE , 2.0
	CASE IS < 40
		TIMES RECOVERY_VALUE , 1.0
	CASE IS < 70
		TIMES RECOVERY_VALUE , 0.9
	CASE IS < 100
		TIMES RECOVERY_VALUE , 0.7
	CASEELSE
		TIMES RECOVERY_VALUE , 0.4
	ENDSELECT
	
	;難易度倍率
	RECOVERY_VALUE = RECOVERY_VALUE * AUTO_HEAL_PT_DIFFICULTY_RECOVERY_RATE() / 100

	;NPCは回復速度に補正
	SIF CHECK_MEMBER_FROM_ID(CHARA_ID) == 0
		RECOVERY_VALUE = RECOVERY_VALUE * AUTO_HEAL_PT_DIFFICULTY_RECOVERY_RATE_NPC() / 100

	BASE:CHARA_ID:HP = LIMIT(BASE:CHARA_ID:HP + RECOVERY_VALUE, 0, MAXBASE:CHARA_ID:HP)

	;PTに居ないキャラはHPが減りすぎると一定確率でお茶を飲む
	IF IN_PT(CHARA_ID) == 0
		IF BASE:CHARA_ID:HP * 100 / MAXBASE:CHARA_ID:HP <= 20
			SIF RAND:(5 + FLAG:4) == 0
				BASE:CHARA_ID:HP += MAXBASE:CHARA_ID:HP / 2
		ENDIF
	ENDIF
RETURN


;自然回復処理 MP
@AUTO_HEAL_PT_MP(CHARA_ID)
#DIM CHARA_ID
#DIM RECOVERY_VALUE
	
	;拘束中、喪失中のキャラは回復しない
	SIF CFLAG:CHARA_ID:10 != 0 || CFLAG:CHARA_ID:29
		RETURN
		
	;毎ターン最大MPの1/50回復が基本
	RECOVERY_VALUE = MAXBASE:CHARA_ID:MP / 50

	;素質補正
	;主に回復系素質
	;現在時間によって回復量が変化する種族や特性も
	SIF TALENT:CHARA_ID:111
		TIMES RECOVERY_VALUE , 1.5
	SIF TALENT:CHARA_ID:112
		TIMES RECOVERY_VALUE , 0.8
	IF TALENT:CHARA_ID:114
		IF TIME:0 == 0 || TIME:0 == 1
			TIMES RECOVERY_VALUE , 0.5
		ELSE
			TIMES RECOVERY_VALUE , 3.0
		ENDIF
	ENDIF
	IF TALENT:CHARA_ID:115
		SIF TIME:0 == 0 || TIME:0 == 1
			TIMES RECOVERY_VALUE , 2.0
	ENDIF
	IF TALENT:CHARA_ID:116
		SIF TIME:0 == 2 || TIME:0 == 3
			TIMES RECOVERY_VALUE , 2.0
	ENDIF

	;汚染度補正
	SELECTCASE FLAG:(CFLAG:CHARA_ID:4 + 50)
	CASE IS < 10
		TIMES RECOVERY_VALUE , 2.0
	CASE IS < 40
		TIMES RECOVERY_VALUE , 1.0
	CASE IS < 70
		TIMES RECOVERY_VALUE , 0.9
	CASE IS < 100
		TIMES RECOVERY_VALUE , 0.7
	CASEELSE
		TIMES RECOVERY_VALUE , 0.4
	ENDSELECT

	;難易度倍率
	RECOVERY_VALUE = RECOVERY_VALUE * AUTO_HEAL_PT_DIFFICULTY_RECOVERY_RATE() / 100

	;NPCは回復速度に補正
	SIF CHECK_MEMBER_FROM_ID(CHARA_ID) == 0
		RECOVERY_VALUE = RECOVERY_VALUE * AUTO_HEAL_PT_DIFFICULTY_RECOVERY_RATE_NPC() / 100

	BASE:CHARA_ID:MP = LIMIT(BASE:CHARA_ID:MP + RECOVERY_VALUE, 0, MAXBASE:CHARA_ID:MP)
	
	;PTに居ないキャラはMPが減りすぎると一定確率でドリンクを飲む
	IF IN_PT(CHARA_ID) == 0
		IF BASE:CHARA_ID:MP * 100 / MAXBASE:CHARA_ID:MP <= 15
			SIF RAND:(8 + FLAG:4) == 0
				BASE:CHARA_ID:MP += MAXBASE:CHARA_ID:MP / 4
		ENDIF
	ENDIF
RETURN


;自然回復処理 EP
@AUTO_HEAL_PT_EP(CHARA_ID)
#DIM CHARA_ID
#DIM RECOVERY_VALUE

	;毎ターン最大EPの1/20回復が基本
	RECOVERY_VALUE = MAXBASE:CHARA_ID:EP / 20
	
	;汚染度補正
	SELECTCASE FLAG:(CFLAG:CHARA_ID:4 + 50)
	CASE IS < 10
		TIMES RECOVERY_VALUE , 2.0
	CASE IS < 40
		TIMES RECOVERY_VALUE , 1.0
	CASE IS < 70
		TIMES RECOVERY_VALUE , 0.9
	CASE IS < 100
		TIMES RECOVERY_VALUE , 0.7
	CASEELSE
		TIMES RECOVERY_VALUE , 0.4
	ENDSELECT

	BASE:CHARA_ID:EP = LIMIT(BASE:CHARA_ID:EP + RECOVERY_VALUE, 0, MAXBASE:CHARA_ID:EP)
RETURN


;自然回復処理 DP
@AUTO_HEAL_PT_DP(CHARA_ID)
#DIM CHARA_ID

	;汚染度に応じて変動
	SELECTCASE FLAG:(CFLAG:CHARA_ID:4 + 50)
	CASE IS < 10
		BASE:CHARA_ID:DP = LIMIT(BASE:CHARA_ID:DP - 5, 0, 150)
	CASE IS < 40
		BASE:CHARA_ID:DP = LIMIT(BASE:CHARA_ID:DP - 1, 0, 150)
	CASE IS < 70
		BASE:CHARA_ID:DP = LIMIT(BASE:CHARA_ID:DP + 2, 0, 150)
	CASE IS < 100
		BASE:CHARA_ID:DP = LIMIT(BASE:CHARA_ID:DP + 5, 0, 150)
	CASEELSE
		BASE:CHARA_ID:DP = LIMIT(BASE:CHARA_ID:DP + 10, 0, 150)
	ENDSELECT
	
	;PTメンバー以外は汚れが50までに抑えられる
	SIF CHECK_MEMBER_FROM_ID(CHARA_ID) == 0
			BASE:CHARA_ID:DP = LIMIT(BASE:CHARA_ID:DP, 0, 50)
			
RETURN

;自然回復処理 NP
@AUTO_HEAL_PT_NP(CHARA_ID)
#DIM CHARA_ID
	
	;汚染度に応じて変動
	SELECTCASE FLAG:(CFLAG:CHARA_ID:4 + 50)
	CASE IS < 10
		BASE:CHARA_ID:NP = LIMIT(BASE:CHARA_ID:NP - 3, 0, 100)
	CASE IS < 40
		BASE:CHARA_ID:NP = LIMIT(BASE:CHARA_ID:NP - 1, 0, 100)
	CASE IS < 70
		BASE:CHARA_ID:NP = LIMIT(BASE:CHARA_ID:NP + 0, 0, 100)
	CASE IS < 100
		BASE:CHARA_ID:NP = LIMIT(BASE:CHARA_ID:NP + 1, 0, 100)
	CASEELSE
		BASE:CHARA_ID:NP = LIMIT(BASE:CHARA_ID:NP + 2, 0, 100)
	ENDSELECT

RETURN

;自然回復処理 CP
@AUTO_HEAL_PT_CP(CHARA_ID)
#DIM CHARA_ID
#DIM RECOVERY_VALUE

	;PTメンバー以外のみを処理する
	SIF IN_PT(CHARA_ID) != 0
		RETURN
		
	;PTに居ないキャラはCP回復行う
	RECOVERY_VALUE = MAXBASE:CHARA_ID:CP * (16 - FLAG:4 * 2) / 100
	
	;全裸になっている場合、ランダムで着替えるか全裸のまま
	IF BASE:CHARA_ID:CP <= 0
		IF RAND:(8 + FLAG:4) == 0
			RECOVERY_VALUE = MAXBASE:CHARA_ID:CP
			CFLAG:CHARA_ID:507 += 2
			CALL CLOTH_TYPE_SELECT_RAND(CHARA_ID)
			CFLAG:CHARA_ID:501 = RESULT
		ELSE
			RECOVERY_VALUE = 0
		ENDIF
	ENDIF
	
	BASE:CHARA_ID:CP = LIMIT(BASE:CHARA_ID:CP + RECOVERY_VALUE, 0, MAXBASE:CHARA_ID:CP)

	;服を着ていると露出癖入手率ダウン
	SIF BASE:CHARA_ID:CP > (MAXBASE:CHARA_ID:CP / 2)
		CFLAG:CHARA_ID:167 -= 1
	
RETURN

;勃起度変動
@AUTO_HEAL_PT_ERECTION(CHARA_ID)
#DIM CHARA_ID
#DIM ERECTION_LEVEL_B
#DIM ERECTION_LEVEL_C
#DIM RECOVERY_BASE_VALUE

	VARSET ERECTION_LEVEL_B
	VARSET ERECTION_LEVEL_C
	
	;TP依存で回復、冷静なほど回復しやすい
	;偏りを避ける為、乱数を重ねてある
	;発情状態では減衰値小さめに
	RECOVERY_BASE_VALUE = (MAXBASE:CHARA_ID:TP - BASE:CHARA_ID:TP) / 20
	IF RECOVERY_BASE_VALUE > 0
		IF !CFLAG:CHARA_ID:28
			ERECTION_LEVEL_B = RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE
			ERECTION_LEVEL_C = RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE
		ELSE
			ERECTION_LEVEL_B = (RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE) / 2
			ERECTION_LEVEL_C = (RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE + RAND:RECOVERY_BASE_VALUE) / 2
		ENDIF
	ENDIF
	
	;勃起度が0超であれば減衰処理
	;発情中なら発情値*10%が最低値として保持される
	;PTに居るメンバーだけ勃起度表示
	IF GET_ERECTION_LEVEL_B(CHARA_ID) > 0 && ERECTION_LEVEL_B > 0
		IF IN_PT(CHARA_ID)
			CALL ERECTION_B(CHARA_ID, -ERECTION_LEVEL_B)
		ELSE
			FLAG:20 = 1
			CALL ERECTION_B(CHARA_ID, -ERECTION_LEVEL_B, 2)
			FLAG:20 = 0
		ENDIF
	ENDIF
	IF GET_ERECTION_LEVEL_C(CHARA_ID) > 0 && ERECTION_LEVEL_C > 0
		IF IN_PT(CHARA_ID)
			CALL ERECTION_C(CHARA_ID, -ERECTION_LEVEL_C)
		ELSE
			FLAG:20 = 1
			CALL ERECTION_C(CHARA_ID, -ERECTION_LEVEL_C, 2)
			FLAG:20 = 0
		ENDIF
	ENDIF
RETURN


;難易度回復倍率
@AUTO_HEAL_PT_DIFFICULTY_RECOVERY_RATE()
#FUNCTION
#DIM CHARA_ID
	;難易度倍率補正
	SELECTCASE FLAG:4
	CASE 0
		RETURNF 150
	CASE 1
		RETURNF 100
	CASE 2
		RETURNF 80
	CASEELSE
		RETURNF 40
	ENDSELECT
RETURNF 0


;難易度回復倍率 NPC用
@AUTO_HEAL_PT_DIFFICULTY_RECOVERY_RATE_NPC()
#FUNCTION
#DIM CHARA_ID
	SELECTCASE FLAG:4
	CASE 0
		RETURNF 140
	CASE 1
		RETURNF 120
	CASE 2
		RETURNF 110
	CASEELSE
		RETURNF 200
	ENDSELECT
RETURNF 0



;自然回復処理(敵)
;ARG:0	対象敵ID
@AUTO_HEAL_ENEMY(ARG:0)

SIF ARG:0 > FLAG:1 - 1
	RETURN

LOCAL:1 = DA:(ARG:0):2 * DA:(ARG:0):19 / 100

;捕食中は更に回復
SIF DA:(ARG:0):10
	LOCAL:1 += DA:(ARG:0):2 * DA:(ARG:0):19 / 100

;汚染度補正
IF FLAG:(DA:(ARG:0):4 + 50) < 10
	TIMES LOCAL:1 , 0.1
ELSEIF FLAG:(DA:(ARG:0):4 + 50) < 40
	TIMES LOCAL:1 , 1.0
ELSEIF FLAG:(DA:(ARG:0):4 + 50) < 70
	TIMES LOCAL:1 , 1.2
ELSEIF FLAG:(DA:(ARG:0):4 + 50) < 100
	TIMES LOCAL:1 , 1.5
ELSE
	TIMES LOCAL:1 , 2.0
ENDIF

DA:(ARG:0):1 += LOCAL:1

SIF DA:(ARG:0):1 > DA:(ARG:0):2
	DA:(ARG:0):1 = DA:(ARG:0):2


IF DA:(ARG:0):0 == 17 && RAND:4 == 0
	;シェイプシフターの場合、ランダムに名前を変更
	SAVESTR:(ARG:0) = %ENEMY_NAME(5 + RAND:12)%
ENDIF
;ドッペルゲンガー変化処理 詳しくは敵詳細データ
IF DA:(ARG:0):0 == 18 && RAND:4 == 0
	;ドッペルゲンガーの場合、ランダムに名前を変更
	IF CHARANUM < 1
		LOCAL:2 = -1
	ELSE
		VARSET LOCAL, 0, 1, MIN(CHARANUM, 11)
		LOCAL:11 = 0
		;11番目以降のキャラには化けない
		FOR LOCAL:0, 1, MIN(CHARANUM, 11)
			SIF LOCAL:0 > 10
				BREAK
			SIF LOCAL:0 == 1 || LOCAL:0 == FLAG:10 || LOCAL:0 == FLAG:11
				CONTINUE
			LOCAL:11 += 1
			LOCAL:(LOCAL:11) = LOCAL:0
		NEXT
		
		IF LOCAL:11 < 1
			LOCAL:2 = -1
		ELSE
			LOCAL:2 = LOCAL:(1 + RAND:(LOCAL:11))
		ENDIF
	ENDIF
	IF LOCAL:2 <= 0
		;化けられるキャラが居ないなら主人公に化ける
		SAVESTR:(ARG:0) = %CALLNAME:1%
		DA:(ARG:0):46 = 1
	ELSE
		SAVESTR:(ARG:0) = %CALLNAME:(LOCAL:2)%
		DA:(ARG:0):46 = (LOCAL:2)
	ENDIF
ENDIF

SIF DA:(ARG:0):10 == 0 && DA:(ARG:0):20 == 0
	DA:(ARG:0):45 = 0

