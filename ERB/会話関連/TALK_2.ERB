;戦意喪失キャラとの会話イベント
;眠らせてもここに飛ぶ
;ARG:0	対象キャラ
@TALK_2(ARG:0)
;LOCAL
;0	LOOP
;1	誘えるかどうか
;2	TEMP
;3	浄化可能かどうか

PRINTFORML 迷宮の中を進んでいくと、

TRYCALL PRINT_SHOW_CLOTH(ARG:0)
SIF CFLAG:(ARG:0):25 > 0
	PRINTFORM 眠っている
PRINTFORM %CALLNAME:(ARG:0)%  (戦闘素質 {ABL:(ARG:0):98, 2} / 速度 {ABL:(ARG:0):97, 2})  と出会った
PRINTFORML 

$TALK_START


;遭遇相手の様子
CALL TALK_LOOK(ARG)

PRINTFORML どうしますか？
PRINTFORML [1] 持っている物を奪う
SIF ITEM:58
	PRINTFORML [2] 気付け薬（残り：{ITEM:58}個）を使ってあげる
SIF ITEM:58 && ITEM:52
	PRINTFORML [3] 気付け薬（残り：{ITEM:58}個）を使って、さらにエナジードリンク（残り：{ITEM:52}個）を渡す。
IF FLAG:11 == 0 && CFLAG:(ARG:0):25 == 0
	;3人目まで埋まってないなら誘える
	LOCAL:1 = 1
	PRINTFORML [4] 仲間に入れる
ELSE
	LOCAL:1 = 0
ENDIF
IF FLAG:11 && CFLAG:1:29 == 0 && CFLAG:(FLAG:10):29 == 0 && CFLAG:(FLAG:11):29 == 0 && FLAG:6 >= 200
	;3人PT＋全員行動可能＋浄化力200以上で、浄化による回復が可能
	LOCAL:3 = 1
	PRINTFORML [5] 穢れを浄化する（浄化力200消費　現在値：{FLAG:6}）
ELSE
	LOCAL:3 = 0
ENDIF
IF (GET_PENIS(1) > 0) && (FLAG:520 == 2 || (FLAG:10 == 0 || (FLAG:11 == 0 && (CFLAG:(FLAG:10):29 || GET_PENIS(FLAG:10) > 0)) || ((CFLAG:(FLAG:10):29 || GET_PENIS(FLAG:10) > 0) && (CFLAG:(FLAG:11):29 || GET_PENIS(FLAG:11) > 0))))
	;主人公がふたなりで、仲間がいないか全員戦意喪失かふたなりだと犯せる
	LOCAL:4 = 1
	PRINTFORML [6] 性欲を発散する

ELSE
	LOCAL:4 = 0
ENDIF
SIF TALENT:(ARG:0):130 >= 1
	PRINTFORML [7] 乳搾り
SIF GET_PENIS(ARG:0) == 1 && ITEM:55
	PRINTFORML [8] %ITEMNAME:55%(残り{ITEM:55}個)を使ってあげる
SIF GET_PENIS(ARG:0) > 0
	PRINTFORML [9] 勃起を治療する
SIF CFLAG:(ARG:0):22 && CFLAG:(ARG:0):36 == 0 && ITEM:57
	PRINTFORML [11] %ITEMNAME:57%(残り{ITEM:57}個)を使ってあげる
SIF BASE:(ARG:0):3 <= MAXBASE:(ARG:0):3 / 2 && ITEM:54
	PRINTFORML [12] %ITEMNAME:54%(残り{ITEM:54}個)を着せてあげる
SIF ITEM:69
	PRINTFORML [13] %ITEMNAME:69%(残り{ITEM:69}個)を飲ませる
SIF ITEM:8 && ITEM:88 >= 10
	PRINTFORML [14] %ITEMNAME:8%を使う
SIF CFLAG:(ARG:0):47 == 1
	PRINTFORML [15] 貞操帯を外す
IF FLAG:(50 + FLAG:5) > 10
	SIF CFLAG:(ARG:0):47 == 0 && (CFLAG:(ARG:0):505 & 1) == 0
		PRINTFORML [21] Vに触手を入れて放置
	SIF (CFLAG:(ARG:0):505 & 2) == 0
		PRINTFORML [22] Bを触手で吸わせて放置
	SIF (CFLAG:(ARG:0):505 & 4) == 0
		PRINTFORML [23] Aに触手を入れて放置
	SIF (CFLAG:(ARG:0):505 & 8) == 0
		PRINTFORML [24] Cを触手で吸わせて放置
	SIF (CFLAG:(ARG:0):505 & 16) == 0
		PRINTFORML [25] 口に触手を入れて放置
ENDIF
SIF PET:0
	PRINTFORML [26] ペットに「食べさせる」
PRINTFORML [110] 能力を見る

PRINTFORML [0] 何もせず別れを告げる


$INPUT_LOOP
INPUT

IF RESULT == 0
	CALL PRINT_MESSAGE(1, 102, ARG:0, CALLNAME:1, CALLNAME:(ARG:0), 1)
	RETURN
ELSEIF RESULT == 1
	CALL PRINT_MESSAGE(1, 138, ARG:0, CALLNAME:1, CALLNAME:(ARG:0), 1)
	IF CFLAG:(ARG:0):5 > 0
		;普通の物が貰える
		CFLAG:(ARG:0):5 -= 30 + RAND:30
		SELECTCASE RAND:100
			CASE 0 TO 4
				LOCAL:2 = 1
			CASE 5 TO 9
				LOCAL:2 = 2
			CASE 10 TO 14
				LOCAL:2 = 10
			CASE 15 TO 19
				LOCAL:2 = 11
			CASE 20 TO 29
				LOCAL:2 = 20
			CASE 30 TO 39
				LOCAL:2 = 24
			CASE 40 TO 59
				LOCAL:2 = 50
			CASE 60 TO 79
				LOCAL:2 = 52
			CASE 80 TO 89
				LOCAL:2 = 54
			CASE 90 TO 94
				LOCAL:2 = 55
			CASE 95 TO 99
				LOCAL:2 = 56
			CASEELSE
				LOCAL:2 = 50
		ENDSELECT
		CALL ADD_ITEM(LOCAL:2, 1, 0)
		CALL PRINT_MESSAGE(ARG:0, 139, LOCAL:2, CALLNAME:1, CALLNAME:(ARG:0))
		CALL PRINT_MESSAGE(1, 105, ARG:0, CALLNAME:1, CALLNAME:(ARG:0), LOCAL:2, 1)
	ELSE
		CALL PRINT_MESSAGE(ARG:0, 139, 0, CALLNAME:1, CALLNAME:(ARG:0))
		CALL PRINT_MESSAGE(1, 106, ARG:0, CALLNAME:1, CALLNAME:(ARG:0), 1)
	ENDIF
	
ELSEIF RESULT == 2 && ITEM:58
	CALL ADD_ITEM(58, -1, 0)
	CALL PRINT_MESSAGE(1, 143, ARG:0, CALLNAME:1, CALLNAME:(ARG:0), 0)
	CFLAG:(ARG:0):5 += 200
	BASE:(ARG:0):1 = MAXBASE:(ARG:0):1 / 4
	CFLAG:(ARG:0):29 = 0

	;主人公が【治療】持ちだと効果増加　ただし発情・戦意喪失・寄生or妊娠末期は無理
	SIF TALENT:1:117 && CFLAG:1:28 == 0 && CFLAG:1:29 == 0 && (CFLAG:1:22 == 0 || CFLAG:1:23 >= 5)
		BASE:(ARG:0):1 += MAXBASE:(ARG:0):1 / 4

	;ED追加
	CALL ADD_HISTORY(DAY, ARG:0,, 5)

	IF LOCAL:1
		CALL PRINT_MESSAGE(ARG:0, 144, 1, CALLNAME:1, CALLNAME:(ARG:0))
		CALL ASK_YN
		IF RESULT == 0
			CALL PRINT_MESSAGE(1, 143, ARG:0, CALLNAME:1, CALLNAME:(ARG:0), 1)
			CALL PRINT_MESSAGE(ARG:0, 144, 2, CALLNAME:1, CALLNAME:(ARG:0))
			CALL PRINT_MESSAGE(ARG:0, 109, 0, CALLNAME:(ARG:0), CALLNAME:(ARG:0))
			IF FLAG:10 == 0
				FLAG:10 = ARG:0
			ELSEIF FLAG:11 == 0
				FLAG:11 = ARG:0
			ELSE
				PRINTFORMW (TALK_2) PT加入処理エラー！{ARG:0}/{FLAG:10}/{FLAG:11}
			ENDIF
			
			;キャラが加入した場合、位置と目的地を主人公に合わせておく
			CFLAG:(ARG:0):4 = CFLAG:1:4
			CFLAG:(ARG:0):13 = CFLAG:1:13
			CFLAG:(ARG:0):14 = CFLAG:1:14
			
			;好感度を最低200に調整
			SIF CFLAG:(ARG:0):5 < 200
				CFLAG:(ARG:0):5 = 200
			;ED追加
			CALL ADD_HISTORY(DAY, ARG:0,, 4)
		ELSE
			CALL PRINT_MESSAGE(1, 143, ARG:0, CALLNAME:1, CALLNAME:(ARG:0), 2)
			CALL PRINT_MESSAGE(ARG:0, 144, 3, CALLNAME:1, CALLNAME:(ARG:0))
		ENDIF
	ELSE
		CALL PRINT_MESSAGE(ARG:0, 144, 0, CALLNAME:1, CALLNAME:(ARG:0))
	ENDIF
ELSEIF RESULT == 3 && ITEM:58 && ITEM:52
	CALL ADD_ITEM(58, -1, 0)
	CALL ADD_ITEM(52, -1, 0)
	CALL PRINT_MESSAGE(1, 143, ARG:0, CALLNAME:1, CALLNAME:(ARG:0), 0)
	CFLAG:(ARG:0):5 += 200
	BASE:(ARG:0):1 = MAXBASE:(ARG:0):1 / 2
	;主人公が【治療】持ちでも【鼓舞】持ちでも効果増加　ただし発情・戦意喪失・寄生or妊娠末期は無理
	IF TALENT:1:117 && CFLAG:1:28 == 0 && CFLAG:1:29 == 0 && (CFLAG:1:22 == 0 || CFLAG:1:23 >= 5)
		BASE:(ARG:0):1 += MAXBASE:(ARG:0):1 / 4
	ELSEIF TALENT:1:118 && CFLAG:1:28 == 0 && CFLAG:1:29 == 0 && (CFLAG:1:22 == 0 || CFLAG:1:23 >= 5)
		BASE:(ARG:0):1 += MAXBASE:(ARG:0):1 / 8
	ENDIF

	CFLAG:(ARG:0):29 = 0
	;ED追加
	CALL ADD_HISTORY(DAY, ARG:0,, 5)

	CALL PRINT_MESSAGE(ARG:0, 144, 4, CALLNAME:1, CALLNAME:(ARG:0))
	IF RAND:4 >= 3
		CALL PRINT_MESSAGE(ARG:0, 144, 5, CALLNAME:1, CALLNAME:(ARG:0))
	ELSE
		CALL PRINT_MESSAGE(ARG:0, 144, 0, CALLNAME:1, CALLNAME:(ARG:0))
	ENDIF


ELSEIF RESULT == 4 && LOCAL:1
	CALL PRINT_MESSAGE(ARG:0, 140, 0, CALLNAME:1, CALLNAME:(ARG:0), 1)
	CALL PRINT_MESSAGE(1, 107, ARG:0, CALLNAME:1, CALLNAME:(ARG:0), 1)
	CFLAG:(ARG:0):5 += 20
	CALL PRINT_MESSAGE(ARG:0, 109, 0, CALLNAME:(ARG:0), CALLNAME:(ARG:0))
	IF FLAG:10 == 0
		FLAG:10 = ARG:0
	ELSEIF FLAG:11 == 0
		FLAG:11 = ARG:0
	ELSE
		PRINTFORMW (TALK_2) PT加入処理エラー！{ARG:0}/{FLAG:10}/{FLAG:11}
	ENDIF
	
	;キャラが加入した場合、位置と目的地を主人公に合わせておく
	CFLAG:(ARG:0):4 = CFLAG:1:4
	CFLAG:(ARG:0):13 = CFLAG:1:13
	CFLAG:(ARG:0):14 = CFLAG:1:14
	CALL RANDOM_ESTRUS_INITIALIZE(ARG:0)
	;好感度を最低120に調整
	SIF CFLAG:(ARG:0):5 < 120
		CFLAG:(ARG:0):5 = 120
;浄化による回復
ELSEIF RESULT == 5 && LOCAL:3
	FLAG:6 -= 200
	CALL PRINT_MESSAGE(1, 166, ARG:0, CALLNAME:1, CALLNAME:(ARG:0))
	CFLAG:(ARG:0):5 += 50
	BASE:(ARG:0):1 = 1
	CFLAG:(ARG:0):29 = 0
	CALL PRINT_MESSAGE(ARG:0, 167, 0, CALLNAME:1, CALLNAME:(ARG:0))
ELSEIF RESULT == 6 && LOCAL:4
CALL NPC_RAPE(ARG:0)
WAIT
CALL ADD_ABL_EXP_PT(13, 3 + RAND:5)
;好感度はもうズタボロ
CFLAG:(ARG:0):5 -= 90 + RAND:30


;母乳飲み
ELSEIF RESULT == 7 && TALENT:(ARG:0):130 >= 1
	CALL PRINT_MESSAGE(ARG:0, 144, 6, CALLNAME:1, CALLNAME:(ARG:0))
	TFLAG:63 = 1
	IF CFLAG:1:29 == 0
		CALL DRINK_MILK_RECOVERY(ARG:0, 1)
	ELSEIF CFLAG:1:29 == 1 && RAND:20 == 0
		CFLAG:1:29 = 0
		CALL PRINT_MESSAGE(1, 36, 58, CALLNAME:1, "")
	ENDIF

	;仲間も回復
	IF FLAG:10 > 0 && CFLAG:(FLAG:10):29 == 0
		CALL DRINK_MILK_RECOVERY(ARG:0, FLAG:10)
	ELSEIF FLAG:10 > 0 && CFLAG:(FLAG:10):29 == 1 && RAND:20 == 0
		CFLAG:(FLAG:10):29 = 0
		CALL PRINT_MESSAGE((FLAG:10), 36, 58, CALLNAME:(FLAG:10), "")
	ENDIF

	IF FLAG:11 > 0 && CFLAG:(FLAG:11):29 == 0
		CALL DRINK_MILK_RECOVERY(ARG:0, FLAG:11)
	ELSEIF FLAG:11 > 0 && CFLAG:(FLAG:11):29 == 1 && RAND:20 == 0
		CFLAG:(FLAG:11):29 = 0
		CALL PRINT_MESSAGE((FLAG:11), 36, 58, CALLNAME:(FLAG:11), "")
	ENDIF
	FOR LOCAL:0,0,(RAND:7 +4)
		CALL DAMAGE_COMMON(ARG:0, 2, 8000 + RAND:4001, 100)
	NEXT
	CALL PRINT_MESSAGE(ARG:0, 144, 12, CALLNAME:1, CALLNAME:(ARG:0))
	;好感度はもちろん下がる ただし膨乳しかけているときと噴乳中毒は別
	CALL ADD_ABL_EXP_PT(13, 3 + RAND:5)
	CFLAG:(ARG:0):5 -= 50 + RAND:20 - CFLAG:(ARG:0):506 - (ABL:(ARG:0):16 * 25) 
	
	SIF CFLAG:(ARG:0):506 > 10
		CFLAG:(ARG:0):506 -= RAND:25

ELSEIF RESULT == 8 && ITEM:55 && GET_PENIS(ARG:0) == 1
	CALL ADD_ITEM(55, -1, 0)
		IF CFLAG:(ARG:0):42 > 0
			CFLAG:(ARG:0):42 = 0
		ENDIF
		CFLAG:(ARG:0):5 += 30

ELSEIF RESULT == 9
	CALL NPC_RAPE_2(ARG:0)
	WAIT
ELSEIF RESULT == 11 && ITEM:57
	CALL ADD_ITEM(57, -1, 0)
	CFLAG:(ARG:0):22 = 0
	CFLAG:(ARG:0):5 += 70
ELSEIF RESULT == 12 && ITEM:54
	CALL ADD_ITEM(54, -1, 0)
	BASE:(ARG:0):3 = MAXBASE:(ARG:0):3
	CFLAG:(ARG:0):44 = 0
	CALL CLOTH_TYPE_2(ARG:0)
	IF (FLAG:(FLAG:5 + 50) + RAND:250) >= 200 || CFLAG:(LOCAL:51):501 == 15 || CFLAG:(LOCAL:51):501 == 22
		CALL PRINT_MESSAGE((ARG:0), 440, 0, CALLNAME:(ARG:0), "")
		CALL PRINT_MESSAGE((ARG:0), 440, 20, CALLNAME:(ARG:0), "")
		CALL PRINT_MESSAGE(ARG:0, 440, 5, CALLNAME:(ARG:0), "")
		CALL PRINT_MESSAGE(ARG:0, 440, 15, CALLNAME:(ARG:0), "")
		CFLAG:(ARG:0):40 = 1
		BASE:(ARG:0):3 = MAXBASE:(ARG:0):3
		;我々の業界ではご褒美ですor人類には早すぎる
		IF ABL:(ARG:0):13 >= 3
			CFLAG:(ARG:0):5 += RAND:30
		ELSE
			CFLAG:(ARG:0):5 -= RAND:30
		ENDIF
	ELSE
		CFLAG:(ARG:0):5 += 40
	ENDIF
ELSEIF RESULT == 13 && ITEM:69
	CALL ADD_ITEM(69, -1, 0)
	IF CFLAG:(ARG:0):506 > 50
		CFLAG:(ARG:0):506 -= 50
	ELSE
		CALL ADD_BUST(ARG:0,-1,1,69)
		CFLAG:(ARG:0):5 -= 10
	ENDIF

ELSEIF RESULT == 14 && ITEM:8 && ITEM:88 >= 10
	CALL ADD_ITEM(8, -1, 0)
	CALL ADD_ITEM(88, -10, 0)
	CFLAG:(ARG:0):504 = 1
	CALL PRINT_MESSAGE(ARG:0, 144, 13, CALLNAME:1, CALLNAME:(ARG:0))

ELSEIF RESULT == 15 && CFLAG:(ARG:0):47 == 1
	CFLAG:(ARG:0):47 = 0
	PRINTFORMW 貞操帯を外した
	GOTO TALK_START
;放置プレイ
ELSEIF RESULT == 21 && FLAG:(50 + FLAG:5) > 10 && CFLAG:(ARG:0):47 == 0 && (CFLAG:(ARG:0):505 & 1) == 0
	CFLAG:(ARG:0):505 |= 1
	GOTO TALK_START
ELSEIF RESULT == 22 && FLAG:(50 + FLAG:5) > 10 && (CFLAG:(ARG:0):505 & 2) == 0
	CFLAG:(ARG:0):505 |= 2
	GOTO TALK_START
ELSEIF RESULT == 23 && FLAG:(50 + FLAG:5) > 10 && (CFLAG:(ARG:0):505 & 4) == 0
	CFLAG:(ARG:0):505 |= 4
	GOTO TALK_START
ELSEIF RESULT == 24 && FLAG:(50 + FLAG:5) > 10 && (CFLAG:(ARG:0):505 & 8) == 0
	CFLAG:(ARG:0):505 |= 8
	GOTO TALK_START
ELSEIF RESULT == 25 && FLAG:(50 + FLAG:5) > 10 && (CFLAG:(ARG:0):505 & 16) == 0
	CFLAG:(ARG:0):505 |= 16
	GOTO TALK_START
ELSEIF RESULT == 26 && PET:0
	PRINTFORML %CALLNAME:1%は自失状態の%CALLNAME:(ARG:0)%を%PETNAME%に与えることにした……
	FOR LOCAL, 0, RAND:5 + 1
		CALL PET_RAPE(ARG:0)
		CFLAG:(ARG:0):5 -= RAND:10 + 5
	NEXT
ELSEIF RESULT == 110
	CALL SHOW_PERSONAL_STATUS(ARG:0)
	GOTO TALK_START
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

