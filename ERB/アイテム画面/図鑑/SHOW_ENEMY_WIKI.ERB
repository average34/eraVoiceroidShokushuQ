;エネミ－図鑑表示
@SHOW_ENEMY_WIKI
;LOCAL
;0	ループ用
;1	改行用
;2	図鑑番号記録用
;5	ページ調整
;6	図鑑が見られるかどうか
;7	直前に選択した番号
;8	素質を見たかどうか
;10	行削除用カウント1(計算用)
;11	行削除用カウント2(計算用)
;12	行削除用カウント3(項目の改行数)
;13	行削除用カウント4(計算用)
;14	行削除用カウント5(計算用)
;15	行削除用カウント6(詳細の改行数)

LOCAL:5 = 0

$START
REDRAW 0
LOCAL:10 = LINECOUNT

DRAWLINE
PRINTFORML どのエネミーの情報を見ますか？

CALL DEBUG_OUTPUT_ENEMY_LIST(LOCAL:5, 20, -1)
LOCAL:20 = RESULT

PRINTL 
PRINTL 
PRINTL [ 0] キャンセル


	$INPUT_LOOP
	INPUT

	LOCAL:11 = LINECOUNT
	;項目表示の改行数を記録
	LOCAL:12 = LOCAL:11 - LOCAL:10

	IF RESULT == 0
		LOCAL:8 = 0
		REDRAW 1
		RETURN
	ELSEIF ENEMY_NAME(RESULT) != "バグ触手" && (RESULT == 99 || RESULT == 100 || GET_ENEMY_WIKI_REGISTERED(RESULT))
		LOCAL:2 = RESULT
		;行削除(初回)
		IF LOCAL:8 == 0
			CLEARLINE LOCAL:12
		;行削除(2回目～)
		ELSEIF LOCAL:8 == 1 && LOCAL:7 != RESULT
			CLEARLINE LOCAL:12 + LOCAL:15
		;連打のログ流れ防止
		ELSEIF LOCAL:7 == RESULT
			CLEARLINE 1
			GOTO INPUT_LOOP
		ENDIF
		LOCAL:7 = RESULT
		LOCAL:13 = LINECOUNT
		CALL ENEMY_WIKI(LOCAL:2)
		LOCAL:14 = LINECOUNT
		;説明表示の改行数を記録
		LOCAL:15 = LOCAL:14 - LOCAL:13
		LOCAL:8 = 1
	ELSEIF RESULT == 777
		CLEARLINE LOCAL:12
		;前ページへ
		LOCAL:5 -= 1
		;最初のページなら最後に飛ぶ
		SIF LOCAL:5 < 0
			LOCAL:5 = LOCAL:20
	ELSEIF RESULT == 999
		CLEARLINE LOCAL:12
		;次ページへ
		LOCAL:5 += 1
		;最後のページなら最初に飛ぶ
		SIF LOCAL:5 > LOCAL:20
			LOCAL:5 = 0
	ELSE
		CLEARLINE 1
		REUSELASTLINE 入力が不正です！
		GOTO INPUT_LOOP
	ENDIF

GOTO START

;戦闘中の相手の図鑑呼び出しのための対象選択 攻撃対象選択構文を流用
;何かを表示した場合は1、キャンセルで抜けた場合0が返る
@SHOW_BATTLE_ENEMY_WIKI
;TFLAG:(LOCAL:0)		LOCAL:0番目の戦闘相手
;DA:(TFLAG:(LOCAL:0)):0	戦闘相手の種類

;LOCAL
;0	LOOP
;1～	その番号の種類の敵がいるかどうか

VARSET LOCAL, 0
FOR LOCAL:0, 1, 11
	IF LOCAL:0 <= TFLAG:15 && LOCAL:(DA:(TFLAG:(LOCAL:0)):0) == 0
		;戦闘している相手のみ見られるようにLOCALにいれる
		LOCAL:(DA:(TFLAG:(LOCAL:0)):0) = 1
		PRINTFORML [{DA:(TFLAG:(LOCAL:0)):0}] %SAVESTR:(TFLAG:(LOCAL:0))%
	ENDIF
NEXT
PRINTFORML [0] キャンセル
$INPUT_LOOP
INPUT
IF RESULT == 0
	;何もしない
	RETURN 0
ELSEIF (LOCAL:(RESULT) > 0)
	CALL ENEMY_WIKI(RESULT)
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF
RETURN 1

;データ表示
@ENEMY_WIKI(ARG:0)
;ARG:0	敵番号

TRYCCALLFORM SET_ENEMY_DATA_{ARG:0}(FLAG:1)
CATCH
PRINTFORMW (CREATE_ENEMY) 未実装の敵(ID:{ARG:0})のため、処理できません
RETURN 0
ENDCATCH
PRINTFORML 
PRINTFORML 
DRAWLINE
PRINTFORM %ENEMY_NAME(ARG:0)%　　　　　　
SIF DA:(FLAG:1):85 & 1
	PRINTFORM 　女性
SIF DA:(FLAG:1):85 & 128
	PRINTFORM 　男性
SIF DA:(FLAG:1):85 & 2
	PRINTFORM 　植物
SIF DA:(FLAG:1):85 & 4
	PRINTFORM 　動物
SIF DA:(FLAG:1):85 & 8
	PRINTFORM 　丸呑
SIF DA:(FLAG:1):85 & 16
	PRINTFORM 　海産
SIF DA:(FLAG:1):85 & 32
	PRINTFORM 　ローパー
SIF DA:(FLAG:1):85 & 64
	PRINTFORM 　スライム
SIF DA:(FLAG:1):85 & 256
	PRINTFORM 　罠
SIF DA:(FLAG:1):85 & 512
	PRINTFORM 　機械

PRINTFORML 系
DRAWLINE
CALL PRINT_MESSAGE(-1, 511, ARG:0, CALLNAME:1, ENEMY_NAME(ARG:0))

DRAWLINE
PRINTFORML HP {DA:(FLAG:1):1}				戦闘素質 {DA:(FLAG:1):5}
PRINTFORML 速度 {DA:(FLAG:1):9}				命中 {DA:(FLAG:1):18}
PRINTFORM 移動タイプ　
IF DA:(FLAG:1):17 >= DA:(FLAG:1):15 && DA:(FLAG:1):17 >= DA:(FLAG:1):16
	PRINTFORML 待ち伏せ
ELSEIF DA:(FLAG:1):15 >= DA:(FLAG:1):16 && DA:(FLAG:1):15 >= DA:(FLAG:1):17
	PRINTFORML 突撃
ELSE
	PRINTFORML 慎重
ENDIF
PRINTFORM 遭遇率　
IF DA:(FLAG:1):86 == 7
	PRINTFORML 特殊
ELSEIF DA:(FLAG:1):86 == 6
	PRINTFORML 稀少
ELSEIF DA:(FLAG:1):86 == 5
	PRINTFORML 見かけにくい
ELSEIF DA:(FLAG:1):86 == 4
	PRINTFORML 少し見かけにくい
ELSEIF DA:(FLAG:1):86 == 3
	PRINTFORML 普通
ELSEIF DA:(FLAG:1):86 == 2
	PRINTFORML 高い
ELSEIF DA:(FLAG:1):86 == 1
	PRINTFORML 非常に高い
ENDIF
PRINTFORM 耐性　　
SIF DA:(FLAG:1):14 < 0
	PRINTFORM 怯み＋{DA:(FLAG:1):14}％　
SIF DA:(FLAG:1):40 < 100 && DA:(FLAG:1):40 > 0
	PRINTFORM 攻撃{DA:(FLAG:1):40}％　
SIF DA:(FLAG:1):41 < 100 && DA:(FLAG:1):41 > 0
	PRINTFORM 炎攻撃{DA:(FLAG:1):41}％　
SIF DA:(FLAG:1):42 < 100 && DA:(FLAG:1):42 > 0
	PRINTFORM 消耗武器{DA:(FLAG:1):42}％　
SIF DA:(FLAG:1):43 < 100 && DA:(FLAG:1):43 > 0
	PRINTFORM 炎武器{DA:(FLAG:1):43}％　
SIF DA:(FLAG:1):44 < 100 && DA:(FLAG:1):44 > 0
	PRINTFORM 非消耗武器{DA:(FLAG:1):44}％　
PRINTFORML 
PRINTFORM 弱点　　
SIF DA:(FLAG:1):14 > 0
	PRINTFORM 怯み－{DA:(FLAG:1):14}％　
SIF DA:(FLAG:1):40 > 100
	PRINTFORM 攻撃{DA:(FLAG:1):40}％　
SIF DA:(FLAG:1):41 > 100
	PRINTFORM 炎攻撃{DA:(FLAG:1):41}％　
SIF DA:(FLAG:1):42 > 100
	PRINTFORM 消耗武器{DA:(FLAG:1):42}％　
SIF DA:(FLAG:1):43 > 100
	PRINTFORM 炎武器{DA:(FLAG:1):43}％　
SIF DA:(FLAG:1):44 > 100
	PRINTFORM 非消耗武器{DA:(FLAG:1):44}％　
PRINTFORML 
PRINTFORM 襲う部位
SIF DA:(FLAG:1):89 & 1
	PRINTFORM 　A
SIF DA:(FLAG:1):89 & 2
	PRINTFORM 　B
SIF DA:(FLAG:1):89 & 4
	PRINTFORM 　C
SIF DA:(FLAG:1):89 & 8
	PRINTFORM 　V
SIF DA:(FLAG:1):89 & 16
	PRINTFORM 　肌
SIF DA:(FLAG:1):89 & 32
	PRINTFORM 　M
PRINTFORML 
PRINTFORML 
FOR LOCAL:0, 0, VARSIZE("DA", 1)
	DA:(FLAG:1):(LOCAL:0) = 0
NEXT

;エネミー図鑑状況の変数をGLOBALからENEMY_WIKI_REGISTERED_GLOBALへ移行する
;あくまで移行のみ。SAVEGLOBALはしない
@MIGRATE_ENEMY_WIKI_DATA()
;移行済みなら何もしない
IF ENEMY_WIKI_REGISTERED_GLOBAL:0
	RETURN
ENDIF

FOR LOCAL, 0, VARSIZE("ENEMY_WIKI_REGISTERED")
	IF LOCAL <= 150
		SELECTCASE GLOBAL:(LOCAL + 100)
			CASE -1
				ENEMY_WIKI_REGISTERED_GLOBAL:LOCAL = 1
			CASE 0
				ENEMY_WIKI_REGISTERED_GLOBAL:LOCAL = 0
			CASE 1
				ENEMY_WIKI_REGISTERED_GLOBAL:LOCAL = 2
		ENDSELECT
	ELSE
		ENEMY_WIKI_REGISTERED_GLOBAL:LOCAL = 0
	ENDIF
NEXT
ENEMY_WIKI_REGISTERED_GLOBAL:0 = 1 ;移行完了チェック用（そもそも要素0は使わないからね）

;エネミー図鑑状況の変数をFLAGからENEMY_WIKI_REGISTEREDへ移行する
@MIGRATE_ENEMY_WIKI_DATA_FROMFLAG()
;移行済みなら何もしない
IF ENEMY_WIKI_REGISTERED:0
	RETURN
ENDIF

FOR LOCAL, 0, VARSIZE("ENEMY_WIKI_REGISTERED")
	IF LOCAL <= 150
		SELECTCASE FLAG:(LOCAL + 700)
			CASE -1
				ENEMY_WIKI_REGISTERED:LOCAL = 1
			CASE 0
				ENEMY_WIKI_REGISTERED:LOCAL = 0
			CASE 1
				ENEMY_WIKI_REGISTERED:LOCAL = 2
		ENDSELECT
	ELSE
		ENEMY_WIKI_REGISTERED:LOCAL = 0
	ENDIF
NEXT
ENEMY_WIKI_REGISTERED:0 = 1 ;移行完了チェック用（そもそも要素0は使わないからね）

;エネミー図鑑登録状況をgloval.savから取得。
;ARG : gloval.sav読み込み前に登録状況をリセットするか
;      0->リセットしない（マージする）/1->リセットする（コピーする）
@LOAD_GLOBAL_ENEMY_WIKI_DATA(ARG)
LOADGLOBAL
CALL MIGRATE_ENEMY_WIKI_DATA
IF ARG
	ARRAYCOPY "ENEMY_WIKI_REGISTERED_GLOBAL", "ENEMY_WIKI_REGISTERED"
ELSE
	FOR LOCAL, 0, VARSIZE("ENEMY_WIKI_REGISTERED")
		ENEMY_WIKI_REGISTERED:LOCAL |= ENEMY_WIKI_REGISTERED_GLOBAL:LOCAL & 2
	NEXT
ENDIF

;エネミー図鑑登録状況をgloval.savに保存。
@SAVE_GLOBAL_ENEMY_WIKI_DATA()
ARRAYCOPY "ENEMY_WIKI_REGISTERED", "ENEMY_WIKI_REGISTERED_GLOBAL"
SAVEGLOBAL

;NoがARGの敵をエネミー図鑑に登録する。
@REGISTER_ENEMY_WIKI(ARG)
ENEMY_WIKI_REGISTERED:ARG |= 2
ENEMY_WIKI_REGISTERED_GLOBAL:ARG |= 2

;NoがARGの敵がエネミー図鑑に登録されているかを返す。
@GET_ENEMY_WIKI_REGISTERED(ARG)
#FUNCTION
RETURNF ENEMY_WIKI_REGISTERED:ARG & 2
