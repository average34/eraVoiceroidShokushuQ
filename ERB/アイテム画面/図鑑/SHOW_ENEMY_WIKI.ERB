;エネミ－図鑑表示
@SHOW_ENEMY_WIKI
;LOCAL
;0	ループ用
;1	改行用
;2	図鑑番号記録用
;5	ページ調整
;6	図鑑が見られるかどうか
;7	直前に選択した番号
;8	素質を見たかどうか
;10	行削除用カウント1(計算用)
;11	行削除用カウント2(計算用)
;12	行削除用カウント3(項目の改行数)
;13	行削除用カウント4(計算用)
;14	行削除用カウント5(計算用)
;15	行削除用カウント6(詳細の改行数)

LOCAL:5 = 0

$START
REDRAW 0
LOCAL:10 = LINECOUNT

DRAWLINE
PRINTFORML どのエネミーの情報を見ますか？

;リスト部分を表示
FOR LOCAL:0, (1+20*LOCAL:5), (21+20*LOCAL:5)
	;敵データ呼び出し
	TRYCCALLFORM SET_ENEMY_DATA_{LOCAL:0}(FLAG:1)
	CATCH
	ENDCATCH
	
	;非表示にするか確認
	LOCAL:6 = 0
	
	;出会ったことのない敵は除外　ただし99番触手と100番淫虫は最初から見られる
	IF FLAG:(LOCAL:0 + 700) == 0 && LOCAL:0 != 99 && LOCAL:0 != 100
		LOCAL:6 = 1
	ENDIF
	
	
	;表示条件を満たした敵は選択肢表示　満たしていない部分は？？？表示
	IF LOCAL:6 == 0 && ENEMY_NAME(LOCAL:0) != "バグ触手"
		LOCALS = %ENEMY_NAME(LOCAL:0)%
		IF FLAG:(LOCAL:0 + 700) < 0
			PRINTFORM [{LOCAL:0,2}] × %LOCALS, 40, LEFT%
		ELSE
			PRINTFORM [{LOCAL:0,2}]    %LOCALS, 40, LEFT%
		ENDIF
	ELSE
		;灰色表示
		LOCALS = ？？？
		SETCOLOR 0x333333
		PRINTPLAINFORM [{LOCAL:0,2}]    %LOCALS, 40, LEFT%
		RESETCOLOR
	ENDIF
	
	
	;処理が終わったら後始末
	FOR LOCAL:1, 0, VARSIZE("DA", 1)
		DA:(FLAG:1):(LOCAL:1) = 0
	NEXT
	
	
	;改行処理
	IF LOCAL:0 % 2 == 0
		PRINTL 
	ENDIF
	IF LOCAL:0 >= 100
		;扱うのは98匹目まで
		BREAK
	ENDIF
NEXT

DRAWLINE
PRINTLC [777] 前のページ
PRINTFORMLC           Page({LOCAL:5 + 1}/6)
PRINTC [999] 次のページ
PRINTL 
PRINTL 
PRINTL
PRINTL [ 0] キャンセル


	$INPUT_LOOP
	INPUT

	LOCAL:11 = LINECOUNT
	;項目表示の改行数を記録
	LOCAL:12 = LOCAL:11 - LOCAL:10

	IF RESULT == 0
		LOCAL:8 = 0
		REDRAW 1
		RETURN
	ELSEIF ENEMY_NAME(RESULT) != "バグ触手" && RESULT > 0 && RESULT < 101 && RESULT != 20 && (RESULT == 99 || RESULT == 100 ||FLAG:(RESULT + 700) > 0 )
		LOCAL:2 = RESULT
		;行削除(初回)
		IF LOCAL:8 == 0
			CLEARLINE LOCAL:12
		;行削除(2回目～)
		ELSEIF LOCAL:8 == 1 && LOCAL:7 != RESULT
			CLEARLINE LOCAL:12 + LOCAL:15
		;連打のログ流れ防止
		ELSEIF LOCAL:7 == RESULT
			CLEARLINE 1
			GOTO INPUT_LOOP
		ENDIF
		LOCAL:7 = RESULT
		LOCAL:13 = LINECOUNT
		CALL ENEMY_WIKI(LOCAL:2)
		LOCAL:14 = LINECOUNT
		;説明表示の改行数を記録
		LOCAL:15 = LOCAL:14 - LOCAL:13
		LOCAL:8 = 1
	ELSEIF RESULT == 777
		CLEARLINE LOCAL:12
		;前ページへ
		LOCAL:5 -= 1
		;最初のページなら最後に飛ぶ
		SIF LOCAL:5 < 0
			LOCAL:5 = 4
	ELSEIF RESULT == 999
		CLEARLINE LOCAL:12
		;次ページへ
		LOCAL:5 += 1
		;最後のページなら最初に飛ぶ
		SIF LOCAL:5 > 4
			LOCAL:5 = 0
	ELSE
		CLEARLINE 1
		REUSELASTLINE 入力が不正です！
		GOTO INPUT_LOOP
	ENDIF

GOTO START

;戦闘中の相手の図鑑呼び出しのための対象選択 攻撃対象選択構文を流用
;何かを表示した場合は1、キャンセルで抜けた場合0が返る
@SHOW_BATTLE_ENEMY_WIKI
;TFLAG:(LOCAL:0)		LOCAL:0番目の戦闘相手
;DA:(TFLAG:(LOCAL:0)):0	戦闘相手の種類

;LOCAL
;0	LOOP
;1～	その番号の種類の敵がいるかどうか

VARSET LOCAL, 0
FOR LOCAL:0, 1, 11
	IF LOCAL:0 <= TFLAG:15 && LOCAL:(DA:(TFLAG:(LOCAL:0)):0) == 0
		;戦闘している相手のみ見られるようにLOCALにいれる
		LOCAL:(DA:(TFLAG:(LOCAL:0)):0) = 1
		PRINTFORML [{DA:(TFLAG:(LOCAL:0)):0}] %SAVESTR:(TFLAG:(LOCAL:0))%
	ENDIF
NEXT
PRINTFORML [0] キャンセル
$INPUT_LOOP
INPUT
IF RESULT == 0
	;何もしない
	RETURN 0
ELSEIF (LOCAL:(RESULT) > 0)
	CALL ENEMY_WIKI(RESULT)
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF
RETURN 1

;データ表示
@ENEMY_WIKI(ARG:0)
;ARG:0	敵番号

TRYCCALLFORM SET_ENEMY_DATA_{ARG:0}(FLAG:1)
CATCH
PRINTFORMW (CREATE_ENEMY) 未実装の敵(ID:{ARG:0})のため、処理できません
RETURN 0
ENDCATCH
PRINTFORML 
PRINTFORML 
DRAWLINE
PRINTFORM %ENEMY_NAME(ARG:0)%　　　　　　
SIF DA:(FLAG:1):85 & 1
	PRINTFORM 　女性
SIF DA:(FLAG:1):85 & 128
	PRINTFORM 　男性
SIF DA:(FLAG:1):85 & 2
	PRINTFORM 　植物
SIF DA:(FLAG:1):85 & 4
	PRINTFORM 　動物
SIF DA:(FLAG:1):85 & 8
	PRINTFORM 　丸呑
SIF DA:(FLAG:1):85 & 16
	PRINTFORM 　海産
SIF DA:(FLAG:1):85 & 32
	PRINTFORM 　ローパー
SIF DA:(FLAG:1):85 & 64
	PRINTFORM 　スライム
SIF DA:(FLAG:1):85 & 256
	PRINTFORM 　罠
SIF DA:(FLAG:1):85 & 512
	PRINTFORM 　機械

PRINTFORML 系
DRAWLINE
CALL PRINT_MESSAGE(-1, 511, ARG:0, CALLNAME:1, ENEMY_NAME(ARG:0))

DRAWLINE
PRINTFORML HP {DA:(FLAG:1):1}				戦闘素質 {DA:(FLAG:1):5}
PRINTFORML 速度 {DA:(FLAG:1):9}				命中 {DA:(FLAG:1):18}
PRINTFORM 移動タイプ　
IF DA:(FLAG:1):17 >= DA:(FLAG:1):15 && DA:(FLAG:1):17 >= DA:(FLAG:1):16
	PRINTFORML 待ち伏せ
ELSEIF DA:(FLAG:1):15 >= DA:(FLAG:1):16 && DA:(FLAG:1):15 >= DA:(FLAG:1):17
	PRINTFORML 突撃
ELSE
	PRINTFORML 慎重
ENDIF
PRINTFORM 遭遇率　
IF DA:(FLAG:1):86 == 7
	PRINTFORML 特殊
ELSEIF DA:(FLAG:1):86 == 6
	PRINTFORML 稀少
ELSEIF DA:(FLAG:1):86 == 5
	PRINTFORML 見かけにくい
ELSEIF DA:(FLAG:1):86 == 4
	PRINTFORML 少し見かけにくい
ELSEIF DA:(FLAG:1):86 == 3
	PRINTFORML 普通
ELSEIF DA:(FLAG:1):86 == 2
	PRINTFORML 高い
ELSEIF DA:(FLAG:1):86 == 1
	PRINTFORML 非常に高い
ENDIF
PRINTFORM 耐性　　
SIF DA:(FLAG:1):14 < 0
	PRINTFORM 怯み＋{DA:(FLAG:1):14}％　
SIF DA:(FLAG:1):40 < 100 && DA:(FLAG:1):40 > 0
	PRINTFORM 攻撃{DA:(FLAG:1):40}％　
SIF DA:(FLAG:1):41 < 100 && DA:(FLAG:1):41 > 0
	PRINTFORM 炎攻撃{DA:(FLAG:1):41}％　
SIF DA:(FLAG:1):42 < 100 && DA:(FLAG:1):42 > 0
	PRINTFORM 消耗武器{DA:(FLAG:1):42}％　
SIF DA:(FLAG:1):43 < 100 && DA:(FLAG:1):43 > 0
	PRINTFORM 炎武器{DA:(FLAG:1):43}％　
SIF DA:(FLAG:1):44 < 100 && DA:(FLAG:1):44 > 0
	PRINTFORM 非消耗武器{DA:(FLAG:1):44}％　
PRINTFORML 
PRINTFORM 弱点　　
SIF DA:(FLAG:1):14 > 0
	PRINTFORM 怯み－{DA:(FLAG:1):14}％　
SIF DA:(FLAG:1):40 > 100
	PRINTFORM 攻撃{DA:(FLAG:1):40}％　
SIF DA:(FLAG:1):41 > 100
	PRINTFORM 炎攻撃{DA:(FLAG:1):41}％　
SIF DA:(FLAG:1):42 > 100
	PRINTFORM 消耗武器{DA:(FLAG:1):42}％　
SIF DA:(FLAG:1):43 > 100
	PRINTFORM 炎武器{DA:(FLAG:1):43}％　
SIF DA:(FLAG:1):44 > 100
	PRINTFORM 非消耗武器{DA:(FLAG:1):44}％　
PRINTFORML 
PRINTFORM 襲う部位
SIF DA:(FLAG:1):89 & 1
	PRINTFORM 　A
SIF DA:(FLAG:1):89 & 2
	PRINTFORM 　B
SIF DA:(FLAG:1):89 & 4
	PRINTFORM 　C
SIF DA:(FLAG:1):89 & 8
	PRINTFORM 　V
SIF DA:(FLAG:1):89 & 16
	PRINTFORM 　肌
SIF DA:(FLAG:1):89 & 32
	PRINTFORM 　M
PRINTFORML 
PRINTFORML 
FOR LOCAL:0, 0, VARSIZE("DA", 1)
	DA:(FLAG:1):(LOCAL:0) = 0
NEXT

