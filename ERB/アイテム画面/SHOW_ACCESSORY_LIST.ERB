;アクセサリ一覧表示
@SHOW_ACCESSORY_LIST

REDRAW 0
DRAWLINE
PRINTFORML 現在所持しているアクセサリは以下の通りです
DRAWLINE

FOR LOCAL:0, 20, 42
	IF ITEM:(LOCAL:0)
		;0番のキャラを利用して表示を行う
		CFLAG:0:7 = LOCAL:0
		PRINTFORM %ITEMNAME:(LOCAL:0), 20, LEFT% {ITEM:(LOCAL:0)}個 
		CALL ITEM_EXPLAIN(0, 0)
		PRINTL  
	ELSE
		SETCOLOR 0x333333
		PRINTFORML －－－
		RESETCOLOR
	ENDIF
NEXT
CFLAG:0:7 = 0
REDRAW 1

;アクセサリ選択
;RETURN	装備するアイテムの番号
@SELECT_ACCESSORY(ARGS:0)

DRAWLINE
IF ARGS:0 == ""
	PRINTFORML 装備するアイテムを選んでください
ELSE
	PRINTFORML %ARGS:0%
ENDIF
DRAWLINE

FOR LOCAL:0, 1, 23
	IF ITEM:(LOCAL:0 + 19)
		LOCAL:(LOCAL:0) = 1
		;0番のキャラを利用して表示を行う
		CFLAG:0:7 = LOCAL:0 + 19
		PRINTFORM [{LOCAL:0,2}] %ITEMNAME:(LOCAL:0 + 19), 20, LEFT% {ITEM:(LOCAL:0 + 19)}個 
		CALL ITEM_EXPLAIN(0, 0)
		PRINTL  
	ELSE
		LOCAL:(LOCAL:0) = 0
		SETCOLOR 0x333333
		PRINTFORML [{LOCAL:0,2}] －－－
		RESETCOLOR
	ENDIF
NEXT
CFLAG:0:7 = 0
PRINTFORML [0] 戻る

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN 0
ELSEIF (RESULT >= 1) && (RESULT <= 22) && (LOCAL:(RESULT) == 1)
	RETURN (RESULT + 19)
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

