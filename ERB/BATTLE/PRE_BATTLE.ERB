;戦闘前処理
;---------------------------------------
; 引数
; ENCOUNT_PATTERN（0：起きてる　1:通常睡眠　2:見張り睡眠　3:気絶睡眠　4:強制敵先制　5:強制睡眠）
;
; 返り値
; 0:戦闘回避　1:味方の勝利　2:全滅　3:逃走
;---------------------------------------
@PRE_BATTLE(ENCOUNT_PATTERN)
#DIM ENCOUNT_PATTERN
#DIM BATTLE_RESULT
#DIM LCOUNT
#DIM ENEMY_COUNT
#DIM ITEM_DROP_RATE
#DIM ENEMY_ACT_POLICY
#DIM MAX_ENEMY_BATTLE_PERFORMANCE
#DIM ABL1_PT_TOTAL
#DIM DETECT_ENEMY
#DIM FIRST_ENCOUNT_ENEMY_ID
#DIM CHARA_ID_OF_AKUOCHI_ENEMY
#DIM CHARA_ID_OF_DAUGHTER_ENEMY
#DIM ESCAPE_SUCCESS_CHARA_ID, 3
#DIM ESCAPE_FAIL_CHARA_ID, 3

;ステータス更新
CALL STATUS_RENEW

;各種変数初期化
VARSET TFLAG, 0
VARSET ENEMY_COUNT
VARSET ENEMY_ACT_POLICY
VARSET MAX_ENEMY_BATTLE_PERFORMANCE
VARSET FIRST_ENCOUNT_ENEMY_ID

;全員行動不能なら戦闘不要（NPCによる救出は別処理）
{
IF !(CFLAG:1:2 == 0 && CFLAG:1:3 == 0 && CFLAG:1:10 == 0 && CFLAG:1:29 == 0) &&
   !(FLAG:10 && (CFLAG:(FLAG:10):2 == 0 && CFLAG:(FLAG:10):3 == 0 && CFLAG:(FLAG:10):10 == 0 && CFLAG:(FLAG:10):29 == 0)) &&
   !(FLAG:11 && (CFLAG:(FLAG:11):2 == 0 && CFLAG:(FLAG:11):3 == 0 && CFLAG:(FLAG:11):10 == 0 && CFLAG:(FLAG:11):29 == 0))
}
	RETURN 0
ENDIF

;結界部屋で戦闘が発生した場合、キャンセルできる
IF FLAG:5 == FLAG:98
	PRINTFORML 敵に襲い掛かられたが、結界のお陰で戦闘を回避することができた！
	CALL WAIT_AUTOMODE(400, 0)
	RETURN 3
ENDIF

;REPEATの対象をリセット
FOR LCOUNT, 0, 3
	IF GET_MEMBER(LCOUNT)
		CFLAG:(GET_MEMBER(LCOUNT)):106 = 0
	ENDIF
NEXT

;戦闘遭遇エネミーの選出
CALL SELECT_BATTLE_ENEMY
ENEMY_COUNT = RESULT
FOR LCOUNT, 1, ENEMY_COUNT + 1
	;より強い敵の行動方針が反映される
	IF MAX_ENEMY_BATTLE_PERFORMANCE < DA:(TFLAG:LCOUNT):5
		MAX_ENEMY_BATTLE_PERFORMANCE = DA:(TFLAG:LCOUNT):5
		ENEMY_ACT_POLICY = DA:(TFLAG:LCOUNT):11
	ENDIF
	;初遭遇時の処理（各戦闘１体だけ）
	IF FIRST_ENCOUNT_ENEMY_ID == 0 && !GET_ENEMY_WIKI_REGISTERED(DA:(TFLAG:LCOUNT):0)
		FIRST_ENCOUNT_ENEMY_ID = TFLAG:LCOUNT
		CALL REGISTER_ENEMY_WIKI(DA:(TFLAG:LCOUNT):0)
		;クイーン戦ならお供も同時に登録する（「右腕たちが現れた！」はアレなので）
		SIF TFLAG:0 == 1
			CALL REGISTER_ENEMY_WIKI(2)
	ENDIF
NEXT

;敵が見つからない場合はキャンセル
IF ENEMY_COUNT <= 0
	PRINTFORML どういうわけか誰も襲ってこなかった……
	CALL WAIT_AUTOMODE(500, 0)
	RETURN 0
ENDIF

;先制チェック
;TFLAG:14	先制フラグ(0:通常　1:味方先制　2:敵先制　3:味方睡眠)
;強制敵先制
IF ENCOUNT_PATTERN == 4
	TFLAG:14 = 2
;強制睡眠
ELSEIF ENCOUNT_PATTERN == 5
	PRINTFORML %CALLNAME:1%%TACHI()%はぐっすり眠っていて敵に気付いていない！
	CALL WAIT_AUTOMODE(500, 0)
	CALL ADD_ABL_EXP_PT(1, 2000)
	TFLAG:14 = 3
;その他の状況
ELSE
	SELECTCASE CFLAG:1:11
	CASE 0
		;突撃
		IF ENEMY_ACT_POLICY == 1
			TFLAG:14 = 1
		ELSEIF ENEMY_ACT_POLICY == 2
			TFLAG:14 = 2
		ELSE
			TFLAG:14 = 0
		ENDIF
	CASE 1
		;慎重
		IF ENEMY_ACT_POLICY == 0
			TFLAG:14 = 2
		ELSEIF ENEMY_ACT_POLICY == 2
			TFLAG:14 = 1
		ELSE
			TFLAG:14 = 0
		ENDIF
	CASE 2
		;待ち伏せ
		IF ENEMY_ACT_POLICY == 0
			TFLAG:14 = 1
		ELSEIF ENEMY_ACT_POLICY == 1
			TFLAG:14 = 2
		ELSE
			TFLAG:14 = 0
		ENDIF
	CASEELSE
		;寝込み
		IF ENCOUNT_PATTERN
			VARSET ABL1_PT_TOTAL
			VARSET DETECT_ENEMY
			;寝こみを襲われた
			FOR LCOUNT, 0, 3
				IF GET_MEMBER(LCOUNT)
					ABL1_PT_TOTAL += ABL:(GET_MEMBER(LCOUNT)):1
					IF TALENT:(GET_MEMBER(LCOUNT)):154
						DETECT_ENEMY = 20 + RAND:(DETECT_ENEMY + 1)
					ENDIF
				ENDIF
			NEXT
			DETECT_ENEMY += RAND:(100 + (GET_PT_MAX_ABL(5) * 20) + (ABL1_PT_TOTAL * 5))
			SELECTCASE ENCOUNT_PATTERN
				CASE 1
					;通常
					IF DETECT_ENEMY >= 150
						PRINTFORML %CALLNAME:1%%TACHI()%は襲ってきた敵の不意を突くことに成功した！
						CALL WAIT_AUTOMODE(400, 0)
						CALL ADD_ABL_EXP_PT(1, 500)
						TFLAG:14 = 1
					ELSEIF DETECT_ENEMY >= 60
						PRINTFORML %CALLNAME:1%%TACHI()%は敵の襲撃に気付いた！
						CALL WAIT_AUTOMODE(400, 0)
						CALL ADD_ABL_EXP_PT(1, 1000)
						TFLAG:14 = 0
					ELSEIF DETECT_ENEMY >= 30
						PRINTFORML %CALLNAME:1%%TACHI()%は何とか目を覚ましたものの、体勢を崩されてしまった！
						CALL WAIT_AUTOMODE(400, 0)
						CALL ADD_ABL_EXP_PT(1, 1500)
						TFLAG:14 = 2
					ELSE
						PRINTFORML %CALLNAME:1%%TACHI()%はぐっすり眠っていて敵に気付いていない！
						CALL WAIT_AUTOMODE(400, 0)
						CALL ADD_ABL_EXP_PT(1, 2000)
						TFLAG:14 = 3
					ENDIF
				CASE 2
					;見張りあり＝失敗なし
					IF DETECT_ENEMY >= 140
						PRINTFORML 見張りのお陰で%CALLNAME:1%%TACHI()%は襲ってきた敵の不意を突くことに成功した！
						CALL WAIT_AUTOMODE(400, 0)
						CALL ADD_ABL_EXP_PT(1, 1500)
						TFLAG:14 = 1
					ELSE
						PRINTFORML 見張りのお陰で%CALLNAME:1%%TACHI()%は敵の襲撃に気付いた！
						CALL WAIT_AUTOMODE(400, 0)
						CALL ADD_ABL_EXP_PT(1, 1000)
						TFLAG:14 = 0
					ENDIF
				CASEELSE
					;気絶＝失敗のみ
					IF DETECT_ENEMY >= 140
						PRINTFORML %CALLNAME:1%%TACHI()%は朦朧としながらも何とか目をさまし、応戦し始めた！
						CALL WAIT_AUTOMODE(400, 0)
						CALL ADD_ABL_EXP_PT(1, 1500)
						TFLAG:14 = 2
					ELSE
						PRINTFORML %CALLNAME:1%%TACHI()%は気絶していて敵に気付いていない！
						CALL WAIT_AUTOMODE(400, 0)
						CALL ADD_ABL_EXP_PT(1, 1000)
						TFLAG:14 = 3
					ENDIF
			ENDSELECT
		;探索など起きてはいるが他のことに気を取られた
		ELSE
			;50%で敵先制、50%で通常
			IF RAND:2 == 0
				TFLAG:14 = 2
			ELSE
				TFLAG:14 = 0
			ENDIF
		ENDIF
	ENDSELECT
ENDIF

;敵の数を覚えておく
TFLAG:15 = ENEMY_COUNT

;戦闘中フラグオン
TFLAG:13 = 1

;初回遭遇時テキスト
IF FIRST_ENCOUNT_ENEMY_ID && FLAG:508 == 0
	CALL PRINT_MESSAGE(-1, 61, DA:FIRST_ENCOUNT_ENEMY_ID:0, CALLNAME:1, ENEMY_NAME(DA:FIRST_ENCOUNT_ENEMY_ID:0), DA:FIRST_ENCOUNT_ENEMY_ID:46)
	CALL PRINT_MESSAGE(1, 61, DA:FIRST_ENCOUNT_ENEMY_ID:0, CALLNAME:1, ENEMY_NAME(DA:FIRST_ENCOUNT_ENEMY_ID:0), DA:FIRST_ENCOUNT_ENEMY_ID:46)
	CALL PRINT_MESSAGE(FLAG:10, 61, DA:FIRST_ENCOUNT_ENEMY_ID:0, CALLNAME:(FLAG:10), ENEMY_NAME(DA:FIRST_ENCOUNT_ENEMY_ID:0), DA:FIRST_ENCOUNT_ENEMY_ID:46)
	CALL PRINT_MESSAGE(FLAG:11, 61, DA:FIRST_ENCOUNT_ENEMY_ID:0, CALLNAME:(FLAG:11), ENEMY_NAME(DA:FIRST_ENCOUNT_ENEMY_ID:0), DA:FIRST_ENCOUNT_ENEMY_ID:46)
ENDIF

;遭遇メッセージ
;初回遭遇時テキスト表示が行われている場合、口上との差異をなくす為遭遇エネミー名を使用
IF FIRST_ENCOUNT_ENEMY_ID && FLAG:508 == 0
	PRINTFORM %SAVESTR:(FIRST_ENCOUNT_ENEMY_ID)%
ELSE
	PRINTFORM %SAVESTR:(TFLAG:1)%
ENDIF
IF TFLAG:2
	PRINTFORM 達
ENDIF
PRINTFORML に遭遇した！
CALL WAIT_AUTOMODE(600, 0)

;戦闘開始テキスト
CHARA_ID_OF_AKUOCHI_ENEMY = GET_CHARA_ID_OF_AKUOCHI_ENEMY()
CHARA_ID_OF_DAUGHTER_ENEMY = GET_CHARA_ID_OF_DAUGHTER_ENEMY()
;敵パーティに悪堕ちキャラがいる
IF CHARA_ID_OF_AKUOCHI_ENEMY > 0
	CALL PRINT_MESSAGE(1, 54, TFLAG:0, CALLNAME:1, CALLNAME:CHARA_ID_OF_AKUOCHI_ENEMY, 1, CHARA_ID_OF_AKUOCHI_ENEMY)
	CALL PRINT_MESSAGE(FLAG:10, 54, TFLAG:0, CALLNAME:(FLAG:10), CALLNAME:CHARA_ID_OF_AKUOCHI_ENEMY, 1, CHARA_ID_OF_AKUOCHI_ENEMY)
	CALL PRINT_MESSAGE(FLAG:11, 54, TFLAG:0, CALLNAME:(FLAG:11), CALLNAME:CHARA_ID_OF_AKUOCHI_ENEMY, 1, CHARA_ID_OF_AKUOCHI_ENEMY)
;敵パーティに娘キャラがいる
ELSEIF CHARA_ID_OF_DAUGHTER_ENEMY
	CALL PRINT_MESSAGE(1, 54, TFLAG:0, CALLNAME:1, CALLNAME:CHARA_ID_OF_DAUGHTER_ENEMY, 2, CHARA_ID_OF_DAUGHTER_ENEMY)
	CALL PRINT_MESSAGE(FLAG:10, 54, TFLAG:0, CALLNAME:(FLAG:10), CALLNAME:CHARA_ID_OF_DAUGHTER_ENEMY, 2, CHARA_ID_OF_DAUGHTER_ENEMY)
	CALL PRINT_MESSAGE(FLAG:11, 54, TFLAG:0, CALLNAME:(FLAG:11), CALLNAME:CHARA_ID_OF_DAUGHTER_ENEMY, 2, CHARA_ID_OF_DAUGHTER_ENEMY)
;どちらも存在しない
ELSE
	CALL PRINT_MESSAGE(1, 54, TFLAG:0, CALLNAME:1, "", 0)
	CALL PRINT_MESSAGE(FLAG:10, 54, TFLAG:0, CALLNAME:(FLAG:10), "", 0)
	CALL PRINT_MESSAGE(FLAG:11, 54, TFLAG:0, CALLNAME:(FLAG:11), "", 0)
ENDIF

IF TFLAG:0 == 0
	CALL SHOW_TUTORIAL(9)
ELSE
	CALL SHOW_TUTORIAL(10)
ENDIF

;拘束状況描写
FOR LCOUNT, 1, ENEMY_COUNT + 1
	;敵が誰かを捕食してる
	IF DA:(TFLAG:(LCOUNT)):10
		PRINTFORML %CALLNAME:(DA:(TFLAG:(LCOUNT)):10)%が%SAVESTR:(TFLAG:(LCOUNT))%に捕らえられているようだ……
		CALL WAIT_AUTOMODE(400, 0)
		;PTメンバーを捕食している場合、捕食枠から拘束枠に移動
		;また初期状態を絶頂＋EP1にする
		IF DA:(TFLAG:(LCOUNT)):10 == 1
			CFLAG:(DA:(TFLAG:(LCOUNT)):10):20 = TFLAG:(LCOUNT)
			DA:(TFLAG:(LCOUNT)):20 = 1
			DA:(TFLAG:(LCOUNT)):70 = 1
			BASE:(DA:(TFLAG:(LCOUNT)):10):2 = 1
			CFLAG:(DA:(TFLAG:(LCOUNT)):10):35 = RAND:6 + 1
			CFLAG:(DA:(TFLAG:(LCOUNT)):10):10 = 0
			DA:(TFLAG:(LCOUNT)):10 = 0
		ELSEIF DA:(TFLAG:(LCOUNT)):10 == FLAG:10
			CFLAG:(DA:(TFLAG:(LCOUNT)):10):20 = TFLAG:(LCOUNT)
			DA:(TFLAG:(LCOUNT)):20 = FLAG:10
			DA:(TFLAG:(LCOUNT)):71 = FLAG:10
			BASE:(DA:(TFLAG:(LCOUNT)):10):2 = 1
			CFLAG:(DA:(TFLAG:(LCOUNT)):10):35 = RAND:6 + 1
			CFLAG:(DA:(TFLAG:(LCOUNT)):10):10 = 0
			DA:(TFLAG:(LCOUNT)):10 = 0
		ELSEIF DA:(TFLAG:(LCOUNT)):10 == FLAG:11
			CFLAG:(DA:(TFLAG:(LCOUNT)):10):20 = TFLAG:(LCOUNT)
			DA:(TFLAG:(LCOUNT)):20 = FLAG:11
			DA:(TFLAG:(LCOUNT)):72 = FLAG:11
			BASE:(DA:(TFLAG:(LCOUNT)):10):2 = 1
			CFLAG:(DA:(TFLAG:(LCOUNT)):10):35 = RAND:6 + 1
			CFLAG:(DA:(TFLAG:(LCOUNT)):10):10 = 0
			DA:(TFLAG:(LCOUNT)):10 = 0
		ELSE
			;PTメンバー以外を捕食している場合、拘束枠を開けておく
			IF DA:(TFLAG:(LCOUNT)):10 == DA:(TFLAG:(LCOUNT)):20
				CFLAG:(DA:(TFLAG:(LCOUNT)):20):20 = 0
				DA:(TFLAG:(LCOUNT)):20 = 0
			ENDIF
		ENDIF
	ELSE
		DA:(TFLAG:(LCOUNT)):20 = 0
	ENDIF
NEXT

TFLAG:11 = 1

;先制攻撃による行動不能処理
IF TFLAG:14 == 1
	PRINTFORML %CALLNAME:1%%TACHI()%の先制攻撃！
	CALL WAIT_AUTOMODE(200, 0)
	CALL PRINT_MESSAGE(1, 60, TFLAG:0, CALLNAME:1, "", 1)
	CALL PRINT_MESSAGE(FLAG:10, 60, TFLAG:0, CALLNAME:(FLAG:10), "", 1)
	CALL PRINT_MESSAGE(FLAG:11, 60, TFLAG:0, CALLNAME:(FLAG:11), "", 1)
	;敵全体にひるみを付加
	FOR LCOUNT, 1, ENEMY_COUNT + 1
		DA:(TFLAG:(LCOUNT)):24 = 1
	NEXT
ELSEIF TFLAG:14 == 2
	PRINTFORML 敵の不意打ちを受けた！
	CALL WAIT_AUTOMODE(200, 0)
	CALL PRINT_MESSAGE(1, 60, TFLAG:0, CALLNAME:1, "", 2)
	CALL PRINT_MESSAGE(FLAG:10, 60, TFLAG:0, CALLNAME:(FLAG:10), "", 2)
	CALL PRINT_MESSAGE(FLAG:11, 60, TFLAG:0, CALLNAME:(FLAG:11), "", 2)
	;味方全体にひるみを付加
	FOR LCOUNT, 0, 3
		SIF GET_MEMBER(LCOUNT)
			CFLAG:(GET_MEMBER(LCOUNT)):24 = 1
	NEXT
ELSEIF TFLAG:14 == 3
	PRINTFORML 敵に寝こみを襲われた！
	CALL WAIT_AUTOMODE(200, 0)
	CALL PRINT_MESSAGE(1, 60, TFLAG:0, CALLNAME:1, "", 3)
	CALL PRINT_MESSAGE(FLAG:10, 60, TFLAG:0, CALLNAME:(FLAG:10), "", 3)
	CALL PRINT_MESSAGE(FLAG:11, 60, TFLAG:0, CALLNAME:(FLAG:11), "", 3)
	;味方全体に睡眠を付加 気絶中なら昏睡になる
	;ただし【回復遅い】は起きている
	;1ターン目は殴られて起きても100％当たるように、ひるみも付加
	FOR LCOUNT, 0, 3
		IF GET_MEMBER(LCOUNT)
			CFLAG:GET_MEMBER(LCOUNT):25 = ((FLAG:8 % 10) >= 2) ? 2 # 1
			SIF TALENT:GET_MEMBER(LCOUNT):112
				CFLAG:GET_MEMBER(LCOUNT):25 = 0
			CFLAG:GET_MEMBER(LCOUNT):24 = 1
			
		ENDIF
	NEXT
ENDIF


IF TALENT:1:28
	CFLAG:1:34 = 1
ENDIF
IF FLAG:10 && TALENT:(FLAG:10):28 && CFLAG:(FLAG:10):29 == 0
	CFLAG:(FLAG:10):34 = 1
ENDIF
IF FLAG:11 && TALENT:(FLAG:11):28 && CFLAG:(FLAG:11):29 == 0
	CFLAG:(FLAG:11):34 = 1
ENDIF

CALL BATTLE_MAIN

BATTLE_RESULT = RESULT + 1
IF BATTLE_RESULT == 1
	;勝利メッセージ
	IF TFLAG:15 == 0
		;敵の数が0になっている＝エンディング処理が入って全部リセットされている
		RETURN
	ENDIF
	PRINTFORML %CALLNAME:1%%TACHI()%は戦闘に勝利した！
	CALL WAIT_AUTOMODE(800, 0)
	
	CHARA_ID_OF_AKUOCHI_ENEMY = GET_CHARA_ID_OF_AKUOCHI_ENEMY()
	CHARA_ID_OF_DAUGHTER_ENEMY =GET_CHARA_ID_OF_DAUGHTER_ENEMY()
	;敵パーティに悪堕ちキャラがいる
	IF CHARA_ID_OF_AKUOCHI_ENEMY > 0
		CALL PRINT_MESSAGE(1, 57, TFLAG:0, CALLNAME:1, CALLNAME:CHARA_ID_OF_AKUOCHI_ENEMY, 1, CHARA_ID_OF_AKUOCHI_ENEMY)
		CALL PRINT_MESSAGE(FLAG:10, 57, TFLAG:0, CALLNAME:(FLAG:10), CALLNAME:CHARA_ID_OF_AKUOCHI_ENEMY, 1, CHARA_ID_OF_AKUOCHI_ENEMY)
		CALL PRINT_MESSAGE(FLAG:11, 57, TFLAG:0, CALLNAME:(FLAG:11), CALLNAME:CHARA_ID_OF_AKUOCHI_ENEMY, 1, CHARA_ID_OF_AKUOCHI_ENEMY)
	;敵パーティに娘キャラがいる
	ELSEIF CHARA_ID_OF_DAUGHTER_ENEMY > 0
		CALL PRINT_MESSAGE(1, 57, TFLAG:0, CALLNAME:1, CALLNAME:CHARA_ID_OF_DAUGHTER_ENEMY, 2, CHARA_ID_OF_DAUGHTER_ENEMY)
		CALL PRINT_MESSAGE(FLAG:10, 57, TFLAG:0, CALLNAME:(FLAG:10), CALLNAME:CHARA_ID_OF_DAUGHTER_ENEMY, 2, CHARA_ID_OF_DAUGHTER_ENEMY)
		CALL PRINT_MESSAGE(FLAG:11, 57, TFLAG:0, CALLNAME:(FLAG:11), CALLNAME:CHARA_ID_OF_DAUGHTER_ENEMY, 2, CHARA_ID_OF_DAUGHTER_ENEMY)
	;どちらも存在しない
	ELSE
		CALL PRINT_MESSAGE(1, 57, TFLAG:0, CALLNAME:1, "", 0)
		CALL PRINT_MESSAGE(FLAG:10, 57, TFLAG:0, CALLNAME:(FLAG:10), "", 0)
		CALL PRINT_MESSAGE(FLAG:11, 57, TFLAG:0, CALLNAME:(FLAG:11), "", 0)
	ENDIF
	
	;好感度上昇
	CALL ADD_FEELING(FLAG:10, 5)
	CALL ADD_FEELING(FLAG:11, 5)
	;ペットパッチ用
	IF PET:0 == 0
		CALL PET_INVITE
	ELSE
		CALL ENEMY_TO_PET
	ENDIF
	;勝利時敵が仲間になることがある
	FOR LCOUNT, FLAG:1 - 1, 0, -1
		IF DA:LCOUNT:1 <= 0
			CALL DELETE_ENEMY_2(LCOUNT)
		ENDIF
	NEXT

	;勝利時は20％以上の確率でアイテム獲得
	VARSET ITEM_DROP_RATE
	IF RAND:5 == 0
		ITEM_DROP_RATE += 1
	ENDIF
	FOR LCOUNT, 0, 3
		IF GET_MEMBER(LCOUNT)
			;戦闘素質の低いキャラが居ると、アイテム入手率・ランクアップ
			IF RAND:10000 < 4000 - (ABL:(GET_MEMBER(LCOUNT)):98 + 10) * (ABL:(GET_MEMBER(LCOUNT)):98 + 10)
				ITEM_DROP_RATE += 1
			ENDIF
		ENDIF
	NEXT
	IF ITEM_DROP_RATE
		PRINTFORML 倒した敵が何か落としていったようだ…
		SELECTCASE RAND:(90 + ITEM_DROP_RATE * 10)
			CASE 0 TO 19
				CALL ADD_ITEM(50, 1, 1)
			CASE 20 TO 69
				CALL ADD_ITEM(54, 1, 1)
			CASE 70 TO 79
				CALL ADD_ITEM(52, 1, 1)
			CASE 80 TO 93
				PRINTFORML 眠気覚ましの薬草だ……%CALLNAME:1%%TACHI()%の眠気が吹き飛んだ！
				FLAG:7 = -1
			CASE 94 TO 95
				CALL ADD_ITEM(61, 1, 1)
			CASE 96 TO 97
				CALL ADD_ITEM(62, 1, 1)
			CASE 98 TO 99
				CALL ADD_ITEM(63, 1, 1)
			CASEELSE
				;高ランクアイテム
				SELECTCASE RAND:4
					CASE 0
						CALL ADD_ITEM(51, 1, 1)
					CASE 1
						CALL ADD_ITEM(53, 1, 1)
					CASE 2
						CALL ADD_ITEM(57, 1, 1)
					CASEELSE
						CALL ADD_ITEM(58, 1, 1)
				ENDSELECT
		ENDSELECT
		TWAIT FLAG:AUTOMODE * 500, 0
	ENDIF
	
	;念のため、拘束解除を入れておく 味方の攻撃で離した扱い
	CALL STATUS_RENEW_RELEASE(1, 0, 2)
	CALL STATUS_RENEW_RELEASE(FLAG:10, 0, 2)
	CALL STATUS_RENEW_RELEASE(FLAG:11, 0, 2)
ELSEIF BATTLE_RESULT == 2
	;敗北メッセージ
	PRINTFORML %CALLNAME:1%%TACHI()%は負けてしまった…
	CALL WAIT_AUTOMODE(1000, 0)
	CALL PRINT_MESSAGE(1, 56, TFLAG:0, CALLNAME:1, "")
	CALL PRINT_MESSAGE(FLAG:10, 56, TFLAG:0, CALLNAME:(FLAG:10), "")
	CALL PRINT_MESSAGE(FLAG:11, 56, TFLAG:0, CALLNAME:(FLAG:11), "")
	;好感度減少
	CALL ADD_FEELING(FLAG:10, -5)
	CALL ADD_FEELING(FLAG:11, -5)
	IF ITEM:64
		CALL ADD_ITEM(64, -1, 0)
		PRINTFORML %ITEMNAME:64%が輝き始めた！
		CALL WAIT_AUTOMODE(200, 0)
		CALL STATUS_RENEW_RELEASE(1, 0, 2)
		CALL STATUS_RENEW_RELEASE(FLAG:10, 0, 2)
		CALL STATUS_RENEW_RELEASE(FLAG:11, 0, 2)
		PRINTFORML %CALLNAME:1%%TACHI()%は%ITEMNAME:64%の効果で完全に回復し、何とかその場から逃げだした！
		CALL WAIT_AUTOMODE(500, 0)
		BATTLE_RESULT = 3
		FOR LCOUNT, 0, 3
			IF GET_MEMBER(LCOUNT)
				CFLAG:GET_MEMBER(LCOUNT):29 = 0
				BASE:GET_MEMBER(LCOUNT):0 = MAXBASE:GET_MEMBER(LCOUNT):0
				BASE:GET_MEMBER(LCOUNT):1 = MAXBASE:GET_MEMBER(LCOUNT):1
				BASE:GET_MEMBER(LCOUNT):2 = MAXBASE:GET_MEMBER(LCOUNT):2
				
				CFLAG:(GET_MEMBER(LCOUNT)):106 = 0
			ENDIF
		NEXT
		FOR LCOUNT, 1, TFLAG:15 + 1
			DA:(TFLAG:(LCOUNT)):70 = 0
			DA:(TFLAG:(LCOUNT)):71 = 0
			DA:(TFLAG:(LCOUNT)):72 = 0
		NEXT
	ELSEIF PET:0 && !TALENT:1:ペット && !RAND:5
		PRINTFORML %PETNAME%が愛想を尽かして去って行ってしまった……
		CALL WAIT_AUTOMODE(500, 0)
		VARSET PET
		VARSET PETNAME
		FOR LCOUNT, 0, CHARANUM
			CFLAG:LCOUNT:400 = 0
		NEXT
		TRYCALLFORM SET_ENEMY_DATA_99(0)
	ENDIF
ENDIF

PRINTFORML 

;ガーディアンに味方２人が捕まった状態で逃げると囮エンド
IF TFLAG:0 == 2 && BATTLE_RESULT == 3 && FLAG:11 && DA:4:10 > 1 && DA:4:20 > 1
	CALL GAMEOVER(2)
ENDIF

;戦闘終了後の後始末
IF TFLAG:0 == 1 && BATTLE_RESULT == 3 && DA:1:1 < DA:1:2 / 2 && FLAG:45 == 0
	;クイーン戦で、クイーンのHPを半分以上削った＋逃げた＋フラグが立っていない場合、あの人を追加
	;Jの時からのお付き合いなので、出さないわけにはいかないのです
	FLAG:45 = 1
	PRINTFORML 
	PRINTFORML ……？　なにやらクイーンの様子がおかしい
	PRINTFORML 何かに怯えているようにも見えるが…
	PRINTFORML [0] そんなの気のせい
	PRINTFORML [1] 周囲を警戒する(隠しキャラ追加)
	$INPUT_LOOP_REIMUSAMA
	TINPUT FLAG:AUTOMODE * 200, 1, 0, ""
	IF RESULT == 0
		PRINTFORML やはり気のせいだったようだ…
		CALL WAIT_AUTOMODE(200, 0)
		PRINTFORML 
	ELSEIF RESULT == 1
		PRINTFORML どうやら誰かがこの迷宮に新しく迷い込んだらしい…
		CALL WAIT_AUTOMODE(200, 0)
		PRINTFORML 
		
		; 霊夢様を追加
		ADDCHARA 299
		FLAG:0 += 1
		CALL CHARA_INITIALIZE_SINGLE(CHARANUM - 1)
		
		BASE:(CHARANUM - 1):0 = MAXBASE:(CHARANUM - 1):0
		BASE:(CHARANUM - 1):1 = MAXBASE:(CHARANUM - 1):1
		BASE:(CHARANUM - 1):2 = MAXBASE:(CHARANUM - 1):2

	ELSE
		REUSELASTLINE 入力値が不正です。再入力してください
		GOTO INPUT_LOOP_REIMUSAMA
	ENDIF
ENDIF

CALL AFTER_BATTLE(1)
CALL AFTER_BATTLE(FLAG:10)
CALL AFTER_BATTLE(FLAG:11)

FOR LCOUNT, 1, TFLAG:15 + 1
	CALL AFTER_BATTLE_E(TFLAG:(LCOUNT))
NEXT

;戦闘から逃走メッセージ
IF BATTLE_RESULT == 3
	;逃走成功・失敗キャラのIDを先頭詰めで保持
	VARSET ESCAPE_SUCCESS_CHARA_ID
	VARSET ESCAPE_FAIL_CHARA_ID
	FOR LCOUNT, 0, 3
		SIF !CFLAG:GET_MEMBER(LCOUNT):10
			ESCAPE_SUCCESS_CHARA_ID:LCOUNT = GET_MEMBER(LCOUNT)
		SIF CFLAG:GET_MEMBER(LCOUNT):10
			ESCAPE_FAIL_CHARA_ID:LCOUNT = GET_MEMBER(LCOUNT)
	NEXT
	FOR LCOUNT, 1, 3
		SIF ESCAPE_SUCCESS_CHARA_ID:(LCOUNT - 1) == 0
			SWAP ESCAPE_SUCCESS_CHARA_ID:LCOUNT, ESCAPE_SUCCESS_CHARA_ID:(LCOUNT - 1)
		SIF ESCAPE_FAIL_CHARA_ID:(LCOUNT - 1) == 0
			SWAP ESCAPE_FAIL_CHARA_ID:LCOUNT, ESCAPE_FAIL_CHARA_ID:(LCOUNT - 1)
	NEXT
	
	;PTメンバーは誰一人拘束されていない 普通に逃げたメッセージ表示
	IF !ESCAPE_FAIL_CHARA_ID:0
		CALL PRINT_MESSAGE(1, 55, TFLAG:0, CALLNAME:1, "")
		CALL PRINT_MESSAGE(FLAG:10, 55, TFLAG:0, CALLNAME:(FLAG:10), "")
		CALL PRINT_MESSAGE(FLAG:11, 55, TFLAG:0, CALLNAME:(FLAG:11), "")
	
	;誰かが拘束されたまま逃走
	ELSE
		;主人公逃走 その他のメンバー置き去り
		IF !CFLAG:1:10
			;メッセージ表示
			FOR LCOUNT, 0, 3
				IF GET_MEMBER(LCOUNT)
					IF !CFLAG:(GET_MEMBER(LCOUNT)):10
						CALL PRINT_MESSAGE(GET_MEMBER(LCOUNT), 62, TFLAG:0, CALLNAME:(GET_MEMBER(LCOUNT)), "", ESCAPE_FAIL_CHARA_ID:0, ESCAPE_FAIL_CHARA_ID:1, 0)
					ELSE
						CALL PRINT_MESSAGE(GET_MEMBER(LCOUNT), 63, TFLAG:0, CALLNAME:(GET_MEMBER(LCOUNT)), "", ESCAPE_SUCCESS_CHARA_ID:0, ESCAPE_SUCCESS_CHARA_ID:1, 1)
					ENDIF
				ENDIF
			NEXT
			
			;PTメンバー好感度変動
			FOR LCOUNT, 1, 3
				IF GET_MEMBER(LCOUNT)
					IF !CFLAG:(GET_MEMBER(LCOUNT)):10
						CALL ADD_FEELING(GET_MEMBER(LCOUNT), -10)
					ELSE
						CALL ADD_FEELING(GET_MEMBER(LCOUNT), -20)
						CFLAG:GET_MEMBER(LCOUNT):5 -= 50
					ENDIF
				ENDIF
			NEXT
			
			;置き去りにしたキャラ離脱処理
			;仲間2→仲間1の順に処理する事で、SWAP処理の手間を省く
			SIF FLAG:11 && CFLAG:(FLAG:11):10
				CALL MEMBER_EXIT_ID(FLAG:11)
			SIF FLAG:10 && CFLAG:(FLAG:10):10
				CALL MEMBER_EXIT_ID(FLAG:10)
			
		;PTメンバー逃走 主人公置き去り
		ELSE
			;メッセージ表示
			FOR LCOUNT, 0, 3
				IF GET_MEMBER(LCOUNT)
					IF !CFLAG:(GET_MEMBER(LCOUNT)):10
						CALL PRINT_MESSAGE(GET_MEMBER(LCOUNT), 62, TFLAG:0, CALLNAME:(GET_MEMBER(LCOUNT)), "", ESCAPE_FAIL_CHARA_ID:0, ESCAPE_FAIL_CHARA_ID:1, 1)
					ELSE
						CALL PRINT_MESSAGE(GET_MEMBER(LCOUNT), 63, TFLAG:0, CALLNAME:(GET_MEMBER(LCOUNT)), "", ESCAPE_SUCCESS_CHARA_ID:0, ESCAPE_SUCCESS_CHARA_ID:1, 0)
					ENDIF
				ENDIF
			NEXT
			
			;PTメンバー好感度変動
			FOR LCOUNT, 1, 3
				IF GET_MEMBER(LCOUNT)
					IF !CFLAG:(GET_MEMBER(LCOUNT)):10
						CALL ADD_FEELING(GET_MEMBER(LCOUNT), 20)
						CFLAG:GET_MEMBER(LCOUNT):5 += 50
					ENDIF
				ENDIF
			NEXT
			
			;逃走したキャラ離脱処理
			;仲間2→仲間1の順に処理する事で、SWAP処理の手間を省く
			SIF FLAG:11 && !CFLAG:(FLAG:11):10
				CALL MEMBER_EXIT_ID(FLAG:11)
			SIF FLAG:10 && !CFLAG:(FLAG:10):10
				CALL MEMBER_EXIT_ID(FLAG:10)
		ENDIF
		
		;---エンディング追加---
		IF CFLAG:1:10
			CALL ADD_HISTORY(DAY, DA:(CFLAG:1:10):0, FLAG:10, 14)
		ELSEIF CFLAG:(FLAG:10):10
			CALL ADD_HISTORY(DAY, DA:(CFLAG:(FLAG:10):10):0, 1, 15, FLAG:11)
		ELSEIF CFLAG:(FLAG:11):10
			CALL ADD_HISTORY(DAY, DA:(CFLAG:(FLAG:11):11):0, 1, 15, FLAG:10)
		ENDIF
	ENDIF
ENDIF


;終わったらTFLAGを掃除
VARSET TFLAG, 0

;HPが0になった敵を削除
FOR LCOUNT, FLAG:1 - 1, 0, -1
	IF DA:LCOUNT:1 <= 0
		CALL DELETE_ENEMY(LCOUNT)
	ENDIF
NEXT

;moge 戦闘終了時にpet:50移行の戦闘用一時変数置き場がクリアされてないのはまずい
;のでvarset
VARSET PET, 0, 50, 100

;ペットパッチ用 念のためDA:0をリセット
TRYCALLFORM SET_ENEMY_DATA_{PET:0}(0, PET:9) 

RETURN BATTLE_RESULT


;戦闘遭遇エネミーの選出処理
;TFLAGに入れるまでの処理のみ
;RESULT: 選出した敵の数
@SELECT_BATTLE_ENEMY
#DIM EXIST_ENEMY, 8, VARSIZE("DA", 1) ;遭遇可能な敵。1次元目は出現優先度（前に入ってる敵が先に出る）で振り分け
#DIM EE_LEN, 8 ;EXIST_ENEMYの各配列にどこまで要素が入っているか
#DIM ENEMY_COUNT ;何体の敵を選出したか
#DIM CURRENT ;現在選択中の敵
#DIM SELECTABLE ;CURRENTが選択可能か
#DIM LCOUNT, 2

VARSET EXIST_ENEMY
VARSET EE_LEN
VARSET ENEMY_COUNT

;同じ部屋にいる敵をEXIST_ENEMYに代入する
FOR LCOUNT, 1, FLAG:1
	IF DA:LCOUNT:4 == FLAG:5 && DA:LCOUNT:1 > 0
		;優先度0（最優先）-正体がばれているドッペルゲンガー
		IF DA:LCOUNT:0 == 18 && DA:LCOUNT:45
			EXIST_ENEMY:0:(EE_LEN:0) = LCOUNT
			EE_LEN:0 += 1
		;優先度2-バイオプラントとナイトメアケージ
		ELSEIF DA:LCOUNT:0 == 4 || DA:LCOUNT:0 == 43
			EXIST_ENEMY:2:(EE_LEN:2) = LCOUNT
			EE_LEN:2 += 1
		;優先度3-ガーディアン
		ELSEIF DA:LCOUNT:0 == 3
			EXIST_ENEMY:3:(EE_LEN:3) = LCOUNT
			EE_LEN:3 += 1
		;優先度4-ウォッチャー
		ELSEIF DA:LCOUNT:0 == 97
			EXIST_ENEMY:4:(EE_LEN:4) = LCOUNT
			EE_LEN:4 += 1
		;優先度5-ヒュプノ
		ELSEIF DA:LCOUNT:0 == 98
			EXIST_ENEMY:5:(EE_LEN:5) = LCOUNT
			EE_LEN:5 += 1
		;優先度6-クイーン
		ELSEIF DA:LCOUNT:0 == 1
			EXIST_ENEMY:6:(EE_LEN:6) = LCOUNT
			EE_LEN:6 += 1
		;優先度7-クイーンお供
		ELSEIF DA:LCOUNT:0 == 2
			EXIST_ENEMY:7:(EE_LEN:7) = LCOUNT
			EE_LEN:7 += 1
		;優先度1-その他の敵
		ELSEIF DA:LCOUNT:0 != 0
			EXIST_ENEMY:1:(EE_LEN:1) = LCOUNT
			EE_LEN:1 += 1
		ENDIF
	ENDIF
NEXT

;優先度の高い順に敵を選出（TFLAGへ代入）
FOR LCOUNT:0, 0, 8
	LOCAL = UNIQUE_RANDOM(-1)
	FOR LCOUNT:1, 0, EE_LEN:(LCOUNT:0)
		;遭遇最大値で終了
		IF ENEMY_COUNT >= MAX_ENCOUNT_ENEMY_NUMBER
			BREAK
		ENDIF
		;選出候補
		CURRENT = EXIST_ENEMY:(LCOUNT:0):(UNIQUE_RANDOM(EE_LEN:(LCOUNT:0)))
		;選出可能か
		SELECTCASE LCOUNT:0
			CASE 0, 1, 2, 3
				SELECTABLE = 1
			CASE 4 ;ウォッチャーは戦いにくい（他に敵がいないときに50%）が他キャラ拘束中は戦える
				SELECTABLE = ENEMY_COUNT >= RAND:2 || DA:CURRENT:20
			CASE 5, 6 ;ヒュプノ、クイーンは単体
				SELECTABLE = ENEMY_COUNT < 1
			CASE 7 ;クイーンのお供はフラグが立っている時だけ
				SELECTABLE = TFLAG:0 == 1
				;SELECTABLE = TFLAG:1 == 1
		ENDSELECT
		;可能なら選出した敵を格納
		IF SELECTABLE
			ENEMY_COUNT += 1
			TFLAG:ENEMY_COUNT = CURRENT
			;クイーン戦フラグ
			SIF LCOUNT:0 == 6
				TFLAG:0 = 1
			;ガーディアン戦フラグ
			SIF LCOUNT:0 == 3
				TFLAG:0 = 2
		ENDIF
	NEXT
NEXT
RETURN ENEMY_COUNT

;戦闘する敵の中に悪堕ちエネミーがいたら悪堕ち前キャラのキャラIDを返す
;いなかった場合は0を返す
@GET_CHARA_ID_OF_AKUOCHI_ENEMY
#FUNCTION
#DIM LCOUNT
FOR LCOUNT, 1, MAX_ENCOUNT_ENEMY_NUMBER + 1
	SIF DA:(TFLAG:LCOUNT):99>0
		RETURNF DA:(TFLAG:LCOUNT):46
NEXT
RETURNF 0

;戦闘する敵の中に娘エネミーがいたらその親キャラのキャラIDを返す
;いなかった場合は0を返す
@GET_CHARA_ID_OF_DAUGHTER_ENEMY
#FUNCTION
#DIM LCOUNT

FOR LCOUNT, 1, MAX_ENCOUNT_ENEMY_NUMBER + 1
	SIF GROUPMATCH(DA:(TFLAG:LCOUNT):0, 19)
		RETURNF DA:(TFLAG:LCOUNT):46
NEXT
RETURNF 0
