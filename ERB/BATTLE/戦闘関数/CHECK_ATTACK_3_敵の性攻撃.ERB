;攻撃（分類3＝敵の性攻撃）を処理
;ARG:0	攻撃側キャラ
;ARG:1	防御側キャラ
;RESULT:0	当たったかどうか（1で命中）
;RESULT:1	ダメージ
@CHECK_ATTACK_3(ARG:0, ARG:1)
;LOCAL
;0	LOOP
;1	当たったかどうか
;2	ダメージ
;3	攻撃力(今の所未使用)
;4	防御力(今の所未使用)
;5	属性倍率
;6	命中率
;7	TEMP 主にLOCAL:2のダメージ倍率別計算
;8	TEMP2


VARSET LOCAL, 0, 1, 9

;----- 攻撃力計算 -----
;基本値
LOCAL:2 = TFLAG:57 * TFLAG:58

;難易度補正
IF FLAG:4 == 2
	LOCAL:2 = LOCAL:2 * 12 / 10
ELSEIF FLAG:4 == 3
	LOCAL:2 = LOCAL:2 * 15 / 10
ENDIF

;防御側TP倍率
LOCAL:7 = 0
IF BASE:(ARG:1):4 >= 100
	LOCAL:7 = 100
ELSE
	LOCAL:7 = LIMIT(100 - (100 - BASE:(ARG:1):4) * 2 / 3, 33, 100)
ENDIF
LOCAL:2 = LOCAL:2 * LOCAL:7 / 100


;防御側CP倍率
LOCAL:7 = 0
LOCAL:8 = BASE:(ARG:1):3 * 100 / MAXBASE:(ARG:1):3
IF LOCAL:8 <= 0 || (LOCAL:8 <= 40 && FLAG:529 == 1)
	LOCAL:7 = 200
ELSEIF LOCAL:8 <= 33
	LOCAL:7 = 150
ELSEIF LOCAL:8 <= 66
	LOCAL:7 = 120
ELSE
	LOCAL:7 = 100
ENDIF
;はいてない
IF CFLAG:(ARG:1):44 == 1 && (TFLAG:63 == 0 || TFLAG:63 == 2 || TFLAG:63 == 3)
	LOCAL:7 = 200
;おもらし（小）
ELSEIF CFLAG:(ARG:1):44 == 2 && TFLAG:63 == 2
	LOCAL:7 = 150
;おもらし（大）
ELSEIF CFLAG:(ARG:1):44 == 3 && TFLAG:63 == 0
	LOCAL:7 = 200
;おもらし（小・大）
ELSEIF CFLAG:(ARG:1):44 == 4 && (TFLAG:63 == 0 || TFLAG:63 == 2)
	LOCAL:7 = 250
ENDIF
LOCAL:2 = LOCAL:2 * LOCAL:7 / 100


;防御中は威力0.5倍
IF CFLAG:(ARG:1):30
	LOCAL:2 = LOCAL:2 * 5 / 10
ENDIF

;溜め中は威力1.5倍
IF CFLAG:(ARG:1):31
	LOCAL:2 = LOCAL:2 * 15 / 10
ENDIF

;集中時は威力1.5倍
IF CFLAG:(ARG:1):32
	LOCAL:2 = LOCAL:2 * 15 / 10
ENDIF

;カウンター時は威力1.2倍
IF CFLAG:(ARG:1):33
	LOCAL:2 = LOCAL:2 * 12 / 10
ENDIF

;絶頂中は威力1.5倍
IF CFLAG:(ARG:1):35
	LOCAL:2 = LOCAL:2 * 15 / 10
ENDIF

;フラート後は威力3倍
IF DA:(ARG:0):35
	LOCAL:2 *= 3
ENDIF

;尻尾ガード
IF TALENT:(ARG:1):215 && (TFLAG:63 == 0 || TFLAG:63 == 3)
	LOCAL:2 = LOCAL:2 * (100 - 3 + (TALENT:(ARG:1):105 * 2)) / 100
ENDIF

;乱数加算および桁補正
LOCAL:2 = LOCAL:2 * (90 + RAND:11 + RAND:11) / 1000


;属性チェック
LOCAL:5 = 100

;ネコ舌にM攻撃するとHPが削れる　当たっていなくても削れるのは痛くてキャンセルしたとかで
IF TALENT:(ARG:1):54 && TFLAG:63 == 9
	DA:(ARG:0):1 -= DA:(ARG:0):2 * 5 / 100
ENDIF

LOCAL:2 = CALC_SENSITIVITY(ARG:1, TFLAG:63, LOCAL:2)

;汚染度補正
SELECTCASE FLAG:4
	CASE -1, 0
		SELECTCASE BATTLE_POLUTION()
			CASE 0
				LOCAL:5 = LOCAL:5 * 5 / 10
			CASE 1
				LOCAL:5 = LOCAL:5 * 7 / 10
			CASE 2, 3
			CASE 4
				LOCAL:5 = LOCAL:5 * 11 / 10
			CASE 5
				LOCAL:5 = LOCAL:5 * 12 / 10
		ENDSELECT
	CASE 1
		SELECTCASE BATTLE_POLUTION()
			CASE 0
				LOCAL:5 = LOCAL:5 * 6 / 10
			CASE 1
				LOCAL:5 = LOCAL:5 * 8 / 10
			CASE 2
				;普通なので何もなし
			CASE 3
				LOCAL:5 = LOCAL:5 * 11 / 10
			CASE 4
				LOCAL:5 = LOCAL:5 * 13 / 10
			CASE 5
				LOCAL:5 = LOCAL:5 * 15 / 10
		ENDSELECT
	CASE 2, 3
		SELECTCASE BATTLE_POLUTION()
			CASE 0
				LOCAL:5 = LOCAL:5 * 7 / 10
			CASE 1
				LOCAL:5 = LOCAL:5 * 9 / 10
			CASE 2
				;普通なので何もなし
			CASE 3
				LOCAL:5 = LOCAL:5 * 12 / 10
			CASE 4
				LOCAL:5 = LOCAL:5 * 14 / 10
			CASE 5
				LOCAL:5 = LOCAL:5 * 16 / 10
		ENDSELECT
ENDSELECT

;命中率チェック
CALL GET_HIT_RATE_ENEMY(ARG:0, ARG:1, 3)
LOCAL:6 = RESULT

;★部位焦らし判定 0ではまずいので、５０～
IF CFLAG:(ARG:1):305 == (50 + TFLAG:63) && CFLAG:(ARG:1):306 > 0
;赤
	SETCOLOR 0xEE1010
	PRINTFORML 焦らされていた%CALLNAME:(ARG:1)%のカラダはより強い快感を受けた！
	RESETCOLOR
;攻撃力の倍率
	LOCAL:5 = LOCAL:5 * (100 + CFLAG:(ARG:1):306 * 50) / 100

;一回で焦らしを解除するかどうかはバランス次第？
	CFLAG:(ARG:1):306 -= 9
	IF CFLAG:(ARG:1):306 < 1
		CFLAG:(ARG:1):306 = 0
	ENDIF

;衣服ダメージまで増加することに注意
	TFLAG:65 /= 10
ENDIF

;該当する所を防御していれば、威力と命中率減少（拘束状態でも避けられる）
IF CFLAG:(ARG:1):50 == (50 + TFLAG:63) && !CFLAG:(ARG:1):35
	PRINTFORML %CALLNAME:(ARG:1)%は激しく抵抗した！
	LOCAL:5 /= 3
	LOCAL:6 = LIMIT(LOCAL:6 / 2, 80, 90)
ELSEIF CFLAG:(ARG:1):50 != (50 + TFLAG:63) && CFLAG:(ARG:1):50 >= 50 && CFLAG:(ARG:1):50 <= 53 && CFLAG:(ARG:1):35 == 0
	;場所を間違えると威力が少し高くなる
	PRINTFORML 別の場所に注意を払っていた%CALLNAME:(ARG:1)%はより強い快感を受けた！
	LOCAL:5 = LOCAL:5 * 4 / 3
ELSEIF CFLAG:(ARG:1):50 >= 50 && CFLAG:(ARG:1):50 <= 53 && CFLAG:(ARG:1):35
	PRINTFORML %CALLNAME:(ARG:1)%は絶頂で力が抜けてしまい、抵抗できないようだ…
ENDIF


PRINTFORML 命中率：{LOCAL:6}％

IF RAND:100 < LOCAL:6
	LOCAL:1 = 1
ENDIF

IF LOCAL:1
	LOCAL:2 = LOCAL:2 * LOCAL:5 / 100
ELSE
	LOCAL:2 = 0
ENDIF

RETURN LOCAL:1, LOCAL:2

