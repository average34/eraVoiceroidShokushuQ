;敵全体の簡易ステータス表示
@SHOW_ENEMY_STATUS
#DIM LCOUNT

FOR LCOUNT, 1, TFLAG:15 + 1
	CALL SHOW_SIMPLE_STATUS_E(TFLAG:LCOUNT)
NEXT


;敵の簡易ステータス表示
;ARG:0	表示するキャラのID（TFLAGではなく、DAのID）
@SHOW_SIMPLE_STATUS_E(ENEMY_ID)
#DIMS ENEMY_NAME
#DIM ENEMY_ID
#DIM ENEMY_HP
#DIM TOOLTIP_PRINTED_FLAG

VARSET TOOLTIP_PRINTED_FLAG
	
IF DA:ENEMY_ID:1 <= 0
	SETCOLOR 0x333333
ENDIF

;敵の名前が22バイト超えたら改行
ENEMY_NAME = 【%SAVESTR:ENEMY_ID%】
STRLENS ENEMY_NAME
IF RESULT < 23
	PRINTFORM %ENEMY_NAME, 22, LEFT%  
ELSE
	PRINTFORML %ENEMY_NAME%  
	CALL SET_TOOLTIP_ENEMY_WIKI(ENEMY_ID)
	TOOLTIP_PRINTED_FLAG = 1
ENDIF

RESETCOLOR

;HPゲージのみ表示する　残量に応じて色を変更
PRINTFORM HP 

IF FLAG:4 == -1
	;very easyはここでHPを8割に　耐久力減少と逃走成功率増加が同時に完成　ばれないようにHPバーにも細工
	SIF DA:ENEMY_ID:1 > DA:ENEMY_ID:2 * 4 / 5
		DA:ENEMY_ID:1 = DA:ENEMY_ID:2 * 4 / 5
	ENEMY_HP = DA:ENEMY_ID:1 * 125 / DA:ENEMY_ID:2

	IF ENEMY_HP > 90
		CALL PRINT_COLORBAR(DA:ENEMY_ID:1, DA:ENEMY_ID:2 * 4 / 5, 20, BARCOLORSET("青緑"), RESULT:1)
	ELSEIF ENEMY_HP > 50
		CALL PRINT_COLORBAR(DA:ENEMY_ID:1, DA:ENEMY_ID:2 * 4 / 5, 20, BARCOLORSET("緑"), RESULT:1)
	ELSEIF ENEMY_HP > 0
		CALL PRINT_COLORBAR(DA:ENEMY_ID:1, DA:ENEMY_ID:2 * 4 / 5, 20, BARCOLORSET("黄"), RESULT:1)
	ELSE
		CALL PRINT_COLORBAR(DA:ENEMY_ID:1, DA:ENEMY_ID:2 * 4 / 5, 20, BARCOLORSET("赤"), RESULT:1)
	ENDIF
ELSE
	ENEMY_HP = DA:ENEMY_ID:1 * 100 / DA:ENEMY_ID:2

	IF ENEMY_HP > 90
		CALL PRINT_COLORBAR(DA:ENEMY_ID:1, DA:ENEMY_ID:2, 20, BARCOLORSET("青緑"), RESULT:1)
	ELSEIF ENEMY_HP > 50
		CALL PRINT_COLORBAR(DA:ENEMY_ID:1, DA:ENEMY_ID:2, 20, BARCOLORSET("緑"), RESULT:1)
	ELSEIF ENEMY_HP > 0
		CALL PRINT_COLORBAR(DA:ENEMY_ID:1, DA:ENEMY_ID:2, 20, BARCOLORSET("黄"), RESULT:1)
	ELSE
		CALL PRINT_COLORBAR(DA:ENEMY_ID:1, DA:ENEMY_ID:2, 20, BARCOLORSET("赤"), RESULT:1)
	ENDIF
ENDIF
IF FLAG:9
	PRINTFORM {DA:ENEMY_ID:1} / {DA:ENEMY_ID:2}
ENDIF
PRINTFORM    
IF DA:ENEMY_ID:10
	IF DA:ENEMY_ID:36
		PRINTFORM %CALLNAME:(DA:ENEMY_ID:36)%捕虜中 
	ELSE
		PRINTFORM %CALLNAME:(DA:ENEMY_ID:10)%拘束中
	ENDIF 
ENDIF
IF DA:ENEMY_ID:20
	IF DA:ENEMY_ID:36
		PRINTFORM %CALLNAME:(DA:ENEMY_ID:36)%捕虜中 
	ELSE
		PRINTFORM %CALLNAME:(DA:ENEMY_ID:20)%拘束中 
	ENDIF
ENDIF
IF DA:ENEMY_ID:35
	PRINTFORM 興奮 
ENDIF
IF DA:ENEMY_ID:80
	PRINTFORM 速度↓({DA:ENEMY_ID:80}) 
ENDIF
IF DA:ENEMY_ID:81
	PRINTFORM 回避↓({DA:ENEMY_ID:81}) 
ENDIF
PRINTL  

SIF !TOOLTIP_PRINTED_FLAG
	CALL SET_TOOLTIP_ENEMY_WIKI(ENEMY_ID)


; 一行分の表示を取得してツールチップ付で再表示
@SET_TOOLTIP_ENEMY_WIKI(ENEMY_ID)
#DIM  ENEMY_ID
#DIMS PRINTED_STR_DATA
#DIMS ENEMY_WIKI_TEXT, 100
#DIMS ENEMY_WIKI_TEXT_ONE_LINE
#DIM  ENEMY_WIKI_LINE_COUNT
#DIM  LCOUNT

VARSET ENEMY_WIKI_TEXT_ONE_LINE

;名前・種別
ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_NAME(DA:ENEMY_ID:0)%　　　　　　
SIF DA:ENEMY_ID:85 & 1
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% 女性
SIF DA:ENEMY_ID:85 & 128
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% 男性
SIF DA:ENEMY_ID:85 & 2
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% 植物
SIF DA:ENEMY_ID:85 & 4
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% 動物
SIF DA:ENEMY_ID:85 & 8
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% 丸呑
SIF DA:ENEMY_ID:85 & 16
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% 海産
SIF DA:ENEMY_ID:85 & 32
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% ローパー
SIF DA:ENEMY_ID:85 & 64
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% スライム
SIF DA:ENEMY_ID:85 & 256
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% 罠
SIF DA:ENEMY_ID:85 & 512
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE% 機械
ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE%系\n
ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE%------------------------------------------------------------------------------\n

;図鑑文章
CALL ENEMY_WIKI_TEXT(DA:ENEMY_ID:0, ENEMY_WIKI_TEXT, ENEMY_WIKI_LINE_COUNT)
FOR LCOUNT, 0, ENEMY_WIKI_LINE_COUNT
	ENEMY_WIKI_TEXT_ONE_LINE = %ENEMY_WIKI_TEXT_ONE_LINE%%ENEMY_WIKI_TEXT:LCOUNT%\n
NEXT

PRINTED_STR_DATA = %HTML_GETPRINTEDSTR(0)%
REPLACE PRINTED_STR_DATA, "【", @"<nonbutton title='%ENEMY_WIKI_TEXT_ONE_LINE%'>【"
REPLACE RESULTS, "】", "】</nonbutton>"
PRINTED_STR_DATA = %RESULTS%

CLEARLINE 1
HTML_PRINT @"%PRINTED_STR_DATA%"
