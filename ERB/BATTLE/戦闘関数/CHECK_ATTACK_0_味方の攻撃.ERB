;攻撃（分類0＝味方攻撃）を処理
;RESULT:0	当たったかどうか（1で命中）
;RESULT:1	ダメージ
@CHECK_ATTACK_0(CHARA_ID, ENEMY_ID)
#DIM CHARA_ID
#DIM ENEMY_ID
#DIM DAMAGE

	VARSET DAMAGE
	
	; ダメージ算出
	CALL CHECK_ATTACK_DAMAGE_0(CHARA_ID, ENEMY_ID)
	DAMAGE = RESULT

	;命中率チェック 使用スキルで分岐
	;通常攻撃
	IF TFLAG:56 == 0 || TFLAG:56 == 4
		CALL GET_HIT_RATE_PT(CHARA_ID, ENEMY_ID, 3)
	;それ以外の攻撃
	ELSE
		CALL GET_HIT_RATE_PT(CHARA_ID, ENEMY_ID, 4)
	ENDIF
	PRINTFORML 命中率：{RESULT}％

	;命中した
	IF RAND:100 < RESULT
		;ヒットした攻撃が火属性の場合、敵に火傷状態が＋1蓄積される(ダメージ0でも)
		SIF TFLAG:56 == 3 || TFLAG:56 == 4
			DA:ENEMY_ID:37 += 1
		RETURN 1, DAMAGE
	ENDIF
	
;攻撃が命中しなかった場合はダメージ0として処理する
RETURN 0, 0


;ダメージ算出用
;攻撃処理では攻撃を回避された際ダメージ0が返る
;直接ダメージ値が欲しい際に使えないので分岐
@CHECK_ATTACK_DAMAGE_0(CHARA_ID, ENEMY_ID)
#DIM CHARA_ID
#DIM ENEMY_ID
#DIM SKILL_TYPE       ;通常攻撃か、それ以外の攻撃か 戦闘レベル参照に利用
#DIM ATTRIBUTE_ATTACK ;攻撃・炎攻撃・消耗等、攻撃属性の補正値計算用
#DIM DAMAGE

	VARSET DAMAGE
	VARSET ATTRIBUTE_ATTACK

	;使用スキルによるタイプ判別
	;攻撃の場合、攻撃戦闘レベルを参照
	IF TFLAG:56 == 0 || TFLAG:56 == 4
		SKILL_TYPE = 3
	;それ以外の攻撃の場合、道具戦闘レベルを参照
	ELSE
		SKILL_TYPE = 4
	ENDIF
	
	;----- 攻撃力計算 -----
	;基本値算出(最低でも1とする)
	IF !CFLAG:CHARA_ID:303
		DAMAGE = LIMIT((25 + TFLAG:57 / 2) * TFLAG:58, 1, __INT_MAX__)
	;ダメージ低下フラグON
	ELSE
		DAMAGE = LIMIT((25 + TFLAG:57 / 2) * TFLAG:58 / 2, 1, __INT_MAX__)
	ENDIF
	
	;HPが0だと少し威力が下がる
	IF BASE:CHARA_ID:0 <= 0
		TIMES DAMAGE, 0.9
	ENDIF

	;骨折レベルが１以上だと威力減衰
	IF CFLAG:CHARA_ID:201 >= 1
		;アリスの人形と霊夢様は80％(娘は面倒なので対象外)
		IF GROUPMATCH(NO:CHARA_ID, 88, 89, 299)
			TIMES DAMAGE, 0.8
		;種族的に身体が頑丈と思われるキャラは70％(淫ピ仙人は鬼として扱う、娘は面倒なので対象外)
		ELSEIF GROUPMATCH(NO:CHARA_ID, 10, 11, 23, 36, 43, 44, 52, 62, 63, 73, 80, 85, 96, 101)
			TIMES DAMAGE, 0.7
		;それ以外でも【痛みに強い】または【気丈】だと60％
		ELSEIF TALENT:CHARA_ID:12 || TALENT:CHARA_ID:41
			TIMES DAMAGE, 0.6
		;それ以外は半分に
		ELSE
			TIMES DAMAGE, 0.5
		ENDIF
	ENDIF

	;TP補正
	;V1.00から低TPによる威力減少をマイルドに
	IF BASE:CHARA_ID:TP >= 100
		DAMAGE = DAMAGE * BASE:CHARA_ID:TP / 100
	ELSE
		DAMAGE = DAMAGE * (100 - (100 - BASE:CHARA_ID:TP) / 4) / 100
	ENDIF

	;使用スキルの対応レベル補正
	SELECTCASE ABL:CHARA_ID:SKILL_TYPE
	CASE 0
		TIMES DAMAGE, 1.0
	CASE 1
		TIMES DAMAGE, 1.2
	CASE 2
		TIMES DAMAGE, 1.5
	CASEELSE
		TIMES DAMAGE, 2.0
	ENDSELECT

	;媚薬装備中は威力2倍
	CALL GET_EQUIP_BONUS(CHARA_ID, 39)
	SIF RESULT
		TIMES DAMAGE, 2.0

	;前進中は威力1.2倍
	SIF CFLAG:CHARA_ID:34
		TIMES DAMAGE, 1.0

	;難易度補正
	SELECTCASE FLAG:4
	;Easy以下
	CASE IS <= 0
		TIMES DAMAGE, 1.7
	;Normal
	CASE 1
		TIMES DAMAGE, 1.2
	;Hard
	CASE 2
		TIMES DAMAGE, 1.0
	;Lunatic
	CASE 3
		TIMES DAMAGE, 0.8
	ENDSELECT
	
	;EASYのみ、クイーン戦はさらに威力２倍
	SIF FLAG:4 <= 0 && TFLAG:0 == 1
		TIMES DAMAGE, 2.0
	;EASYのみ、ガーディアン戦はさらに威力1.5倍
	SIF FLAG:4 <= 0 && TFLAG:0 == 2
		TIMES DAMAGE, 1.5

	;消耗武器が割と不遇なのでこっそり威力補正
	SIF TFLAG:56 == 2 || TFLAG:56 == 3
		TIMES DAMAGE, 1.2

	;眠気ペナルティ
	CALL GET_EQUIP_BONUS(CHARA_ID, 37)
	IF CHARA_ID != FLAG:15 && CHARA_ID != FLAG:16 && RESULT == 1
		IF FLAG:7 >= 11
			TIMES DAMAGE, 0.2
		ELSEIF FLAG:7 >= 7
			TIMES DAMAGE, 0.5
		ENDIF
	ENDIF

	;服装補正
	CALL CLOTH_TYPE_EFFECT_攻撃補正(CHARA_ID)
	SIF RESULT > 0
		DAMAGE = DAMAGE * (100 + RESULT:1) / 100

	;乱数加算および桁補正
	DAMAGE = DAMAGE * (90 + RAND:11 + RAND:11) / 1000

	;敵の防御力＝ランク　低いとダメージ増　ランク1つ下がるごとにダメージ補正5％
	DAMAGE = DAMAGE * ((7 - DA:ENEMY_ID:86) * 5 + 100) / 100

	;攻撃属性による補正
	SELECTCASE TFLAG:56
	;攻撃
	CASE 0
		ATTRIBUTE_ATTACK = DA:ENEMY_ID:40
	;非消耗
	CASE 1
		ATTRIBUTE_ATTACK = DA:ENEMY_ID:44
	;消耗
	CASE 2
		ATTRIBUTE_ATTACK = DA:ENEMY_ID:42
	;炎
	CASE 3
		ATTRIBUTE_ATTACK = DA:ENEMY_ID:43
	;炎攻撃
	CASE 4
		ATTRIBUTE_ATTACK = DA:ENEMY_ID:41
	ENDSELECT

	;補正率未指定なら100%
	IF ATTRIBUTE_ATTACK == 0
		ATTRIBUTE_ATTACK = 100
	;補正率-1なら攻撃無効
	ELSEIF ATTRIBUTE_ATTACK == -1
		ATTRIBUTE_ATTACK = 0
	ENDIF

	;攻撃属性による補正は難易度・部屋の汚染度に影響を受ける
	SELECTCASE FLAG:4
	CASE -1, 0
		SELECTCASE BATTLE_POLUTION()
		CASE 0
			TIMES ATTRIBUTE_ATTACK, 1.7
		CASE 1
			TIMES ATTRIBUTE_ATTACK, 1.4
		CASE 2, 3
		CASE 4
			TIMES ATTRIBUTE_ATTACK, 0.8
		CASE 5
			TIMES ATTRIBUTE_ATTACK, 0.7
		ENDSELECT
	CASE 1
		SELECTCASE BATTLE_POLUTION()
		CASE 0
			TIMES ATTRIBUTE_ATTACK, 1.5
		CASE 1
			TIMES ATTRIBUTE_ATTACK, 1.3
		CASE 2
			;普通なので何もなし
		CASE 3
			TIMES ATTRIBUTE_ATTACK, 0.8
		CASE 4
			TIMES ATTRIBUTE_ATTACK, 0.7
		CASE 5
			TIMES ATTRIBUTE_ATTACK, 0.5
		ENDSELECT
	CASE 2, 3
		SELECTCASE BATTLE_POLUTION()
		CASE 0
			TIMES ATTRIBUTE_ATTACK, 1.5
		CASE 1
			TIMES ATTRIBUTE_ATTACK, 1.3
		CASE 2
			;普通なので何もなし
		CASE 3
			TIMES ATTRIBUTE_ATTACK, 0.7
		CASE 4
			TIMES ATTRIBUTE_ATTACK, 0.6
		CASE 5
			TIMES ATTRIBUTE_ATTACK, 0.4
		ENDSELECT
	ENDSELECT
	DAMAGE = DAMAGE * ATTRIBUTE_ATTACK / 100
	
RETURN DAMAGE