; AI戦闘コマンド 通常攻撃 ばら撒き弾
; コマンドの実行率を0.00%～100.00%で返す
; emueraは小数が使えないので*100した値(0-10000)を扱う
@GET_COMMAND_EXECUTION_RATE_14(CHARA_ID, TARGET_ID, SUPPORT_VALUE)
#DIM CHARA_ID
#DIM TARGET_ID
#DIM SUPPORT_VALUE ; Reserved
#DIM RATE
#DIM DATA_TABLE, 15, 5
#DIM DAMAGE
#DIM HIT_RATE
#DIM ENEMY_HP

	VARSET RATE
	
	; 想定攻撃力を取得
	CALL CHECK_ATTACKED_DAMAGE(CHARA_ID, 14, TARGET_ID)
	DAMAGE = RESULT
	
	; 想定命中率を取得
	CALL CHECK_ATTACKED_HIT_RATE(CHARA_ID, 14, TARGET_ID)
	HIT_RATE = RESULT
	
	; 想定敵HPを取得
	CALL CHECK_ATTACKED_ENEMY_HP(TARGET_ID)
	ENEMY_HP = RESULT
	
	; ターゲットが存在しないか、HP0以下による対象不適正
	SIF TARGET_ID == 0 || !MATCH(TFLAG, TARGET_ID, 1, 11) || DA:TARGET_ID:1 <= 0
		RETURN 0
		
	; 性格別コマンド実行基礎値(攻撃的な性格別に並べ替え済)
	;                超弱気    弱気    普通    強気  超強気
	DATA_TABLE:10:0 =  3900,   3500,   3100,   2700,   2300  ; 10:ねっけつかん
	DATA_TABLE:2:0  =  4000,   3600,   3200,   2800,   2400  ; 02:おてんば
	DATA_TABLE:14:0 =  4100,   3700,   3300,   2900,   2500  ; 14:わがまま
	DATA_TABLE:3:0  =  4200,   3800,   3400,   3000,   2600  ; 03:がんこもの
	DATA_TABLE:6:0  =  4300,   3900,   3500,   3100,   2700  ; 06:くろうにん
	DATA_TABLE:4:0  =  4400,   4000,   3600,   3200,   2800  ; 04:がんばりや
	DATA_TABLE:5:0  =  4500,   4100,   3700,   3300,   2900  ; 05:きれもの
	DATA_TABLE:1:0  =  4600,   4200,   3800,   3400,   3000  ; 01:おせっかい
	DATA_TABLE:0:0  =  4700,   4300,   3900,   3500,   3100  ; 00:ふつう
	DATA_TABLE:9:0  =  4800,   4400,   4000,   3600,   3200  ; 09:ぬけめがない
	DATA_TABLE:8:0  =  4900,   4500,   4100,   3700,   3300  ; 08:セクシーギャル
	DATA_TABLE:7:0  =  5000,   4600,   4200,   3800,   3400  ; 07:しあわせもの
	DATA_TABLE:11:0 =  4900,   4500,   4100,   3700,   3300  ; 11:のんきもの
	DATA_TABLE:13:0 =  4800,   4400,   4000,   3600,   3200  ; 13:やさしいひと
	DATA_TABLE:12:0 =  4700,   4300,   3900,   3500,   3100  ; 12:ひっこみじあん
	RATE = AI_COMMAND_DATATABLE_TO_RATE(CHARA_ID, DATA_TABLE)
	
	; この攻撃で敵を倒せそう(他の味方が先に倒してそうな敵は除外)
	SIF ENEMY_HP > 0 && ENEMY_HP <= DAMAGE
		RATE += 1000
		
	; 命中率による補正
	SELECTCASE HIT_RATE
	CASE IS <= 20
		TIMES RATE, 0.5
	CASE IS <= 40
		TIMES RATE, 0.6
	CASE IS <= 60
		TIMES RATE, 0.8
	CASE IS <= 80
		TIMES RATE, 1.0
	CASE IS <= 100
		TIMES RATE, 1.2
	CASEELSE
		TIMES RATE, 1.4
	ENDSELECT
	
	; 一番HPが少ない敵を優先的に狙う
	SIF CHECK_HP_MIN_LIVE_MEMBER_ENEMY(TARGET_ID)
		TIMES RATE, 1.2
		
	; 戦闘素質による補正
	SELECTCASE ABL:CHARA_ID:98
	CASE IS <= 10
		TIMES RATE, 2.0
	CASE IS <= 20
		TIMES RATE, 1.8
	CASE IS <= 30
		TIMES RATE, 1.6	
	CASE IS <= 40
		TIMES RATE, 1.4
	CASE IS <= 50
		TIMES RATE, 1.2
	CASE IS <= 60
		TIMES RATE, 1.0
	CASE IS <= 70
		TIMES RATE, 0.9
	CASE IS <= 80
		TIMES RATE, 0.8
	CASEELSE
		TIMES RATE, 0.6
	ENDSELECT
		
RETURN LIMIT(RATE, 0, 10000)
