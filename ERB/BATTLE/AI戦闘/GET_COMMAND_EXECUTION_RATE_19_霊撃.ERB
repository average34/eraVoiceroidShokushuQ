; AI戦闘コマンド 通常攻撃 霊撃
; コマンドの実行率を0.00%～100.00%で返す
; emueraは小数が使えないので*100した値(0-10000)を扱う
@GET_COMMAND_EXECUTION_RATE_19(CHARA_ID, TARGET_ID, SUPPORT_VALUE)
#DIM CHARA_ID
#DIM TARGET_ID
#DIM SUPPORT_VALUE ; Reserved
#DIM RATE
#DIM DATA_TABLE, 15, 5
#DIM DAMAGE
#DIM HIT_RATE
#DIM ENEMY_HP

	VARSET RATE
	
	; 想定攻撃力を取得
	CALL CHECK_ATTACKED_DAMAGE(CHARA_ID, 19, TARGET_ID)
	DAMAGE = RESULT
	
	; 想定命中率を取得
	CALL CHECK_ATTACKED_HIT_RATE(CHARA_ID, 19, TARGET_ID)
	HIT_RATE = RESULT
	
	; 想定敵HPを取得
	CALL CHECK_ATTACKED_ENEMY_HP(TARGET_ID)
	ENEMY_HP = RESULT
	
	; 拘束時の霊撃は自分を拘束している敵以外は攻撃対象にできない
	IF CFLAG:CHARA_ID:10 || CFLAG:CHARA_ID:20
		SIF !(DA:TARGET_ID:10 == CHARA_ID || DA:TARGET_ID:20 == CHARA_ID)
			RETURN 0
	ENDIF
	
	; 性格別コマンド実行基礎値(攻撃的な性格別に並べ替え済)
	;                超弱気    弱気    普通    強気  超強気
	DATA_TABLE:10:0 =  4800,   4400,   4000,   3600,   3200  ; 10:ねっけつかん
	DATA_TABLE:2:0  =  4900,   4500,   4100,   3700,   3300  ; 02:おてんば
	DATA_TABLE:14:0 =  5000,   4600,   4200,   3800,   3400  ; 14:わがまま
	DATA_TABLE:3:0  =  4900,   4500,   4100,   3700,   3300  ; 03:がんこもの
	DATA_TABLE:6:0  =  4800,   4400,   4000,   3600,   3200  ; 06:くろうにん
	DATA_TABLE:4:0  =  4700,   4300,   3900,   3500,   3100  ; 04:がんばりや
	DATA_TABLE:5:0  =  4600,   4200,   3800,   3400,   3000  ; 05:きれもの
	DATA_TABLE:1:0  =  4500,   4100,   3700,   3300,   2900  ; 01:おせっかい
	DATA_TABLE:0:0  =  4400,   4000,   3600,   3200,   2800  ; 00:ふつう
	DATA_TABLE:9:0  =  4300,   3900,   3500,   3100,   2700  ; 09:ぬけめがない
	DATA_TABLE:8:0  =  4200,   3800,   3400,   3000,   2600  ; 08:セクシーギャル
	DATA_TABLE:7:0  =  4100,   3700,   3300,   2900,   2500  ; 07:しあわせもの
	DATA_TABLE:11:0 =  4000,   3600,   3200,   2800,   2400  ; 11:のんきもの
	DATA_TABLE:13:0 =  3900,   3500,   3100,   2700,   2300  ; 13:やさしいひと
	DATA_TABLE:12:0 =  3800,   3400,   3000,   2600,   2200  ; 12:ひっこみじあん
	RATE = AI_COMMAND_DATATABLE_TO_RATE(CHARA_ID, DATA_TABLE)
	
	; 霊撃は拘束時・通常時共に使用可能なので分岐
	; 拘束時
	IF CFLAG:CHARA_ID:10 || CFLAG:CHARA_ID:20
		
		; 残りHPが少ないなら撃てなくなる前に撃つ
		SELECTCASE BASE:CHARA_ID:HP
		; 66%以上
		CASE IS >= (MAXBASE:CHARA_ID:HP / 100 * 66)
			; 補正なし
		; 33%以上
		CASE IS >= (MAXBASE:CHARA_ID:HP / 100 * 33)
			TIMES RATE, 1.2
		; 0%以上
		CASE IS >= 0
			TIMES RATE, 1.5
		ENDSELECT
		
		; 残りMPが少ないならなんとしても逃げ出したい
		SELECTCASE BASE:CHARA_ID:MP
		; 66%以上
		CASE IS >= (MAXBASE:CHARA_ID:MP / 100 * 66)
			; 補正なし
		; 33%以上
		CASE IS >= (MAXBASE:CHARA_ID:MP / 100 * 33)
			TIMES RATE, 1.5
		; 0%以上
		CASE IS >= 0
			TIMES RATE, 2.5
		ENDSELECT
		
	; 通常時
	ELSE
		; 敵が誰かを拘束・丸呑みしているなら解放を狙う
		SIF DA:TARGET_ID:10 || DA:TARGET_ID:20 || DA:TARGET_ID:21
			RATE += 1000
			
		; この攻撃で敵を倒せそう(他の味方が先に倒してそうな敵は除外)
		SIF ENEMY_HP > 0 && ENEMY_HP <= DAMAGE
			RATE += 1000
			
		; 通常時の霊撃はHPが減る割に、あまり当たらないので倍率を下げる
		TIMES RATE, 0.6
		
		; 残りHPが少ないなら温存するより攻撃に回す
		SELECTCASE BASE:CHARA_ID:HP
		; 80%以上
		CASE IS >= (MAXBASE:CHARA_ID:HP / 100 * 80)
			TIMES RATE, 0.5
		; 60%以上
		CASE IS >= (MAXBASE:CHARA_ID:HP / 100 * 60)
			TIMES RATE, 0.6
		; 40%以上
		CASE IS >= (MAXBASE:CHARA_ID:HP / 100 * 40)
			TIMES RATE, 0.8
		; 20%以上
		CASE IS >= (MAXBASE:CHARA_ID:HP / 100 * 20)
			TIMES RATE, 1.0
		; 0%以上
		CASE IS >= 0
			TIMES RATE, 1.2
		ENDSELECT
		
		; 霊撃を撃った後の残りHPに余裕があるキャラは撃ちやすい
		SELECTCASE BASE:CHARA_ID:HP - (ABL:CHARA_ID:98 * MAXBASE:CHARA_ID:HP / 100)
		; 66%以上
		CASE IS >= (MAXBASE:CHARA_ID:HP / 100 * 66)
			TIMES RATE, 1.5
		; 33%以上
		CASE IS >= (MAXBASE:CHARA_ID:HP / 100 * 33)
			TIMES RATE, 1.2
		; それ以下
		CASEELSE
			; 補正なし
		ENDSELECT
		
		; 命中率による補正
		SELECTCASE HIT_RATE
		CASE IS <= 20
			TIMES RATE, 0.5
		CASE IS <= 40
			TIMES RATE, 0.6
		CASE IS <= 60
			TIMES RATE, 0.8
		CASE IS <= 80
			TIMES RATE, 1.0
		CASE IS <= 100
			TIMES RATE, 1.2
		CASEELSE
			TIMES RATE, 1.4
		ENDSELECT
		
		; 一番HPが少ない敵を優先的に狙う
		SIF CHECK_HP_MIN_LIVE_MEMBER_ENEMY(TARGET_ID)
			TIMES RATE, 1.2
			
		; 戦闘素質による補正
		SELECTCASE ABL:CHARA_ID:98
		CASE IS <= 10
			TIMES RATE, 0.6
		CASE IS <= 20
			TIMES RATE, 0.7
		CASE IS <= 30
			TIMES RATE, 0.8	
		CASE IS <= 40
			TIMES RATE, 0.9
		CASE IS <= 50
			TIMES RATE, 1.0
		CASE IS <= 60
			TIMES RATE, 1.2
		CASE IS <= 70
			TIMES RATE, 1.4
		CASE IS <= 80
			TIMES RATE, 1.8
		CASEELSE
			TIMES RATE, 2.0
		ENDSELECT
	ENDIF
	
RETURN LIMIT(RATE, 0, 10000)
