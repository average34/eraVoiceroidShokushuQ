; AI戦闘コマンド 消耗武器 みだれうち
; コマンドの実行率を0.00%～100.00%で返す
; emueraは小数が使えないので*100した値(0-10000)を扱う
@GET_COMMAND_EXECUTION_RATE_24(CHARA_ID, TARGET_ID, SUPPORT_VALUE)
#DIM LCOUNT
#DIM CHARA_ID
#DIM TARGET_ID
#DIM SUPPORT_VALUE ; 装備部位の情報が付与されたままのコマンド番号 装備品1:コマンド番号+100 装備品2:コマンド番号+200
#DIM RATE
#DIM DATA_TABLE, 15, 5
#DIM TEMP
#DIM HIT_RATE
#DIM DAMAGE

	VARSET RATE
	
	; 想定攻撃力を取得
	CALL CHECK_ATTACKED_DAMAGE(CHARA_ID, SUPPORT_VALUE, TARGET_ID)
	DAMAGE = RESULT
	
	; 想定命中率を取得
	CALL CHECK_ATTACKED_HIT_RATE(CHARA_ID, SUPPORT_VALUE, TARGET_ID)
	HIT_RATE = RESULT
	
	; 性格別コマンド実行基礎値(攻撃的な性格別に並べ替え済)
	;                超弱気    弱気    普通    強気  超強気
	DATA_TABLE:10:0 =  4000,   4400,   4800,   4400,   4000  ; 10:ねっけつかん
	DATA_TABLE:2:0  =  4100,   4500,   4900,   4500,   4100  ; 02:おてんば
	DATA_TABLE:14:0 =  4200,   4600,   5000,   4600,   4200  ; 14:わがまま
	DATA_TABLE:3:0  =  4100,   4500,   4900,   4500,   4100  ; 03:がんこもの
	DATA_TABLE:6:0  =  4000,   4400,   4800,   4400,   4000  ; 06:くろうにん
	DATA_TABLE:4:0  =  3900,   4300,   4700,   4300,   3900  ; 04:がんばりや
	DATA_TABLE:5:0  =  3800,   4200,   4600,   4200,   3800  ; 05:きれもの
	DATA_TABLE:1:0  =  3700,   4100,   4500,   4100,   3700  ; 01:おせっかい
	DATA_TABLE:0:0  =  3600,   4000,   4400,   4000,   3600  ; 00:ふつう
	DATA_TABLE:9:0  =  3500,   3900,   4300,   3900,   3500  ; 09:ぬけめがない
	DATA_TABLE:8:0  =  3400,   3800,   4200,   3800,   3400  ; 08:セクシーギャル
	DATA_TABLE:7:0  =  3300,   3700,   4100,   3700,   3300  ; 07:しあわせもの
	DATA_TABLE:11:0 =  3200,   3600,   4000,   3600,   3200  ; 11:のんきもの
	DATA_TABLE:13:0 =  3100,   3500,   3900,   3500,   3100  ; 13:やさしいひと
	DATA_TABLE:12:0 =  3000,   3400,   3800,   3400,   3000  ; 12:ひっこみじあん
	RATE = AI_COMMAND_DATATABLE_TO_RATE(CHARA_ID, DATA_TABLE)
	
	; 敵複数体がまだ生存しているなら補正
	VARSET TEMP
	FOR LCOUNT, 1, (TFLAG:15 + 1)
		SIF DA:(TFLAG:LCOUNT):1 > 0
			TEMP++
	NEXT
	SIF TEMP >= 2
		RATE += 1200
	
	; 命中率による補正
	SELECTCASE HIT_RATE
	CASE IS <= 20
		TIMES RATE, 0.5
	CASE IS <= 40
		TIMES RATE, 0.6
	CASE IS <= 60
		TIMES RATE, 0.8
	CASE IS <= 80
		TIMES RATE, 1.0
	CASE IS <= 100
		TIMES RATE, 1.2
	CASEELSE
		TIMES RATE, 1.4
	ENDSELECT
	
	; 敵を一体でも倒せるなら補正大
	FOR LCOUNT, 1, TFLAG:15 + 1
		IF DA:(TFLAG:LCOUNT):1 > 0 && DA:(TFLAG:LCOUNT):1 <= DAMAGE
			TIMES RATE, 1.7
		ENDIF
	NEXT
	
	; 戦闘素質による補正
	SELECTCASE ABL:CHARA_ID:98
	CASE IS <= 10
		TIMES RATE, 0.6
	CASE IS <= 20
		TIMES RATE, 0.7
	CASE IS <= 30
		TIMES RATE, 0.8	
	CASE IS <= 40
		TIMES RATE, 0.9
	CASE IS <= 50
		TIMES RATE, 1.0
	CASE IS <= 60
		TIMES RATE, 1.2
	CASE IS <= 70
		TIMES RATE, 1.4
	CASE IS <= 80
		TIMES RATE, 1.8
	CASEELSE
		TIMES RATE, 2.0
	ENDSELECT
	
RETURN LIMIT(RATE, 0, 10000)
