; AI戦闘コマンド 非消耗武器 弱攻撃
; コマンドの実行率を0.00%～100.00%で返す
; emueraは小数が使えないので*100した値(0-10000)を扱う
@GET_COMMAND_EXECUTION_RATE_30(CHARA_ID, TARGET_ID, SUPPORT_VALUE)
#DIM CHARA_ID
#DIM TARGET_ID
#DIM SUPPORT_VALUE ; 装備部位の情報が付与されたままのコマンド番号 装備品1:コマンド番号+100 装備品2:コマンド番号+200
#DIM RATE
#DIM DATA_TABLE, 15, 5
#DIM DAMAGE
#DIM HIT_RATE
#DIM ENEMY_HP

	VARSET RATE
	
	; 想定攻撃力を取得
	CALL CHECK_ATTACKED_DAMAGE(CHARA_ID, SUPPORT_VALUE, TARGET_ID)
	DAMAGE = RESULT
	
	; 想定命中率を取得
	CALL CHECK_ATTACKED_HIT_RATE(CHARA_ID, SUPPORT_VALUE, TARGET_ID)
	HIT_RATE = RESULT
	
	; 想定敵HPを取得
	CALL CHECK_ATTACKED_ENEMY_HP(TARGET_ID)
	ENEMY_HP = RESULT
	
	; ターゲットが存在しないか、HP0以下による対象不適正
	SIF TARGET_ID == 0 || !MATCH(TFLAG, TARGET_ID, 1, 11) || DA:TARGET_ID:1 <= 0
		RETURN 0
		
	; 上位攻撃 中攻撃の命中率が100%を超えているなら弱攻撃を行う理由がない
	CALL CHECK_ATTACKED_HIT_RATE(CHARA_ID, SUPPORT_VALUE + 1, TARGET_ID)
	SIF RESULT >= 100
		RETURN 0
		
	; 性格別コマンド実行基礎値(攻撃的な性格別に並べ替え済)
	;                超弱気    弱気    普通    強気  超強気
	DATA_TABLE:10:0 =  2600,   3000,   3400,   3800,   4200  ; 10:ねっけつかん
	DATA_TABLE:2:0  =  2700,   3100,   3500,   3900,   4300  ; 02:おてんば
	DATA_TABLE:14:0 =  2800,   3200,   3600,   4000,   4400  ; 14:わがまま
	DATA_TABLE:3:0  =  2900,   3300,   3700,   4100,   4500  ; 03:がんこもの
	DATA_TABLE:6:0  =  3000,   3400,   3800,   4200,   4600  ; 06:くろうにん
	DATA_TABLE:4:0  =  3100,   3500,   3900,   4300,   4700  ; 04:がんばりや
	DATA_TABLE:5:0  =  3200,   3600,   4000,   4400,   4800  ; 05:きれもの
	DATA_TABLE:1:0  =  3300,   3700,   4100,   4500,   4900  ; 01:おせっかい
	DATA_TABLE:0:0  =  3400,   3800,   4200,   4600,   5000  ; 00:ふつう
	DATA_TABLE:9:0  =  3300,   3700,   4100,   4500,   4900  ; 09:ぬけめがない
	DATA_TABLE:8:0  =  3200,   3600,   4000,   4400,   4800  ; 08:セクシーギャル
	DATA_TABLE:7:0  =  3100,   3500,   3900,   4300,   4700  ; 07:しあわせもの
	DATA_TABLE:11:0 =  3000,   3400,   3800,   4200,   4600  ; 11:のんきもの
	DATA_TABLE:13:0 =  2900,   3300,   3700,   4100,   4500  ; 13:やさしいひと
	DATA_TABLE:12:0 =  2800,   3200,   3600,   4000,   4400  ; 12:ひっこみじあん
	RATE = AI_COMMAND_DATATABLE_TO_RATE(CHARA_ID, DATA_TABLE)
	
	; 同LV他攻撃の攻撃力が、より強い場合使用率を下げる
	CALL CHECK_ATTACKED_DAMAGE(CHARA_ID, 10, TARGET_ID)
	SIF RESULT > DAMAGE
		RATE -= RAND:1000
	CALL CHECK_ATTACKED_DAMAGE(CHARA_ID, 120, TARGET_ID)
	SIF SUPPORT_VALUE != 120 && RESULT > DAMAGE
		RATE -= RAND:1000
	CALL CHECK_ATTACKED_DAMAGE(CHARA_ID, 220, TARGET_ID)
	SIF SUPPORT_VALUE != 220 && RESULT > DAMAGE
		RATE -= RAND:1000
	CALL CHECK_ATTACKED_DAMAGE(CHARA_ID, 130, TARGET_ID)
	SIF SUPPORT_VALUE != 130 && RESULT > DAMAGE
		RATE -= RAND:1000
	CALL CHECK_ATTACKED_DAMAGE(CHARA_ID, 230, TARGET_ID)
	SIF SUPPORT_VALUE != 230 && RESULT > DAMAGE
		RATE -= RAND:1000
		
	; 敵が誰かを拘束・丸呑みしているなら解放を狙う
	SIF DA:TARGET_ID:10 || DA:TARGET_ID:20 || DA:TARGET_ID:21
		RATE += 1000
		
	; この攻撃で敵を倒せそう(他の味方が先に倒してそうな敵は除外)
	SIF ENEMY_HP > 0 && ENEMY_HP <= DAMAGE
		RATE += 1000
	
	; 命中率による補正
	SELECTCASE HIT_RATE
	CASE IS <= 20
		TIMES RATE, 0.5
	CASE IS <= 40
		TIMES RATE, 0.6
	CASE IS <= 60
		TIMES RATE, 0.8
	CASE IS <= 80
		TIMES RATE, 1.0
	CASE IS <= 100
		TIMES RATE, 1.2
	CASEELSE
		TIMES RATE, 1.4
	ENDSELECT
	
	; 一番HPが少ない敵を優先的に狙う
	SIF CHECK_HP_MIN_LIVE_MEMBER_ENEMY(TARGET_ID)
		TIMES RATE, 1.2
		
	; 戦闘素質による補正
	SELECTCASE ABL:CHARA_ID:98
	CASE IS <= 10
		TIMES RATE, 0.6
	CASE IS <= 20
		TIMES RATE, 0.7
	CASE IS <= 30
		TIMES RATE, 0.8	
	CASE IS <= 40
		TIMES RATE, 0.9
	CASE IS <= 50
		TIMES RATE, 1.0
	CASE IS <= 60
		TIMES RATE, 1.2
	CASE IS <= 70
		TIMES RATE, 1.4
	CASE IS <= 80
		TIMES RATE, 1.8
	CASEELSE
		TIMES RATE, 2.0
	ENDSELECT
		
RETURN LIMIT(RATE, 0, 10000)
