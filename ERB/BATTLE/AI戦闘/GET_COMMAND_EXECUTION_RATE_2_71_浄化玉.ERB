; AI戦闘コマンド アイテム 浄化玉
; コマンドの実行率を0.00%～100.00%で返す
; emueraは小数が使えないので*100した値(0-10000)を扱う
@GET_COMMAND_EXECUTION_RATE_2_71(CHARA_ID, TARGET_ID)
#DIM CHARA_ID
#DIM TARGET_ID
#DIM RATE
#DIM DATA_TABLE, 15, 5
#DIM POLLUTION

	VARSET RATE
	
	; 部屋の想定汚染度を取得
	CALL CHECK_CLEANED_POLLUTION()
	POLLUTION = RESULT
	
	; 使えるアイテムストックがない
	SIF CHECK_STOCK_ITEM(71) == 0
		RETURN 0
	
	; 想定汚染度が0なら浄化の必要なし
	SIF POLLUTION == 0
		RETURN 0
		
	; 性格別コマンド実行基礎値(攻撃的な性格別に並べ替え済)
	;                超弱気    弱気    普通    強気  超強気
	DATA_TABLE:10:0 =  2300,   2700,   3100,   3500,   3900  ; 10:ねっけつかん
	DATA_TABLE:2:0  =  2400,   2800,   3200,   3600,   4000  ; 02:おてんば
	DATA_TABLE:14:0 =  2500,   2900,   3300,   3700,   4100  ; 14:わがまま
	DATA_TABLE:3:0  =  2600,   3000,   3400,   3800,   4200  ; 03:がんこもの
	DATA_TABLE:6:0  =  2700,   3100,   3500,   3900,   4300  ; 06:くろうにん
	DATA_TABLE:4:0  =  2800,   3200,   3600,   4000,   4400  ; 04:がんばりや
	DATA_TABLE:5:0  =  2900,   3300,   3700,   4100,   4500  ; 05:きれもの
	DATA_TABLE:1:0  =  3000,   3400,   3800,   4200,   4600  ; 01:おせっかい
	DATA_TABLE:0:0  =  3100,   3500,   3900,   4300,   4700  ; 00:ふつう
	DATA_TABLE:9:0  =  3200,   3600,   4000,   4400,   4800  ; 09:ぬけめがない
	DATA_TABLE:8:0  =  3300,   3700,   4100,   4500,   4900  ; 08:セクシーギャル
	DATA_TABLE:7:0  =  3400,   3800,   4200,   4600,   5000  ; 07:しあわせもの
	DATA_TABLE:11:0 =  3300,   3700,   4100,   4500,   4900  ; 11:のんきもの
	DATA_TABLE:13:0 =  3200,   3600,   4000,   4400,   4800  ; 13:やさしいひと
	DATA_TABLE:12:0 =  3100,   3500,   3900,   4300,   4700  ; 12:ひっこみじあん
	RATE = AI_COMMAND_DATATABLE_TO_RATE(CHARA_ID, DATA_TABLE)
	
	; 残り浄化力による補正
	SELECTCASE LIMIT(FLAG:6 - (CFLAG:1:50 == 8 ? 50 # 0) - (CFLAG:(FLAG:10):50 == 8 ? 50 # 0) - (CFLAG:(FLAG:11):50 == 8 ? 50 # 0), 0, 300)
	CASE 300
		TIMES RATE, 0.5
	CASE IS >= 200
		TIMES RATE, 0.8
	CASE IS >= 100
		TIMES RATE, 1.0
	CASE IS >= 0
		TIMES RATE, 1.2
	ENDSELECT
	
	; 部屋汚染度による補正
	SELECTCASE POLLUTION
	CASE IS <= 9
		TIMES RATE, 0.1
	CASE IS <= 19
		TIMES RATE, 0.2
	CASE IS <= 39
		TIMES RATE, 0.5
	CASE IS <= 59
		TIMES RATE, 1.0
	CASE IS <= 79
		TIMES RATE, 1.7
	CASEELSE
		TIMES RATE, 2.0
	ENDSELECT
	
	; TPによる補正
	; 冷静な程まわりが見える
	SELECTCASE BASE:CHARA_ID:TP
	CASE IS >= 160
		TIMES RATE, 0.4
	CASE IS >= 120
		TIMES RATE, 0.7
	CASE IS >= 80
		TIMES RATE, 1.0
	CASE IS >= 40
		TIMES RATE, 1.3
	CASEELSE
		TIMES RATE, 1.6
	ENDSELECT
	
	; 戦闘素質による補正
	SELECTCASE ABL:CHARA_ID:98
	CASE IS <= 10
		TIMES RATE, 2.0
	CASE IS <= 20
		TIMES RATE, 1.8
	CASE IS <= 30
		TIMES RATE, 1.6	
	CASE IS <= 40
		TIMES RATE, 1.4
	CASE IS <= 50
		TIMES RATE, 1.2
	CASE IS <= 60
		TIMES RATE, 1.0
	CASE IS <= 70
		TIMES RATE, 0.9
	CASE IS <= 80
		TIMES RATE, 0.8
	CASEELSE
		TIMES RATE, 0.6
	ENDSELECT
	
RETURN LIMIT(RATE, 0, 10000)
