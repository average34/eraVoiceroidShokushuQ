; AI戦闘コマンド アイテム 興奮剤
; コマンドの実行率を0.00%～100.00%で返す
; emueraは小数が使えないので*100した値(0-10000)を扱う
@GET_COMMAND_EXECUTION_RATE_2_56(CHARA_ID, TARGET_ID)
#DIM CHARA_ID
#DIM TARGET_ID
#DIM RATE
#DIM DATA_TABLE, 15, 5
#DIM TARGET_TP

	; 戦闘素質の高いキャラが低TPに陥っている場合に使用、発情付与の効果あり
	; 発情中は行動が勝手に書き換わる為、現在発情中のキャラには使用しない

	VARSET RATE
	
	; 想定TPを取得
	CALL CHECK_RECOVERED_TP(TARGET_ID)
	TARGET_TP = RESULT
	
	; 使えるアイテムストックがない
	SIF CHECK_STOCK_ITEM(56) == 0
		RETURN 0
	
	; 対象キャラが存在しない
	SIF !TARGET_ID
		RETURN 0
	
	; 拘束中のキャラTPを変動させても意味がない
	SIF CFLAG:TARGET_ID:10 || CFLAG:TARGET_ID:20
		RETURN 0
		
	; 丸呑み中のキャラにアイテムは届かない
	SIF CFLAG:TARGET_ID:21
		RETURN 0
		
	; 興奮剤の効果で発情中のキャラを
	; 追加発情させるとまともに行動出来なくなるので除外
	SIF CFLAG:TARGET_ID:28
		RETURN 0
	
	; 喪失中のキャラの回復は無意味
	SIF CFLAG:TARGET_ID:29
		RETURN 0
		
	; 対象キャラのTPが150以上なら無効
	SIF TARGET_TP >= 150
		RETURN 0
		
	; 性格別コマンド実行基礎値(攻撃的な性格別に並べ替え済)
	;                超弱気    弱気    普通    強気  超強気
	DATA_TABLE:10:0 =  4900,   4500,   4100,   3700,   3300  ; 10:ねっけつかん
	DATA_TABLE:2:0  =  5000,   4600,   4200,   3800,   3400  ; 02:おてんば
	DATA_TABLE:14:0 =  4900,   4500,   4100,   3700,   3300  ; 14:わがまま
	DATA_TABLE:3:0  =  4800,   4400,   4000,   3600,   3200  ; 03:がんこもの
	DATA_TABLE:6:0  =  4700,   4300,   3900,   3500,   3100  ; 06:くろうにん
	DATA_TABLE:4:0  =  4600,   4200,   3800,   3400,   3000  ; 04:がんばりや
	DATA_TABLE:5:0  =  4500,   4100,   3700,   3300,   2900  ; 05:きれもの
	DATA_TABLE:1:0  =  4400,   4000,   3600,   3200,   2800  ; 01:おせっかい
	DATA_TABLE:0:0  =  4300,   3900,   3500,   3100,   2700  ; 00:ふつう
	DATA_TABLE:9:0  =  4200,   3800,   3400,   3000,   2600  ; 09:ぬけめがない
	DATA_TABLE:8:0  =  4100,   3700,   3300,   2900,   2500  ; 08:セクシーギャル
	DATA_TABLE:7:0  =  4000,   3600,   3200,   2800,   2400  ; 07:しあわせもの
	DATA_TABLE:11:0 =  3900,   3500,   3100,   2700,   2300  ; 11:のんきもの
	DATA_TABLE:13:0 =  3800,   3400,   3000,   2600,   2200  ; 13:やさしいひと
	DATA_TABLE:12:0 =  3700,   3300,   2900,   2500,   2100  ; 12:ひっこみじあん
	RATE = AI_COMMAND_DATATABLE_TO_RATE(CHARA_ID, DATA_TABLE)
	
	; 対象TPによる補正
	SELECTCASE TARGET_TP
	CASE IS <= 50
		TIMES RATE, 1.25
	CASE IS <= 80
		TIMES RATE, 1.0
	CASE IS <= 119
		TIMES RATE, 0.75
	CASE IS <= 149
		TIMES RATE, 0.5
	CASEELSE
		; コマンド無効化済
	ENDSELECT
	
	; 対象戦闘素質による補正
	SELECTCASE ABL:TARGET_ID:98
	CASE IS <= 20
		TIMES RATE, 0.4
	CASE IS <= 40
		TIMES RATE, 0.6
	CASE IS <= 60
		TIMES RATE, 0.8
	CASE IS <= 80
		TIMES RATE, 1.25
	CASEELSE
		TIMES RATE, 1.7
	ENDSELECT
	
	; TPによる補正
	; 冷静な程まわりが見える
	SELECTCASE BASE:CHARA_ID:TP
	CASE IS >= 160
		TIMES RATE, 0.4
	CASE IS >= 120
		TIMES RATE, 0.7
	CASE IS >= 80
		TIMES RATE, 1.0
	CASE IS >= 40
		TIMES RATE, 1.3
	CASEELSE
		TIMES RATE, 1.6
	ENDSELECT
	
RETURN LIMIT(RATE, 0, 10000)
