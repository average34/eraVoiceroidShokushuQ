; AI戦闘コマンド アイテム 玉露
; コマンドの実行率を0.00%～100.00%で返す
; emueraは小数が使えないので*100した値(0-10000)を扱う
@GET_COMMAND_EXECUTION_RATE_2_51(CHARA_ID, TARGET_ID)
#DIM CHARA_ID
#DIM TARGET_ID
#DIM RATE
#DIM DATA_TABLE, 15, 5
#DIM TARGET_HP

	VARSET RATE
	
	; 想定HPを取得
	CALL CHECK_RECOVERED_HP(TARGET_ID)
	TARGET_HP = RESULT
	
	; 使えるアイテムストックがない
	SIF CHECK_STOCK_ITEM(51) == 0
		RETURN 0
	
	; 対象キャラが存在しない
	SIF !TARGET_ID
		RETURN 0
	
	; 丸呑み中のキャラにアイテムは届かない
	SIF CFLAG:TARGET_ID:21
		RETURN 0
		
	; 喪失中のキャラの回復は無意味
	SIF CFLAG:TARGET_ID:29
		RETURN 0
		
	; 残HP 70%以上なら回復不要
	SIF TARGET_HP >= (MAXBASE:TARGET_ID:HP / 100 * 70)
		RETURN 0
		
	; 性格別コマンド実行基礎値(攻撃的な性格別に並べ替え済)
	;                超弱気    弱気    普通    強気  超強気
	DATA_TABLE:10:0 =  2600,   3000,   3400,   3800,   3400  ; 10:ねっけつかん
	DATA_TABLE:2:0  =  2700,   3100,   3500,   3900,   3500  ; 02:おてんば
	DATA_TABLE:14:0 =  2800,   3200,   3600,   4000,   3600  ; 14:わがまま
	DATA_TABLE:3:0  =  2900,   3300,   3700,   4100,   3700  ; 03:がんこもの
	DATA_TABLE:6:0  =  3000,   3400,   3800,   4200,   3800  ; 06:くろうにん
	DATA_TABLE:4:0  =  3100,   3500,   3900,   4300,   3900  ; 04:がんばりや
	DATA_TABLE:5:0  =  3200,   3600,   4000,   4400,   4000  ; 05:きれもの
	DATA_TABLE:1:0  =  3300,   3700,   4100,   4500,   4100  ; 01:おせっかい
	DATA_TABLE:0:0  =  3400,   3800,   4200,   4600,   4200  ; 00:ふつう
	DATA_TABLE:9:0  =  3500,   3900,   4300,   4700,   4300  ; 09:ぬけめがない
	DATA_TABLE:8:0  =  3600,   4000,   4400,   4800,   4400  ; 08:セクシーギャル
	DATA_TABLE:7:0  =  3700,   4100,   4500,   4900,   4500  ; 07:しあわせもの
	DATA_TABLE:11:0 =  3800,   4200,   4600,   5000,   4600  ; 11:のんきもの
	DATA_TABLE:13:0 =  3700,   4100,   4500,   4900,   4500  ; 13:やさしいひと
	DATA_TABLE:12:0 =  3600,   4000,   4400,   4800,   4400  ; 12:ひっこみじあん
	RATE = AI_COMMAND_DATATABLE_TO_RATE(CHARA_ID, DATA_TABLE)
	
	; 自分に対しての回復行動率を少し高めに
	SIF CHARA_ID == TARGET_ID
		RATE += 500
		
	; HPの切れた拘束中キャラに霊撃チャンスを与える
	SIF CFLAG:TARGET_ID:20 && BASE:TARGET_ID:HP == 0
		RATE += 500
	
	; 戦闘素質の低いキャラは霊撃の消耗が少ないので優先回復したい
	SELECTCASE ABL:TARGET_ID:98
	CASE IS <= 20
		TIMES RATE, 1.5
	CASE IS <= 40
		TIMES RATE, 1.2
	CASE IS <= 60
		TIMES RATE, 1.0
	CASE IS <= 80
		TIMES RATE, 0.8
	CASEELSE
		TIMES RATE, 0.5
	ENDSELECT
	
	; 残りHPによる補正
	SELECTCASE TARGET_HP
	CASE IS >= (MAXBASE:TARGET_ID:HP / 100 * 80)
		TIMES RATE, 0.1
	CASE IS >= (MAXBASE:TARGET_ID:HP / 100 * 60)
		TIMES RATE, 0.4
	CASE IS >= (MAXBASE:TARGET_ID:HP / 100 * 40)
		TIMES RATE, 0.6
	CASE IS >= (MAXBASE:TARGET_ID:HP / 100 * 20)
		TIMES RATE, 0.8
	CASE IS >  0
		TIMES RATE, 1.1
	CASE 0
		TIMES RATE, 1.7
	ENDSELECT
	
	; TPによる補正
	; 冷静な程まわりが見える
	SELECTCASE BASE:CHARA_ID:TP
	CASE IS >= 160
		TIMES RATE, 0.4
	CASE IS >= 120
		TIMES RATE, 0.7
	CASE IS >= 80
		TIMES RATE, 1.0
	CASE IS >= 40
		TIMES RATE, 1.3
	CASEELSE
		TIMES RATE, 1.6
	ENDSELECT
	
	; 戦闘素質による補正
	SELECTCASE ABL:CHARA_ID:98
	CASE IS <= 10
		TIMES RATE, 2.0
	CASE IS <= 20
		TIMES RATE, 1.8
	CASE IS <= 30
		TIMES RATE, 1.6	
	CASE IS <= 40
		TIMES RATE, 1.4
	CASE IS <= 50
		TIMES RATE, 1.2
	CASE IS <= 60
		TIMES RATE, 1.0
	CASE IS <= 70
		TIMES RATE, 0.9
	CASE IS <= 80
		TIMES RATE, 0.8
	CASEELSE
		TIMES RATE, 0.6
	ENDSELECT
	
RETURN LIMIT(RATE, 0, 10000)
