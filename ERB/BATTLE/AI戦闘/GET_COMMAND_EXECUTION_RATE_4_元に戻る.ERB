; AI戦闘コマンド 元に戻る
; コマンドの実行率を0.00%～100.00%で返す
; emueraは小数が使えないので*100した値(0-10000)を扱う
@GET_COMMAND_EXECUTION_RATE_4(CHARA_ID, TARGET_ID, SUPPORT_VALUE)
#DIM CHARA_ID
#DIM TARGET_ID
#DIM SUPPORT_VALUE ; Reserved
#DIM RATE
#DIM DATA_TABLE, 15, 5

	VARSET RATE
		
	; 性格別コマンド実行基礎値(攻撃的な性格別に並べ替え済)
	;                超弱気    弱気    普通    強気  超強気
	DATA_TABLE:10:0 =  3200,   3600,   3200,   2800,   2400  ; 10:ねっけつかん
	DATA_TABLE:2:0  =  3300,   3700,   3300,   2900,   2500  ; 02:おてんば
	DATA_TABLE:14:0 =  3400,   3800,   3400,   3000,   2600  ; 14:わがまま
	DATA_TABLE:3:0  =  3500,   3900,   3500,   3100,   2700  ; 03:がんこもの
	DATA_TABLE:6:0  =  3600,   4000,   3600,   3200,   2800  ; 06:くろうにん
	DATA_TABLE:4:0  =  3700,   4100,   3700,   3300,   2900  ; 04:がんばりや
	DATA_TABLE:5:0  =  3800,   4200,   3800,   3400,   3000  ; 05:きれもの
	DATA_TABLE:1:0  =  3900,   4300,   3900,   3500,   3100  ; 01:おせっかい
	DATA_TABLE:0:0  =  4000,   4400,   4000,   3600,   3200  ; 00:ふつう
	DATA_TABLE:9:0  =  4100,   4500,   4100,   3700,   3300  ; 09:ぬけめがない
	DATA_TABLE:8:0  =  4200,   4600,   4200,   3800,   3400  ; 08:セクシーギャル
	DATA_TABLE:7:0  =  4300,   4700,   4300,   3900,   3500  ; 07:しあわせもの
	DATA_TABLE:11:0 =  4400,   4800,   4400,   4000,   3600  ; 11:のんきもの
	DATA_TABLE:13:0 =  4500,   4900,   4500,   4100,   3700  ; 13:やさしいひと
	DATA_TABLE:12:0 =  4600,   5000,   4600,   4200,   3800  ; 12:ひっこみじあん
	RATE = AI_COMMAND_DATATABLE_TO_RATE(CHARA_ID, DATA_TABLE)
	
	; テンションが低いと下がりたがる
	SELECTCASE BASE:CHARA_ID:TP
	; 100%以上
	CASE IS >= 200
		TIMES RATE, 0.1
	; 66%以上
	CASE IS >= (MAXBASE:CHARA_ID:TP / 100 * 66)
		TIMES RATE, 0.6
	; 33%以上
	CASE IS >= (MAXBASE:CHARA_ID:TP / 100 * 33)
		TIMES RATE, 1.2
	; 0%以上
	CASE IS >= 0
		TIMES RATE, 2.5
	ENDSELECT
	
	; [目立ちたがり]所持キャラは下がりたがらない
	SIF TALENT:CHARA_ID:28
		TIMES RATE, 0.5
	
	; 戦闘素質による補正
	SELECTCASE ABL:CHARA_ID:98
	CASE IS <= 10
		TIMES RATE, 2.0
	CASE IS <= 20
		TIMES RATE, 1.8
	CASE IS <= 30
		TIMES RATE, 1.6	
	CASE IS <= 40
		TIMES RATE, 1.4
	CASE IS <= 50
		TIMES RATE, 1.2
	CASE IS <= 60
		TIMES RATE, 1.0
	CASE IS <= 70
		TIMES RATE, 0.9
	CASE IS <= 80
		TIMES RATE, 0.8
	CASEELSE
		TIMES RATE, 0.6
	ENDSELECT
	
RETURN LIMIT(RATE, 0, 10000)
