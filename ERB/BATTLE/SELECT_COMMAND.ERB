;コマンド選択
@SELECT_COMMAND_MAIN
#DIM LCOUNT

;REPEAT を実行していた場合、ここで解除
IF TFLAG:17 == 1
	TFLAG:17 = 0
ENDIF

;1ターンのみ自動戦闘になっているキャラの自動戦闘を解除しておく
FOR LCOUNT, 0, 3
	IF GET_MEMBER(LCOUNT) && CFLAG:(GET_MEMBER(LCOUNT)):122 == 2
		CFLAG:(GET_MEMBER(LCOUNT)):122 = 0
	ENDIF
NEXT

;戦闘コマンド入力中は即自書き換え
REDRAW 0

;主人公のコマンド選択
$COMMAND1
;キャンセルで戻ってきた際、選択済コマンドが残るので対処
CFLAG:1:50 = 0
SIF CFLAG:1:122 == 2
	CFLAG:1:122 = 0
IF IS_POSSIBLE_BATTLE_ACTION(1) && TFLAG:17 == 0 && !CFLAG:1:122
	CALL SHOW_BATTLE_STATUS
ENDIF
CALL SELECT_COMMAND(1)
;コマンド選択再実行
IF RESULT == -1
	GOTO COMMAND1
;作戦を変更
ELSEIF RESULT == -2
	GOTO COMMAND1
;このターンはおまかせ
ELSEIF RESULT == -3
	GOTO COMMAND2
;キャンセルされたら戻る
ELSEIF RESULT == 0
	GOTO COMMAND1
ENDIF
CALL SELECT_COMMAND_TARGET(1, CFLAG:1:50)
;キャンセルされたら戻る
IF RESULT == 0
	GOTO COMMAND1
ENDIF

;仲間１のコマンド選択
$COMMAND2
;キャンセルで戻ってきた際、選択済コマンドが残るので対処
CFLAG:(FLAG:10):50 = 0
SIF CFLAG:(FLAG:10):122 == 2
	CFLAG:(FLAG:10):122 = 0
IF FLAG:10
	IF IS_POSSIBLE_BATTLE_ACTION(FLAG:10) && TFLAG:17 == 0 && !CFLAG:(FLAG:10):122
		CALL SHOW_BATTLE_STATUS
	ENDIF
	CALL SELECT_COMMAND(FLAG:10)
	;コマンド選択再実行
	IF RESULT == -1
		GOTO COMMAND2
	;作戦を変更
	ELSEIF RESULT == -2
		GOTO COMMAND1
	;このターンはおまかせ
	ELSEIF RESULT == -3
		GOTO COMMAND3
	;キャンセルされたら戻る
	ELSEIF RESULT == 0
		GOTO COMMAND1
	ENDIF
	CALL SELECT_COMMAND_TARGET(FLAG:10, CFLAG:(FLAG:10):50)
	IF RESULT == 0
		;キャンセルされたら戻る
		GOTO COMMAND2
	ENDIF
ENDIF

;仲間２のコマンド選択
$COMMAND3
;キャンセルで戻ってきた際、選択済コマンドが残るので対処
CFLAG:(FLAG:11):50 = 0
SIF CFLAG:(FLAG:11):122 == 2
	CFLAG:(FLAG:11):122 = 0
IF FLAG:11
	IF IS_POSSIBLE_BATTLE_ACTION(FLAG:11) && TFLAG:17 == 0 && !CFLAG:(FLAG:11):122
		CALL SHOW_BATTLE_STATUS
	ENDIF
	CALL SELECT_COMMAND(FLAG:11)
	
	;コマンド選択再実行
	IF RESULT == -1
		GOTO COMMAND3
	;作戦を変更
	ELSEIF RESULT == -2
		GOTO COMMAND1
	;このターンはおまかせ
	ELSEIF RESULT == -3
		GOTO COMMAND_COMPLETE
	;キャンセルされたら戻る
	ELSEIF RESULT == 0
		;２人目が行動不能なら１人目まで戻る
		;AI自動行動中も同様
		IF !IS_POSSIBLE_BATTLE_ACTION(FLAG:10) || CFLAG:(FLAG:10):122 == 1
			GOTO COMMAND1
		ELSE
			GOTO COMMAND2
		ENDIF
	ENDIF
	CALL SELECT_COMMAND_TARGET(FLAG:11, CFLAG:(FLAG:11):50)
	;キャンセルされたら戻る
	IF RESULT == 0
		GOTO COMMAND3
	ENDIF
ENDIF

$COMMAND_COMPLETE
;オートリピート中 or 誰も行動できない(AI行動含)なら、１回だけステータスを表示しておく
IF TFLAG:17 == 2 || ((!IS_POSSIBLE_BATTLE_ACTION(1) || CFLAG:1:122 == 1) && (!IS_POSSIBLE_BATTLE_ACTION(FLAG:10) || CFLAG:(FLAG:10):122 == 1) && (!IS_POSSIBLE_BATTLE_ACTION(FLAG:11) || CFLAG:(FLAG:11):122 == 1))
	CALL SHOW_BATTLE_STATUS
	
	;行動可能キャラ全員がAI処理の場合、専用のコマンドを利用する(非リピート中のみ)
	IF TFLAG:17 == 0 && ((IS_POSSIBLE_BATTLE_ACTION(1) && CFLAG:1:122 == 1) || (IS_POSSIBLE_BATTLE_ACTION(FLAG:10) && CFLAG:(FLAG:10):122 == 1) || (IS_POSSIBLE_BATTLE_ACTION(FLAG:11) && CFLAG:(FLAG:11):122 == 1))
		CALL SELECT_COMMAND_ALL_AI
		;コマンド選択再実行になったら1人目から再確認が必要
		IF RESULT == -2
			GOTO COMMAND1
		ENDIF
	ENDIF
ENDIF

;AIによるコマンド選択
CALL SELECT_COMMAND_AI

REDRAW 1


;コマンド選択（個別）
;ARG:0	対象キャラID
;RETURN	選択したコマンド
;       0:キャンセル -1:コマンド再選択 -2:最初からコマンド再選択
@SELECT_COMMAND(ARG:0)

; 行動不能系状態異常の場合、何もしないで固定
IF CFLAG:(ARG:0):24 || CFLAG:(ARG:0):25 || CFLAG:(ARG:0):29
	CFLAG:(ARG:0):50 = 45
	CFLAG:(ARG:0):51 = 0
	CFLAG:(ARG:0):52 = 0
	RETURN 1
ENDIF

; NEXTコマンドが指定されている場合、NEXT利用で固定
IF CFLAG:(ARG:0):52
	CFLAG:(ARG:0):50 = -1
	; CFLAG:(ARG:0):51 は前のターンの値を流用（同じ対象を攻撃、などがあるため）
	RETURN 1
ENDIF

; 既に諦めているなら入力省略
IF TFLAG:16 == 2
	CFLAG:(ARG:0):50 = 44
	RETURN 44
ENDIF

; AI行動時は何も行わずに戻る
SIF CFLAG:(ARG:0):122
	RETURN 1
	
; コマンド関連変数を初期化
CFLAG:(ARG:0):50 = 0
CFLAG:(ARG:0):51 = 0
CFLAG:(ARG:0):52 = 0
CFLAG:(ARG:0):54 = 0

; 状態によって専用のコマンド選択に分岐
; 催眠中
IF CFLAG:(ARG:0):43
	CALL SELECT_COMMAND_HYPNO(ARG:0)	
; 拘束中
ELSEIF CFLAG:(ARG:0):10 || CFLAG:(ARG:0):20
	CALL SELECT_COMMAND_BIND(ARG:0)
; 通常時
ELSE
	CALL SELECT_COMMAND_BASIC(ARG:0)
ENDIF

RETURN RESULT


;コマンド選択（通常時）
;ARG:0	対象キャラID
;RETURN	選択したコマンド　キャンセル時は0
@SELECT_COMMAND_BASIC(ARG:0)
#DIM LCOUNT				; LOOP用
#DIM COMMAND_FLAG, 100	; 各コマンドが実行できるかどうか
#DIM EQUIP_ITEM, 2		; 装備品の種類（0：非武器　1：消耗武器　2：非消耗武器）

; 初期化
VARSET COMMAND_FLAG, 0, 0, 100
VARSET EQUIP_ITEM, 0, 0, 2

; 共通コマンド
; 1:防御　2:アイテム　3:前進　4:後退　5:逃げる  6:REPEAT  7:AUTO REPEAT  8:緊急浄化　19:霊撃
COMMAND_FLAG:1 = 1
COMMAND_FLAG:2 = 2
COMMAND_FLAG:3 = (!CFLAG:(ARG:0):34) ? 3 # 0
COMMAND_FLAG:4 = (CFLAG:(ARG:0):34) ? 4 # 0
COMMAND_FLAG:5 = 5
COMMAND_FLAG:6 = 6
COMMAND_FLAG:7 = 7
SIF FLAG:6 - (CFLAG:1:50 == 8 ? 50 # 0) - (CFLAG:(FLAG:10):50 == 8 ? 50 # 0) - (CFLAG:(FLAG:11):50 == 8 ? 50 # 0) >= 50
	COMMAND_FLAG:8 = 8
COMMAND_FLAG:19 = (BASE:(ARG:0):0) ? 19 # 0

; 通常攻撃コマンド
; 10:弱攻撃 ～ 15:貫通弾
FOR LCOUNT, 0, 6
	; 攻撃戦闘レベルが足りていればコマンド選択可能
	SIF ABL:(ARG:0):3 > (LCOUNT - 3)
		COMMAND_FLAG:(10 + LCOUNT) = 10 + LCOUNT
NEXT

; 装備の種類を調べておく（0：非武器　1：消耗武器　2：非消耗武器）
FOR LCOUNT, 0, 2
	IF (CFLAG:(ARG:0):(7 + LCOUNT) >= 1) && (CFLAG:(ARG:0):(7 + LCOUNT) <= 9)
		EQUIP_ITEM:LCOUNT = 1
	ELSEIF (CFLAG:(ARG:0):(7 + LCOUNT) >= 10) && (CFLAG:(ARG:0):(7 + LCOUNT) <= 19)
		EQUIP_ITEM:LCOUNT = 2
	ELSE
		EQUIP_ITEM:LCOUNT = 0
	ENDIF
NEXT

; 装備1攻撃コマンド
; 20 ～ 25 装備によって変動
FOR LCOUNT, 0, 6
	; 非武器 + アクセサリ装備ならアイテムが使える
	IF EQUIP_ITEM:0 == 0 && CFLAG:(ARG:0):7 != 0
		COMMAND_FLAG:60 = 60
		
	; 消耗武器
	ELSEIF EQUIP_ITEM:0 == 1
		; 道具戦闘レベル + 残弾が足りていればコマンド選択可能
		SIF (ABL:(ARG:0):4 > (LCOUNT - 3)) && CHECK_BULLET_ENOUGH(CFLAG:(ARG:0):17, 20 + LCOUNT)
			COMMAND_FLAG:(20 + LCOUNT) = 120 + LCOUNT
		
	; 非消耗武器
	ELSE
		; 道具戦闘レベルが足りていればコマンド選択可能
		SIF ABL:(ARG:0):4 > (LCOUNT - 3)
			COMMAND_FLAG:(20 + LCOUNT) = 130 + LCOUNT
	ENDIF
NEXT

; 装備2攻撃コマンド
; 30 ～ 35 装備によって変動
FOR LCOUNT, 0, 6
	; 非武器 + アクセサリ装備ならアイテムが使える
	IF EQUIP_ITEM:1 == 0 && CFLAG:(ARG:0):8 != 0
		COMMAND_FLAG:61 = 61
		
	; 消耗武器
	ELSEIF EQUIP_ITEM:1 == 1
		; 道具戦闘レベル + 残弾が足りていればコマンド選択可能
		SIF (ABL:(ARG:0):4 > (LCOUNT - 3)) && CHECK_BULLET_ENOUGH(CFLAG:(ARG:0):18, 20 + LCOUNT)
			COMMAND_FLAG:(30 + LCOUNT) = 220 + LCOUNT
		
	; 非消耗武器
	ELSE
		; 道具戦闘レベルが足りていればコマンド選択可能
		SIF ABL:(ARG:0):4 > (LCOUNT - 3)
			COMMAND_FLAG:(30 + LCOUNT) = 230 + LCOUNT
	ENDIF
NEXT

; 58:おねだり
; 触手中毒1以上ならおねだり可能
IF ABL:(ARG:0):11 > 0
	; 58はコマンド選択用、54-58はおねだりコマンドID(リピート判定に使用)
	COMMAND_FLAG:54 = 54
	COMMAND_FLAG:55 = 55
	COMMAND_FLAG:56 = 56
	COMMAND_FLAG:57 = 57
	COMMAND_FLAG:58 = 58
ENDIF

; 83:キス　84:手淫
; 触手中毒2以上 + 精液中毒2以上で上記性攻撃が使用可能
IF ABL:(ARG:0):11 > 1 && ABL:(ARG:0):14 > 1
	COMMAND_FLAG:83 = 83
	COMMAND_FLAG:84 = 84
ENDIF
; 85:フェラチオ　86:パイズリ
; 触手中毒3以上 + 精液中毒3以上で上記性攻撃が使用可能
IF ABL:(ARG:0):11 > 2 && ABL:(ARG:0):14 > 2
	COMMAND_FLAG:85 = 85
	COMMAND_FLAG:86 = 86
ENDIF

;REPEAT中なら入力省略
;ただし、使用できないコマンドなら無効化(コマンド100以降は装備品による攻撃)
IF TFLAG:17
	; 前回コマンドがリピート可能(道具戦闘以外)
	IF COMMAND_FLAG:(CFLAG:(ARG:0):101 % 100) && CFLAG:(ARG:0):101 < 100
		CFLAG:(ARG:0):50 = CFLAG:(ARG:0):101
		
	; 前回コマンドが非消耗武器ならリピート可能
	ELSEIF (CFLAG:(ARG:0):101 >= 130 && CFLAG:(ARG:0):101 < 140) || (CFLAG:(ARG:0):101 >= 230 && CFLAG:(ARG:0):101 < 240)
		CFLAG:(ARG:0):50 = CFLAG:(ARG:0):101
		
	; 前回コマンドが消耗武器
	ELSEIF (CFLAG:(ARG:0):101 >= 120 && CFLAG:(ARG:0):101 < 130) || (CFLAG:(ARG:0):101 >= 220 && CFLAG:(ARG:0):101 < 230)
		; コマンド100番以降を表示コマンド番号に照らし合わせる
		; リピート可能
		IF COMMAND_FLAG:((((CFLAG:(ARG:0):101 / 100) + 1) * 10) + ((CFLAG:(ARG:0):101 - 20) % 100))
			CFLAG:(ARG:0):50 = CFLAG:(ARG:0):101
		; リピート不可なら中攻撃を使用
		ELSE
			CFLAG:(ARG:0):50 = 11
		ENDIF
		
	; 全てに該当しないなら中攻撃を使用
	ELSE
		CFLAG:(ARG:0):50 = 11
	ENDIF
	
	;覚え直し
	CFLAG:(ARG:0):101 = CFLAG:(ARG:0):50
	RETURN CFLAG:(ARG:0):50
ENDIF


PRINTFORML %CALLNAME:(ARG:0)%の行動を選択してください

;炎熱変換持ちは炎攻撃が撃てる
IF TALENT:(ARG:0):151
	SETCOLOR 0xEE1010
ENDIF
PRINTFORMLC 【通常攻撃】（威力：{SHOT_POWER(ARG:0)}）
RESETCOLOR

;火炎放射器は炎が撃てる
IF CFLAG:(ARG:0):7 == 3
	SETCOLOR 0xEE1010
ENDIF
IF EQUIP_ITEM:0 == 1
	PRINTFORMLC 【%ITEMNAME:(CFLAG:(ARG:0):7)%】（{EQUIP_POWER(CFLAG:(ARG:0):7, ARG:0)} / {CFLAG:(ARG:0):17}）
ELSEIF EQUIP_ITEM:0 == 2
	PRINTFORMLC 【%ITEMNAME:(CFLAG:(ARG:0):7)%】（{EQUIP_POWER(CFLAG:(ARG:0):7, ARG:0)}）
ELSE
	SETCOLOR 0x505050
	PRINTFORMLC 【%ITEMNAME:(CFLAG:(ARG:0):7)%】
	RESETCOLOR
ENDIF
RESETCOLOR

;火炎放射器は炎が撃てる
IF CFLAG:(ARG:0):8 == 3
	SETCOLOR 0xEE1010
ENDIF
IF EQUIP_ITEM:1 == 1
	PRINTFORMLC 【%ITEMNAME:(CFLAG:(ARG:0):8)%】（{EQUIP_POWER(CFLAG:(ARG:0):8, ARG:0)} / {CFLAG:(ARG:0):18}）
ELSEIF EQUIP_ITEM:1 == 2
	PRINTFORMLC 【%ITEMNAME:(CFLAG:(ARG:0):8)%】（{EQUIP_POWER(CFLAG:(ARG:0):8, ARG:0)}）
ELSE
	SETCOLOR 0x505050
	PRINTFORMLC 【%ITEMNAME:(CFLAG:(ARG:0):8)%】
	RESETCOLOR
ENDIF
RESETCOLOR

PRINTFORML  

; 攻撃コマンド表示
FOR LCOUNT, 0, 6
	; 10 通常攻撃
	PRINTFORMLC \@(COMMAND_FLAG:(10 + LCOUNT)) ? [{10 + LCOUNT}] %SKILLNAME(10 + LCOUNT), 12, LEFT%(%GET_HIT_RATE_TEMP(ARG:0, 10 + LCOUNT, 3)%％) # 　 \@

	; 20 装備1攻撃
	; アクセサリ利用可
	IF EQUIP_ITEM:0 == 0 && LCOUNT == 0
		PRINTFORMLC \@(COMMAND_FLAG:(60)) ? [60] %SKILLNAME(60)% # 　 \@
	; 消耗武器
	ELSEIF EQUIP_ITEM:0 == 1
		; 通常表示
		IF CHECK_BULLET_ENOUGH(CFLAG:(ARG:0):17, 20 + LCOUNT)
			PRINTFORMLC \@(COMMAND_FLAG:(20 + LCOUNT)) ? [{20 + LCOUNT}] %SKILLNAME(20 + LCOUNT), 12, LEFT%(%GET_HIT_RATE_TEMP(ARG:0, 20 + LCOUNT, 4)%％) # 　 \@
		; 残弾不足表示
		ELSE
			IF ABL:(ARG:0):4 > (LCOUNT - 3)
				SETCOLOR 0x333333
				PRINTFORMLC [--] %SKILLNAME(20 + LCOUNT), 12, LEFT%(---％)
				RESETCOLOR
			ENDIF
		ENDIF
	; 非消耗武器
	ELSEIF EQUIP_ITEM:0 == 2
		PRINTFORMLC \@(COMMAND_FLAG:(20 + LCOUNT)) ? [{20 + LCOUNT}] %SKILLNAME(30 + LCOUNT), 12, LEFT%(%GET_HIT_RATE_TEMP(ARG:0, 30 + LCOUNT, 4)%％) # 　 \@
	ELSE
		PRINTFORMLC  
	ENDIF
	
	; 30 装備2攻撃
	; アクセサリ利用可
	IF EQUIP_ITEM:1 == 0 && LCOUNT == 0
		PRINTFORMLC \@(COMMAND_FLAG:(61)) ? [61] %SKILLNAME(61)% # 　 \@
	; 消耗武器
	ELSEIF EQUIP_ITEM:1 == 1
		; 通常表示
		IF CHECK_BULLET_ENOUGH(CFLAG:(ARG:0):18, 20 + LCOUNT)
			PRINTFORMLC \@(COMMAND_FLAG:(30 + LCOUNT)) ? [{30 + LCOUNT}] %SKILLNAME(20 + LCOUNT), 12, LEFT%(%GET_HIT_RATE_TEMP(ARG:0, 20 + LCOUNT, 4)%％) # 　 \@
		; 残弾不足表示
		ELSE
			IF ABL:(ARG:0):4 > (LCOUNT - 3)
				SETCOLOR 0x333333
				PRINTFORMLC [--] %SKILLNAME(20 + LCOUNT), 12, LEFT%(---％)
				RESETCOLOR
			ENDIF
		ENDIF
	; 非消耗武器
	ELSEIF EQUIP_ITEM:1 == 2
		PRINTFORMLC \@(COMMAND_FLAG:(30 + LCOUNT)) ? [{30 + LCOUNT}] %SKILLNAME(30 + LCOUNT), 12, LEFT%(%GET_HIT_RATE_TEMP(ARG:0, 30 + LCOUNT, 4)%％) # 　 \@
	ELSE
		PRINTFORMLC  
	ENDIF
	PRINTFORML  
NEXT
PRINTFORML 

; 性攻撃表示
; 触手中毒2以上 + 精液中毒2以上なら表示する
IF ABL:(ARG:0):11 > 1 && ABL:(ARG:0):14 > 1
	PRINTFORMLC 【性攻撃】
	PRINTFORML 
	IF (ABL:(ARG:0):11 > 1 && ABL:(ARG:0):14 > 1)
		PRINTFORMLC \@(COMMAND_FLAG:83) ? [83] %SKILLNAME(83)% # 　 \@
		PRINTFORMLC \@(COMMAND_FLAG:84) ? [84] %SKILLNAME(84)% # 　 \@
		PRINTFORML  
	ENDIF
	IF (ABL:(ARG:0):11 > 2 && ABL:(ARG:0):14 > 2)
		PRINTFORMLC \@(COMMAND_FLAG:85) ? [85] %SKILLNAME(85)% # 　 \@
		PRINTFORMLC \@(COMMAND_FLAG:86) ? [86] %SKILLNAME(86)% # 　 \@
		PRINTFORML  
	ENDIF
	PRINTFORML 
ENDIF

;共通コマンド表示
FOR LCOUNT, 1, 5
	IF (COMMAND_FLAG:(LCOUNT))
		PRINTFORMLC [{LCOUNT,2}] %SKILLNAME(LCOUNT)%
	ENDIF
NEXT
; 3か4は片方しか使えないはずなので、ここまでで３つ表示
; 逃げるコマンドは成功率0%ならグレーアウト(グレーにするだけで選択可能、無駄な抵抗をする子はかわいい)
SIF GET_ESCAPE_RATE(ARG:0) == 0
	SETCOLOR 0x333333
PRINTFORML  
PRINTFORMLC [ 5] %SKILLNAME(5)%(成功率:{GET_ESCAPE_RATE(ARG:0)}％)
RESETCOLOR

;おねだりコマンド表示
PRINTFORMLC \@(COMMAND_FLAG:58) ? [58] %SKILLNAME(58)% # 　 \@

; 霊撃
PRINTFORML 
IF COMMAND_FLAG:8
	PRINTFORMLC [ 8] 緊急浄化
ELSE
	SETCOLOR 0x505050
	PRINTFORMLC [ 8] 緊急浄化
	RESETCOLOR
ENDIF
IF COMMAND_FLAG:19
	PRINTFORMLC [19] %SKILLNAME(19)% (消費HP：{(ABL:(ARG:0):98 * MAXBASE:(ARG:0):0) / 100} 成功率:{10000 * BASE:(ARG:0):0 / ABL:(ARG:0):98 / MAXBASE:(ARG:0):0}％)
ELSE
	SETCOLOR 0x333333
	PRINTFORMLC [--] %SKILLNAME(19)% (消費HP：{(ABL:(ARG:0):98 * MAXBASE:(ARG:0):0) / 100} 成功率:0％)
	RESETCOLOR
ENDIF

PRINTFORML 

PRINTFORMLC [ 6] REPEAT
PRINTFORMLC [ 7] AUTO REPEAT

;キャンセルはいつでも可能
PRINTFORMLC [ 0] %SKILLNAME(0)%

PRINTFORML  

PRINTFORML 
PRINTFORMLC [97] 図鑑を確認する
PRINTFORMLC [98] 作戦を変更
PRINTFORMLC [99] このターンはおまかせ
PRINTFORML 

;ここまでで選択肢の表示完了
$INPUT_LOOP
INPUT

;戦闘中システムコマンド 通常コマンドとは別判定
SELECTCASE RESULT
;図鑑を確認
CASE 97
	CALL SHOW_BATTLE_ENEMY_WIKI
	SIF RESULT
		WAIT
	RETURN -1
;作戦を変更
CASE 98
	CALL SET_AI_BATTLE_CONFIG
	RETURN -2
;このターンはおまかせ
CASE 99
	CFLAG:(ARG:0):122 = 2
	RETURN -3
ENDSELECT

; 実行不可能なコマンドは処理しないように弾く(0:キャンセル のみ例外)
IF RESULT >= 100 || (!COMMAND_FLAG:(RESULT) && RESULT != 0)
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

; コマンド内容によって分岐
SELECTCASE RESULT
CASE 0
	RETURN 0
; REPEAT開始
CASE 6
	TFLAG:17 = 1
	RESTART
; AUTO REPEAT開始
CASE 7
	TFLAG:17 = 2
	RESTART
ENDSELECT

; 特殊な処理をしない場合は全てここ
CFLAG:(ARG:0):50 = COMMAND_FLAG:(RESULT)
RETURN COMMAND_FLAG:(RESULT)


;コマンド選択（催眠時）
;ARG:0	対象キャラID
;RETURN	選択したコマンド　キャンセル時は0
@SELECT_COMMAND_HYPNO(ARG:0)
#DIM LCOUNT, 2			; LOOP用
#DIM COMMAND_FLAG, 100	; 各コマンドが実行できるかどうか

;初期化
VARSET COMMAND_FLAG, 0, 0, 100

; 共通コマンド
; 6:REPEAT　7:AUTO REPEAT
COMMAND_FLAG:6 = 6
COMMAND_FLAG:7 = 7

; 58:おねだり
; 触手中毒1以上ならおねだり可能
IF ABL:(ARG:0):11 > 0
	; 58はコマンド選択用、54-58はおねだりコマンドID(リピート判定に使用)
	COMMAND_FLAG:54 = 54
	COMMAND_FLAG:55 = 55
	COMMAND_FLAG:56 = 56
	COMMAND_FLAG:57 = 57
	COMMAND_FLAG:58 = 58
ENDIF

; 71:平常行動　72:自慰　74:着替える　75:逃げる(催眠中は前に出ると同じ効果)　76:安眠
COMMAND_FLAG:71 = 71
COMMAND_FLAG:72 = 72
COMMAND_FLAG:74 = 74
COMMAND_FLAG:75 = 75
COMMAND_FLAG:76 = 76

; 73:仲間を襲う
; PTメンバーが2人以上 自分が拘束されていない
IF GET_MEMBER_SUM() >= 2 && !CFLAG:(ARG:0):20
	; 自分以外のキャラを襲える状態にある (丸呑みされていない)
	IF ARG:0 == 1 && ((FLAG:10 && !CFLAG:(FLAG:10):21) || (FLAG:11 && !CFLAG:(FLAG:11):21))
		COMMAND_FLAG:73 = 73
	ELSEIF ARG:0 == FLAG:10 && (!CFLAG:1:21 || (FLAG:11 && !CFLAG:(FLAG:11):21))
		COMMAND_FLAG:73 = 73
	ELSEIF ARG:0 == FLAG:11 && (!CFLAG:1:21 || (FLAG:10 && !CFLAG:(FLAG:10):21))
		COMMAND_FLAG:73 = 73
	ENDIF
ENDIF

; 催眠強度依存コマンド
; 80:踏む
IF CFLAG:(ARG:0):43 < 3
	COMMAND_FLAG:80 = 80
ENDIF
; 81:ビンタ　82:折る
IF CFLAG:(ARG:0):43 == 1
	COMMAND_FLAG:81 = 81
	COMMAND_FLAG:82 = 82
ENDIF
; 83:キス　84:手淫
IF CFLAG:(ARG:0):43 >= 2
	COMMAND_FLAG:83 = 83
	COMMAND_FLAG:84 = 84
ENDIF
; 85:フェラチオ　86:パイズリ
IF CFLAG:(ARG:0):43 >= 3
	COMMAND_FLAG:85 = 85
	COMMAND_FLAG:86 = 86
ENDIF

; REPEAT中なら入力省略
IF TFLAG:17
	; 前回コマンドが使用不能、もしくは未入力であれば平常行動に変更
	IF !COMMAND_FLAG:(CFLAG:(ARG:0):101 % 100) || CFLAG:(ARG:0):101 == 0
		CFLAG:(ARG:0):50 = 71
		
	; 特に問題がなければ前回行動をリピート
	ELSE
		CFLAG:(ARG:0):50 = CFLAG:(ARG:0):101
	ENDIF
	
	; 覚え直し
	CFLAG:(ARG:0):101 = CFLAG:(ARG:0):50
	RETURN CFLAG:(ARG:0):50
ENDIF


PRINTFORML %CALLNAME:(ARG:0)%の行動を選択してください

; コマンド表示
; 催眠コマンド表示中だけ使うLOCAL変数
; LOCAL
; 1	共通行動スキップ値
; 2	変動行動スキップ値

LOCAL:1 = 0
LOCAL:2 = 0

FOR LCOUNT:0, 0, 5

	; 共通行動
	; コマンドが空白か、特定のコマンドならスキップ値+1 (仲間を襲う, 逃げる)
	FOR LCOUNT:1, LCOUNT:0, 5
		IF COMMAND_FLAG:(LCOUNT:0 + 71 + LOCAL:1) && !(COMMAND_FLAG:(LCOUNT:0 + 71 + LOCAL:1) == 73 || COMMAND_FLAG:(LCOUNT:0 + 71 + LOCAL:1) == 75)
			BREAK
		ELSE
			; 別コマンドに浸食しないようリミットを掛ける
			LOCAL:1 = LIMIT (LOCAL:1 + 1, 0, 4)
		ENDIF
	NEXT
	
	PRINTFORMLC \@(COMMAND_FLAG:(LCOUNT:0 + 71 + LOCAL:1)) ? [{LCOUNT:0 + 71 + LOCAL:1}] %SKILLNAME(LCOUNT:0 + 71 + LOCAL:1)% # 　 \@
	
	; 催眠強度によって変動する行動
	FOR LCOUNT:1, LCOUNT:0, 5
		IF COMMAND_FLAG:(LCOUNT:0 + 80 + LOCAL:2)
			BREAK
		ELSE
			; 別コマンドに浸食しないようリミットを掛ける
			LOCAL:2 = LIMIT (LOCAL:2 + 1, 0, 4)
		ENDIF
	NEXT

	PRINTFORMLC \@(COMMAND_FLAG:(LCOUNT:0 + 80 + LOCAL:2)) ? [{LCOUNT:0 + 80 + LOCAL:2}] %SKILLNAME(LCOUNT:0 + 80 + LOCAL:2)% # 　 \@
	
	; 仲間を襲う (一回だけ表示)
	IF LCOUNT:0 == 0
		; 襲えない場合はグレー表示
		SIF !COMMAND_FLAG:73
			SETCOLOR 0x333333
		PRINTFORMLC [\@(COMMAND_FLAG:73) ? 73 # -- \@] %SKILLNAME(73)%
		RESETCOLOR
	ENDIF
	
	PRINTFORML 
NEXT

; 共通コマンド表示
PRINTFORMLC \@(COMMAND_FLAG:75) ? [75] %SKILLNAME(75)%(成功率:{GET_ESCAPE_RATE(ARG:0)}％) # 　 \@
PRINTFORMLC \@(COMMAND_FLAG:58) ? [58] %SKILLNAME(58)% # 　 \@
PRINTFORML 
PRINTFORMLC [ 6] REPEAT
PRINTFORMLC [ 7] AUTO REPEAT
PRINTFORMLC [ 0] %SKILLNAME(0)%
PRINTFORML 
PRINTFORMLC [97] 図鑑を確認する
PRINTFORMLC [98] 作戦を変更
PRINTFORMLC [99] このターンはおまかせ
PRINTFORML 

$INPUT_LOOP
INPUT

;戦闘中システムコマンド 通常コマンドとは別判定
SELECTCASE RESULT
;図鑑を確認
CASE 97
	CALL SHOW_BATTLE_ENEMY_WIKI
	SIF RESULT
		WAIT
	RETURN -1
;作戦を変更
CASE 98
	CALL SET_AI_BATTLE_CONFIG
	RETURN -2
;このターンはおまかせ
CASE 99
	CFLAG:(ARG:0):122 = 2
	RETURN -3
ENDSELECT

; 実行不可能なコマンドは処理しないように弾く(0:キャンセル のみ例外)
IF RESULT >= 100 || (!COMMAND_FLAG:(RESULT) && RESULT != 0)
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

; コマンド内容によって分岐
SELECTCASE RESULT
CASE 0
	RETURN 0
; REPEAT開始
CASE 6
	TFLAG:17 = 1
	RESTART
; AUTO REPEAT開始
CASE 7
	TFLAG:17 = 2
	RESTART
ENDSELECT

; 特殊な処理をしない場合は全てここ
CFLAG:(ARG:0):50 = COMMAND_FLAG:(RESULT)
RETURN COMMAND_FLAG:(RESULT)


;コマンド選択（拘束時）
;ARG:0	対象キャラID
;RETURN	選択したコマンド　キャンセル時は0
@SELECT_COMMAND_BIND(ARG:0)
#DIM COMMAND_FLAG, 100	; 各コマンドが実行できるかどうか

;初期化
VARSET COMMAND_FLAG, 0, 0, 100

;共通コマンド
;1:防御　6:REPEAT  7:AUTO REPEAT  19:霊撃
COMMAND_FLAG:1 = 1
COMMAND_FLAG:6 = 6
COMMAND_FLAG:7 = 7
COMMAND_FLAG:19 = (BASE:(ARG:0):0) ? 19 # 0

; 58:おねだり
; 触手中毒1以上ならおねだり可能
IF ABL:(ARG:0):11 > 0
	; 58はコマンド選択用、54-58はおねだりコマンドID(リピート判定に使用)
	COMMAND_FLAG:54 = 54
	COMMAND_FLAG:55 = 55
	COMMAND_FLAG:56 = 56
	COMMAND_FLAG:57 = 57
	COMMAND_FLAG:58 = 58
ENDIF

; 拘束時専用コマンド
COMMAND_FLAG:42 = 42
COMMAND_FLAG:43 = 43
COMMAND_FLAG:44 = (TFLAG:16 == 1) ? 44 # 0
COMMAND_FLAG:50 = (CFLAG:(ARG:0):38 < 5) ? 50 # 0
COMMAND_FLAG:51 = (CFLAG:(ARG:0):38 < 5) ? 51 # 0
COMMAND_FLAG:52 = (CFLAG:(ARG:0):38 < 5) ? 52 # 0
COMMAND_FLAG:53 = (CFLAG:(ARG:0):38 < 5) ? 53 # 0

; REPEAT中なら入力省略
IF TFLAG:17
	; 前回コマンドが使用不能、もしくは未入力であれば脱出に変更
	IF !COMMAND_FLAG:(CFLAG:(ARG:0):102) || CFLAG:(ARG:0):102 == 0
		CFLAG:(ARG:0):50 = 42
		
	; 特に問題がなければ前回行動をリピート
	ELSE
		CFLAG:(ARG:0):50 = CFLAG:(ARG:0):102
	ENDIF
	
	; 覚え直し
	CFLAG:(ARG:0):102 = CFLAG:(ARG:0):50
	RETURN CFLAG:(ARG:0):50
ENDIF


PRINTFORML %CALLNAME:(ARG:0)%の行動を選択してください

PRINTFORMLC [ 1] %SKILLNAME(1)%
CALL GET_RELEASE_RATE(ARG:0)
PRINTFORMLC [42] %SKILLNAME(42)%(成功率:{RESULT}％)
PRINTFORMLC [43] %SKILLNAME(43)%
PRINTFORML  

;拘束レベル5以上で抵抗不可(文字色変更)
SIF CFLAG:(ARG:0):38 >= 5
	SETCOLOR 0x333333
PRINTFORMLC [\@(COMMAND_FLAG:50) ? 50 # -- \@] %SKILLNAME(50)%
PRINTFORMLC [\@(COMMAND_FLAG:51) ? 51 # -- \@] %SKILLNAME(51)%
PRINTFORMLC [\@(COMMAND_FLAG:52) ? 52 # -- \@] %SKILLNAME(52, ARG:0)%
PRINTFORML  
PRINTFORMLC [\@(COMMAND_FLAG:53) ? 53 # -- \@] %SKILLNAME(53)%
RESETCOLOR

;おねだりコマンド表示
PRINTFORMLC \@(COMMAND_FLAG:58) ? [58] %SKILLNAME(58)% # 　 \@

; 諦める
SIF !COMMAND_FLAG:44
	SETCOLOR 0x333333
PRINTFORMLC [\@(COMMAND_FLAG:44) ? 44 # -- \@] %SKILLNAME(44)%（全滅まで放置）
RESETCOLOR

; 霊撃
PRINTFORML 
IF COMMAND_FLAG:19
	PRINTFORMLC [19] %SKILLNAME(19)% (消費HP：{(ABL:(ARG:0):98 * MAXBASE:(ARG:0):0) / 100} 成功率:{10000 * BASE:(ARG:0):0 / ABL:(ARG:0):98 / MAXBASE:(ARG:0):0}％)
ELSE
	SETCOLOR 0x333333
	PRINTFORMLC [--] %SKILLNAME(19)% (消費HP：{(ABL:(ARG:0):98 * MAXBASE:(ARG:0):0) / 100} 成功率:0％)
	RESETCOLOR
ENDIF

; 汎用コマンド
PRINTFORML
PRINTFORMLC [ 6] REPEAT
PRINTFORMLC [ 7] AUTO REPEAT
PRINTFORMLC [ 0] %SKILLNAME(0)%
PRINTFORML  
PRINTFORMLC [97] 図鑑を確認する
PRINTFORMLC [98] 作戦を変更
PRINTFORMLC [99] このターンはおまかせ
PRINTFORML 

$INPUT_LOOP
INPUT

;戦闘中システムコマンド 通常コマンドとは別判定
SELECTCASE RESULT
;図鑑を確認
CASE 97
	CALL SHOW_BATTLE_ENEMY_WIKI
	SIF RESULT
		WAIT
	RETURN -1
;作戦を変更
CASE 98
	CALL SET_AI_BATTLE_CONFIG
	RETURN -2
;このターンはおまかせ
CASE 99
	CFLAG:(ARG:0):122 = 2
	RETURN -3
ENDSELECT

; 実行不可能なコマンドは処理しないように弾く(0:キャンセル のみ例外)
IF RESULT >= 100 || (!COMMAND_FLAG:(RESULT) && RESULT != 0)
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

; コマンド内容によって分岐
SELECTCASE RESULT
CASE 0
	RETURN 0
; REPEAT開始
CASE 6
	TFLAG:17 = 1
	RESTART
; AUTO REPEAT開始
CASE 7
	TFLAG:17 = 2
	RESTART
; 諦め
CASE 44
	LOCAL:1 = CFLAG:(ARG:0):20
	IF LOCAL:1 == 0 && CFLAG:(ARG:0):10
		LOCAL:1 = CFLAG:(ARG:0):10
	ENDIF
	
	CALL PRINT_MESSAGE(ARG:0, 1, 44, CALLNAME:(ARG:0), SAVESTR:(LOCAL:1), 0, DA:(LOCAL:1):0)
	TFLAG:16 = 2
	CFLAG:(ARG:0):50 = 44
	RETURN 44
ENDSELECT

; 特殊な処理をしない場合は全てここ
CFLAG:(ARG:0):50 = COMMAND_FLAG:(RESULT)
RETURN COMMAND_FLAG:(RESULT)


;戦闘行動可能か否か
@IS_POSSIBLE_BATTLE_ACTION(CHARA_ID)
#FUNCTION
#DIM CHARA_ID

	;処理するのはPTメンバーのみ
	SIF !CHECK_MEMBER_FROM_ID(CHARA_ID)
		RETURNF 0
		
	; 通常行動不可能
	SIF !IS_POSSIBLE_ACTION(CHARA_ID)
		RETURNF 0
		
	;NEXTコマンド決定済み
	SIF CFLAG:CHARA_ID:52 != 0
		RETURNF 0
		
RETURNF 1

;通常行動可能か否か
@IS_POSSIBLE_ACTION(CHARA_ID)
#FUNCTION
#DIM CHARA_ID

	;処理するのはPTメンバーのみ
	SIF !CHECK_MEMBER_FROM_ID(CHARA_ID)
		RETURNF 0
		
	;怯み
	SIF CFLAG:CHARA_ID:24
		RETURNF 0
		
	;睡眠
	SIF CFLAG:CHARA_ID:25
		RETURNF 0
		
	;戦意喪失
	SIF CFLAG:CHARA_ID:29
		RETURNF 0
		
	;諦めた
	SIF TFLAG:16 == 2
		RETURNF 0
		
	;NEXTコマンド決定済
	SELECTCASE CFLAG:CHARA_ID:52
	;強制自慰
	CASE 46
		RETURNF 0
	;抱きつかれ
	CASE 95
		RETURNF 0
	;抱きつき
	CASE 96
		RETURNF 0
	ENDSELECT
RETURNF 1

;コマンドの対象選択
;ARG:0	キャラID
;ARG:1	対象コマンド
;RETURN	0:キャンセルされた　1:正常終了
@SELECT_COMMAND_TARGET(ARG:0, ARG:1)

;LOCAL
;0	LOOP
;1	TARGET
;2	TEMP

; AI行動時は何も行わずに戻る
SIF CFLAG:(ARG:0):122
	RETURN 1
	
LOCAL:1 = 0

;特定のコマンドは特殊
IF ARG:1 == 2
	;アイテム選択
	CALL SELECT_ITEM_BATTLE(ARG:0)
	IF RESULT == 0
		RETURN 0
	ENDIF
	;キャンセルされていない場合、SELECT_ITEM_BATTLE の中で対象選択も終わっているので、1を返すだけ
	RETURN 1
ELSEIF ARG:1 == 5
	;逃げる確認
	;誰かが拘束されているなら警告
	LOCAL:2 = 0
	IF CFLAG:1:20
		LOCAL:2 = 1
	ENDIF
	IF FLAG:10
		IF CFLAG:(FLAG:10):20
			LOCAL:2 = 1
		ENDIF
	ENDIF
	IF FLAG:11
		IF CFLAG:(FLAG:11):20
			LOCAL:2 = 1
		ENDIF
	ENDIF
	IF LOCAL:2
		SETCOLOR 0xEEEE10
		PRINTFORML ※拘束されている味方を見捨てて逃げることになります！
		RESETCOLOR
		PRINTFORML 本当に逃げますか？
		CALL ASK_YN
		IF RESULT == 1
			RETURN 0
		ENDIF
	ENDIF
	;逃げる確定の場合、対象選択をスキップ
	CFLAG:(ARG:0):51 = 0
	RETURN 1
ELSEIF ARG:1 >= 54 && ARG:1 <= 58
	;おねだり系コマンドは、おねだり部位をランダムにする
	CFLAG:(ARG:0):50 = RAND:5 + 54
	RETURN 1
ELSEIF (ARG:1 == 1) || (ARG:1 == 3) || (ARG:1 == 4) || ((ARG:1 % 100) == 35) || (ARG:1 >= 71 && ARG:1 <= 77)
	;自分に使う系の場合、対象選択をスキップ
	IF TFLAG:17
		CFLAG:(ARG:0):51 = ARG:0
		RETURN 1
	ELSE
		CALL SELECT_ENEMY(ARG:1, 2)
		IF RESULT
			;自分に使う場合、対象を自分のIDに差し替えておく
			CFLAG:(ARG:0):51 = ARG:0
			RETURN 1
		ENDIF
	ENDIF
ELSEIF (ARG:1 == 15) || ((ARG:1 % 100) == 24)
	;敵全体系
	CALL SELECT_ENEMY(ARG:1, 1)
ELSEIF (ARG:1 < 0)
	;コマンドIDが0未満なら、対象選択をスキップ
	RETURN 1
ELSEIF (ARG:1 >= 40 && ARG:1 <= 53)
	;コマンドIDが40台の場合、対象を0で固定
	CFLAG:(ARG:0):51 = 0
	RETURN 1
ELSEIF TFLAG:17
	;REPEAT 中の対象選択
	IF CFLAG:(ARG:0):106
		;前回選択した対象が保存されている場合、それを使う
		CFLAG:(ARG:0):51 = CFLAG:(ARG:0):106
	ELSE
		;まだ対象を覚えていない場合、敵単体対象は、残った敵の先頭
		LOCAL:2 = 1
		FOR LOCAL:0, 1, TFLAG:15 + 1
			IF TFLAG:(LOCAL:0) && DA:(TFLAG:(LOCAL:0)):1 > 0
				LOCAL:2 = LOCAL:0
				BREAK
			ENDIF
		NEXT
		CFLAG:(ARG:0):51 = TFLAG:(LOCAL:2)
	ENDIF
	RETURN 1
;緊急浄化は浄化力50必要
ELSEIF ARG:1 == 8 && FLAG:6 >= 50
	RETURN 1
ELSEIF ARG:1 == 8 && FLAG:6 < 50
	RETURN 0
ELSEIF CFLAG:(ARG:0):20 && ARG:1 == 19
	;拘束中の霊撃は拘束者のみ対象
	CFLAG:(ARG:0):51 = 	CFLAG:(ARG:0):20
	RETURN 1
ELSEIF ARG:1 == 60
	SELECTCASE CFLAG:(ARG:0):7
		CASE 20 TO 23
			CALL SELECT_ENEMY(ARG:1, 0)
		CASEELSE
			PRINTFORML %ITEMNAME:(CFLAG:(ARG:0):7)%
			PRINTFORML [1] 使用する
			PRINTFORML [0] キャンセル
			INPUT
			IF RESULT != 1
				;不正な数値は全部キャンセル扱い
				RESULT = 0
			ENDIF
			CFLAG:(ARG:0):51 = ARG:0
	ENDSELECT
ELSEIF ARG:1 == 61
	SELECTCASE CFLAG:(ARG:0):8
		CASE 20 TO 23
			CALL SELECT_ENEMY(ARG:1, 0)
		CASEELSE
			PRINTFORML %ITEMNAME:(CFLAG:(ARG:0):8)%
			PRINTFORML [1] 使用する
			PRINTFORML [0] キャンセル
			INPUT
			IF RESULT != 1
				;不正な数値は全部キャンセル扱い
				RESULT = 0
			ENDIF
			CFLAG:(ARG:0):51 = ARG:0
	ENDSELECT
ELSE
	;その他（敵単体系）
	CALL SELECT_ENEMY(ARG:1, 0)
ENDIF

LOCAL:1 = RESULT

IF LOCAL:1 == 0
	;キャンセルされた
	RETURN 0
ELSE
	;対象決定
	
	;並び順が入っているだけなので、敵IDに変換しておく
	CFLAG:(ARG:0):51 = TFLAG:(LOCAL:1)
	RETURN 1
ENDIF



;対象を選択する
;ARG:0	スキルID
;ARG:1	対象範囲（0:単体　1:全体　2:自分）
@SELECT_ENEMY(ARG:0, ARG:1)
;LOCAL
;0	LOOP
;1～10	敵生存フラグ

VARSET LOCAL, 1, 11

IF ARG:0 > 100
	LOCAL:0 = ARG:0 % 100
	PRINTFORML 【%SKILLNAME(LOCAL:0)%】 %SKILL_EXPLAIN(LOCAL:0)%
ELSE
	PRINTFORML 【%SKILLNAME(ARG:0)%】 %SKILL_EXPLAIN(ARG:0)%
ENDIF

IF ARG:1 == 1 || ARG:1 == 2
	IF FLAG:505 || TFLAG:17
		RETURN 1
	ENDIF
	PRINTFORML [1] 使用する
	PRINTFORML [0] キャンセル
	INPUT
	IF RESULT == 1
		RETURN 1
	ELSE
		;不正な数値は全部キャンセル扱い
		RETURN 0
	ENDIF
ELSE
	IF FLAG:505
		;入力省略チェック
		LOCAL:11 = 0
		LOCAL:12 = 0
		FOR LOCAL:0, 1, 11
			IF LOCAL:0 <= TFLAG:15 && DA:(TFLAG:(LOCAL:0)):1 > 0
				;生きている敵をカウント
				LOCAL:11 += 1
				LOCAL:12 = LOCAL:0
			ENDIF
		NEXT
		IF LOCAL:11 == 1
			RETURN LOCAL:12
		ENDIF
	ENDIF
	FOR LOCAL:0, 1, 11
		IF LOCAL:0 <= TFLAG:15 && DA:(TFLAG:(LOCAL:0)):1 > 0
			;まだ生きている敵のみ選択可能
			LOCAL:(LOCAL:0) = 1
			PRINTFORML [{LOCAL:0}] %SAVESTR:(TFLAG:(LOCAL:0))%
		ENDIF
	NEXT
	PRINTFORML [0] キャンセル
	
$INPUT_LOOP
	INPUT
	
	IF RESULT == 0
		RETURN 0
	ELSEIF (RESULT > 0) && (RESULT <= 10) && (LOCAL:(RESULT) > 0)
		RETURN RESULT
	ELSE
		REUSELASTLINE 入力値が不正です。再入力してください
		GOTO INPUT_LOOP
	ENDIF
ENDIF


