;固定イベント
;155の教室と一部メッセージ呼び出しも共通
;RETURN	0:失敗した　1:成功した　2:以後のイベントスキップ
@CHECK_FIXED_EVENT_S_158
#DIM LCOUNT
;LOCAL
;0	LOOP
;1	目標値
;2	達成値
;3	戻り値
;4	アイテムの種類決定

LOCAL:3 = 0
LOCAL:4 = 0

;目標値（イベント難易度）決定
LOCAL:1 = 60


;達成値決定
CALL GET_SEARCH_RATE
LOCAL:2 = RESULT


IF LOCAL:2 >= LOCAL:1 || FLAG:158 >= 100
	;経験値が入るのは最初だけ
	IF FLAG:158 < 100
		CALL ADD_ABL_EXP_PT(0, 1000)
	ENDIF
	CALL ADD_ABL_EXP_PT(0, 700)
	PRINTFORML ここは誰もいない教室のようだ！どうしますか？
	
	$INPUT_LOOP
	PRINTFORML [0] 休む
	PRINTFORML [1] 立ち去る


	INPUT

	IF RESULT == 0
		IF FLAG:158 < 100
			PRINTFORML %CALLNAME:1%%TACHI()%が休む準備をしていると使えそうなものが残っていた！
			CALL PRINT_MESSAGE(1, 200, 155, CALLNAME:1, "")
			CALL PRINT_MESSAGE(FLAG:10, 200, 155, CALLNAME:1, "")
			CALL PRINT_MESSAGE(FLAG:11, 200, 155, CALLNAME:1, "")
			FOR LOCAL:0, 0,(RAND:2 + 1)
				SELECTCASE RAND:120
					CASE 0 TO 9
						LOCAL:4 = 20
					CASE 10 TO 19
						LOCAL:4 = 24
					CASE 20 TO 29
						LOCAL:4 = 30
					CASE 30 TO 39
						LOCAL:4 = 31
					CASE 40 TO 49
						LOCAL:4 = 36
					CASE 50 TO 59
						LOCAL:4 = 38
					CASE 60 TO 69
						LOCAL:4 = 50
					CASE 70 TO 79
						LOCAL:4 = 52
					CASE 80 TO 89
						LOCAL:4 = 54
					CASE 90 TO 94
						LOCAL:4 = 51
					CASE 95 TO 99
						LOCAL:4 = 53
					CASE 100 TO 109
						LOCAL:4 = 58
					CASEELSE
						LOCAL:4 = 59
				ENDSELECT
				CALL ADD_ITEM(LOCAL:4, RAND:2 + 1, 1)
			NEXT
		ENDIF
		
		;汚染されていると触手化する教室
		IF FLAG:(CFLAG:1:4 + 50) >= 40
			PRINTFORML %CALLNAME:1%%TACHI()%は誰もいない教室でゆっくり休もうとした……
			PRINTFORML しかし座っていた椅子が急に触手になり%CALLNAME:1%%TACHI()%の体をいじりだした！
			FOR LCOUNT, 0, 3
				IF GET_MEMBER(LCOUNT)
					CALL PRINT_MESSAGE(1, 200, 158, CALLNAME:1, "", 1)
					TFLAG:63 = RAND:4
					IF TFLAG:63 == 3
						CALL LOST_VIRGIN((LCOUNT), 1, 4)
					ENDIF
					LOCAL:5 = CALC_SENSITIVITY((LCOUNT), TFLAG:63, 50)
					FOR LOCAL:0, 0, 3 + RAND:4
						CALL DAMAGE_COMMON((LCOUNT), 2, LOCAL:5 * 7 * (TALENT:(LCOUNT):104 + 2) * (TALENT:(LCOUNT):78 + 1), 500)
					NEXT
				ENDIF
			NEXT
			PRINTFORML %CALLNAME:1%%TACHI()%はなんとか逃げ出した……
			
			
		ELSE
			PRINTFORML %CALLNAME:1%%TACHI()%は誰もいない教室でゆっくり休んだ……
			;睡眠より回復量が少なめ
			FOR LCOUNT, 0, 3
				IF GET_MEMBER(LCOUNT)
					;汚染度補正
					IF FLAG:(CFLAG:(LCOUNT):4 + 50) < 10
						LOCAL:4 = 100
					ELSEIF FLAG:(CFLAG:(LCOUNT):4 + 50) < 40
						LOCAL:4 = 90
					ENDIF
					
					BASE:(LCOUNT):0 += MAXBASE:(LCOUNT):0 * LOCAL:4 / 10000 / 5
					IF BASE:(LCOUNT):0 > MAXBASE:(LCOUNT):0
						BASE:(LCOUNT):0 = MAXBASE:(LCOUNT):0
					ENDIF
					BASE:(LCOUNT):1 += MAXBASE:(LCOUNT):1 * LOCAL:4 / 10000 / 8
					IF BASE:(LCOUNT):1 > MAXBASE:(LCOUNT):1
						BASE:(LCOUNT):1 = MAXBASE:(LCOUNT):1
					ENDIF
					BASE:(LCOUNT):2 += MAXBASE:(LCOUNT):2 * LOCAL:4 / 100 / 15
					IF BASE:(LCOUNT):2 > MAXBASE:(LCOUNT):2
						BASE:(LCOUNT):2 = MAXBASE:(LCOUNT):2
					ENDIF
				ENDIF
			NEXT
			FLAG:7 -= 2
			CALL PRINT_MESSAGE(1, 200, 155, CALLNAME:1, "", 1)
			CALL PRINT_MESSAGE(FLAG:10, 200, 155, CALLNAME:1, "", 1)
			CALL PRINT_MESSAGE(FLAG:11, 200, 155, CALLNAME:1, "", 1)
		ENDIF
		WAIT
	ELSEIF RESULT == 1
		;開けない場合、イベントは残す
		CALL PRINT_MESSAGE(1, 200, 155, CALLNAME:1, "", 2)
		CALL PRINT_MESSAGE(FLAG:10, 200, 155, CALLNAME:1, "", 2)
		CALL PRINT_MESSAGE(FLAG:11, 200, 155, CALLNAME:1, "", 2)
	ELSE
		REUSELASTLINE 入力値が不正です。再入力してください
		GOTO INPUT_LOOP
	ENDIF
	LOCAL:3 = 1
	;初回にアイテムを発見させたいのでここで初発見フラグを足す
	IF FLAG:158 < 100
		FLAG:158 += 100
	ENDIF
ELSE
	;見つからなかった場合（通常は何もしない）
ENDIF

RETURN LOCAL:3

