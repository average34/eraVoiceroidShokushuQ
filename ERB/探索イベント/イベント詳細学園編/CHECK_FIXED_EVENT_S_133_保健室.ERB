;固定イベント
;RETURN	0:失敗した　1:成功した　2:以後のイベントスキップ
@CHECK_FIXED_EVENT_S_133
#DIM LCOUNT
;LOCAL
;0	LOOP
;1	目標値
;2	達成値
;3	戻り値
;4	アイテムの種類決定

LOCAL:3 = 0
LOCAL:4 = 0

;目標値（イベント難易度）決定
LOCAL:1 = 60


;達成値決定
CALL GET_SEARCH_RATE
LOCAL:2 = RESULT


IF LOCAL:2 >= LOCAL:1
	;開けなかった場合、何度も経験値が入るという小技
	CALL ADD_ABL_EXP_PT(0, 700)
	PRINTFORML ここは保健室のようだ！どうしますか？
	
	$INPUT_LOOP
	PRINTFORML [0] 休む
	PRINTFORML [1] 家探しする
	PRINTFORML [9] 立ち去る


	INPUT

	IF RESULT == 0
		PRINTFORML %CALLNAME:1%%TACHI()%は整えられたベッドでゆっくり休んだ……
		;睡眠より回復量が少なめ
		FOR LCOUNT, 0, 3
			IF GET_MEMBER(LCOUNT)
				;汚染度補正
				IF FLAG:(CFLAG:(LCOUNT):4 + 50) < 10
					LOCAL:4 = 100
				ELSEIF FLAG:(CFLAG:(LCOUNT):4 + 50) < 40
					LOCAL:4 = 90
				ELSEIF FLAG:(CFLAG:(LCOUNT):4 + 50) < 70
					LOCAL:4 = 75
				ELSEIF FLAG:(CFLAG:(LCOUNT):4 + 50) < 100
					LOCAL:4 = 70
				ELSE
					LOCAL:4 = 40
				ENDIF
				
				BASE:(LCOUNT):0 += MAXBASE:(LCOUNT):0 * LOCAL:4 / 100 / 2
				IF BASE:(LCOUNT):0 > MAXBASE:(LCOUNT):0
					BASE:(LCOUNT):0 = MAXBASE:(LCOUNT):0
				ENDIF
				BASE:(LCOUNT):1 += MAXBASE:(LCOUNT):1 * LOCAL:4 / 100 / 4
				IF BASE:(LCOUNT):1 > MAXBASE:(LCOUNT):1
					BASE:(LCOUNT):1 = MAXBASE:(LCOUNT):1
				ENDIF
				BASE:(LCOUNT):2 += MAXBASE:(LCOUNT):2 * LOCAL:4 / 100 / 15
				IF BASE:(LCOUNT):2 > MAXBASE:(LCOUNT):2
					BASE:(LCOUNT):2 = MAXBASE:(LCOUNT):2
				ENDIF
			ENDIF
		NEXT
		FLAG:7 -= 2
		CALL PRINT_MESSAGE(1, 200, 132, CALLNAME:1, "")
		CALL PRINT_MESSAGE(FLAG:10, 200, 132, CALLNAME:1, "")
		CALL PRINT_MESSAGE(FLAG:11, 200, 132, CALLNAME:1, "")
		WAIT
	ELSEIF RESULT == 1
		
		PRINTFORML %CALLNAME:1%%TACHI()%は保健室を探してみた…
		PRINTFORML アイテムが見つかった！
		CALL PRINT_MESSAGE(1, 200, 132, CALLNAME:1, "", 1)
		CALL PRINT_MESSAGE(FLAG:10, 200, 132, CALLNAME:1, "", 1)
		CALL PRINT_MESSAGE(FLAG:11, 200, 132, CALLNAME:1, "", 1)
		FOR LCOUNT, 0,(GET_PT_MAX_ABL(1) + 1)
			SELECTCASE RAND:120
				CASE 0 TO 9
					LOCAL:4 = 8
				CASE 10 TO 19
					LOCAL:4 = 39
				CASE 20 TO 29
					LOCAL:4 = 54
				CASE 30 TO 39
					LOCAL:4 = 55
				CASE 40 TO 49
					LOCAL:4 = 56
				CASE 50 TO 59
					LOCAL:4 = 57
				CASE 60 TO 69
					LOCAL:4 = 58
				CASE 70 TO 79
					LOCAL:4 = 67
				CASE 80 TO 89
					LOCAL:4 = 67
				CASE 90 TO 94
					LOCAL:4 = 77
				CASE 95 TO 99
					LOCAL:4 = 78
				CASE 100 TO 109
					LOCAL:4 = 69
				CASEELSE
					LOCAL:4 = 73
			ENDSELECT
			CALL ADD_ITEM(LOCAL:4, RAND:3 + 1, 1)
		NEXT
		IF RAND:6 == 0
			;確率で荒らしすぎて使えなくなる
			FLAG:133 = 0
			PRINTFORMW 家探ししすぎてもう何も残っていないし休むのも難しくなってしまった……
		ENDIF
		WAIT
	ELSEIF RESULT == 9
		;開けない場合、イベントは残す
		CALL PRINT_MESSAGE(1, 200, 132, CALLNAME:1, "", 2)
		CALL PRINT_MESSAGE(FLAG:10, 200, 132, CALLNAME:1, "", 2)
		CALL PRINT_MESSAGE(FLAG:11, 200, 132, CALLNAME:1, "", 2)
	ELSE
		REUSELASTLINE 入力値が不正です。再入力してください
		GOTO INPUT_LOOP
	ENDIF
	LOCAL:3 = 1
ELSE
	;見つからなかった場合（通常は何もしない）
ENDIF

RETURN LOCAL:3

