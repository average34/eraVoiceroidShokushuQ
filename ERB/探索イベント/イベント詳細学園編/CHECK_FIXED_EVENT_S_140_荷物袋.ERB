;固定イベント
;RETURN	0:失敗した　1:成功した　2:以後のイベントスキップ
@CHECK_FIXED_EVENT_S_140
;LOCAL
;0	LOOP
;1	目標値
;2	達成値
;3	戻り値
;4	アイテムの種類決定

LOCAL:3 = 0
LOCAL:4 = 0

;目標値（イベント難易度）決定
LOCAL:1 = 60


;達成値決定
CALL GET_SEARCH_RATE
LOCAL:2 = RESULT


IF LOCAL:2 >= LOCAL:1
	;開けなかった場合、何度も経験値が入るという小技
	CALL ADD_ABL_EXP_PT(0, 700)
	PRINTFORML 誰かの荷物袋が落ちている！　開けてみますか？
	
	$INPUT_LOOP
	PRINTFORML [0] はい
	PRINTFORML [1] いいえ
	IF GET_PENIS(1) > 0
		PRINTFORML [2] %CALLNAME:1%のおちんちんを中に入れて安全を確認する
	ENDIF
	IF FLAG:10 && GET_PENIS(FLAG:10) > 0
		PRINTFORML [3] %CALLNAME:(FLAG:10)%のおちんちんを中に入れて安全を確認する
	ENDIF
	IF FLAG:11 && GET_PENIS(FLAG:11) > 0
		PRINTFORML [4] %CALLNAME:(FLAG:11)%のおちんちんを中に入れて安全を確認する
	ENDIF

	INPUT

	IF RESULT == 0 || (RESULT == 2 && GET_PENIS(1) > 0) || (RESULT == 3 && GET_PENIS(FLAG:10) > 0) || (RESULT == 4 && GET_PENIS(FLAG:11) > 0)
		IF RESULT >= 2
			PRINTFORML おちんちんを中に入れたがどうやら安全なようだ！
		ENDIF
		;何度も発生
		FLAG:140 = RAND:30 + 1
		
		PRINTFORML %CALLNAME:1%%TACHI()%は荷物袋を開けてみた…
		PRINTFORML 中にはアイテムが入っていた！
		CALL PRINT_MESSAGE(1, 200, 140, CALLNAME:1, "")
		CALL PRINT_MESSAGE(FLAG:10, 200, 140, CALLNAME:1, "")
		CALL PRINT_MESSAGE(FLAG:11, 200, 140, CALLNAME:1, "")
		FOR LOCAL:0, 0,(GET_PT_MAX_ABL(1) + 1)
			SELECTCASE RAND:120
				CASE 0 TO 9
					LOCAL:4 = 8
				CASE 10 TO 19
					LOCAL:4 = 50
				CASE 20 TO 29
					LOCAL:4 = 52
				CASE 30 TO 39
					LOCAL:4 = 54
				CASE 40 TO 49
					LOCAL:4 = 57
				CASE 50 TO 59
					LOCAL:4 = 58
				CASE 60 TO 69
					LOCAL:4 = 59
				CASE 70 TO 79
					LOCAL:4 = 67
				CASE 80 TO 89
					LOCAL:4 = 39
				CASE 90 TO 94
					LOCAL:4 = 51
				CASE 95 TO 99
					LOCAL:4 = 53
				CASE 100 TO 109
					LOCAL:4 = 55
				CASEELSE
					LOCAL:4 = 56
			ENDSELECT
		CALL ADD_ITEM(LOCAL:4, RAND:3 + 1, 1)
		NEXT
		WAIT
	ELSEIF RESULT == 1
		;開けない場合、イベントは残す
		CALL PRINT_MESSAGE(1, 200, 140, CALLNAME:1, "", 1)
		CALL PRINT_MESSAGE(FLAG:10, 200, 140, CALLNAME:1, "", 1)
		CALL PRINT_MESSAGE(FLAG:11, 200, 140, CALLNAME:1, "", 1)
	ELSE
		REUSELASTLINE 入力値が不正です。再入力してください
		GOTO INPUT_LOOP
	ENDIF
	LOCAL:3 = 1
ELSE
	;見つからなかった場合（通常は何もしない）
ENDIF

RETURN LOCAL:3

