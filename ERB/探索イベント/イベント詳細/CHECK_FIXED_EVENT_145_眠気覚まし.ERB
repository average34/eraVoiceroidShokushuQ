;固定イベント
;RETURN	0:失敗した　1:成功した　2:以後のイベントスキップ
@CHECK_FIXED_EVENT_145
;LOCAL
;0	LOOP
;1	目標値
;2	達成値
;3	戻り値

LOCAL:3 = 0

;目標値（イベント難易度）決定
LOCAL:1 = 50


;達成値決定
CALL GET_SEARCH_RATE
LOCAL:2 = RESULT


IF LOCAL:2 >= LOCAL:1 || FLAG:145 >= 100
	;経験値が入るのは最初だけ
	IF FLAG:145 < 100
		CALL ADD_ABL_EXP_PT(0, 500)
		FLAG:145 += 100
	ENDIF
	PRINTFORML 迷宮の隅に薬草が生えているのを発見した！
	PRINTFORML この薬草は眠気を解消する効果があるようだ…
	PRINTFORML 今すぐこの薬草を使いますか？（現在の眠気：{FLAG:7}）
	CALL ASK_YN
	IF RESULT == 0
		;使う場合、イベント削除
		FLAG:145 = 0
		
		PRINTFORMW %CALLNAME:1%%TACHI()%の眠気が吹き飛んだ！
		CALL PRINT_MESSAGE(1, 200, 145, CALLNAME:1, "", 1)
		CALL PRINT_MESSAGE(FLAG:10, 200, 145, CALLNAME:1, "", 1)
		CALL PRINT_MESSAGE(FLAG:11, 200, 145, CALLNAME:1, "", 1)
		FLAG:7 = -1
	ELSE
		;使わない場合、イベントは残す
		PRINTFORMW %CALLNAME:1%%TACHI()%は薬草を摘まずに残しておくことにした…
		CALL PRINT_MESSAGE(1, 200, 145, CALLNAME:1, "", 2)
		CALL PRINT_MESSAGE(FLAG:10, 200, 145, CALLNAME:1, "", 2)
		CALL PRINT_MESSAGE(FLAG:11, 200, 145, CALLNAME:1, "", 2)
	ENDIF
	LOCAL:3 = 1
ELSE
	;見つからなかった場合（通常は何もしない）
ENDIF

RETURN LOCAL:3

