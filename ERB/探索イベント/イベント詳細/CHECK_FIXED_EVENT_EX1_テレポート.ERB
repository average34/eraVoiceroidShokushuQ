;固定イベント
;RETURN	0:失敗した　1:成功した　2:以後のイベントスキップ
@CHECK_FIXED_EVENT_EX1

CALL ADD_ABL_EXP_PT(0, 500)

;危機回避と術式知識が高いと避けられる
IF (ABL:1:5 + TALENT:1:164 + RAND:2 > 2) || (FLAG:10 && ABL:(FLAG:10):5 + TALENT:(FLAG:10):164  + RAND:2 > 2) || (FLAG:11 && ABL:(FLAG:11):5 + TALENT:(FLAG:11):164 + RAND:2 > 2)
	PRINTFORML 起動していない魔法陣を見つけた
	IF TALENT:1:164 == 2 || (FLAG:10 && TALENT:(FLAG:10):164 == 2) || (FLAG:11 && TALENT:(FLAG:11):164 == 2)
		PRINTFORM どうやらテレポートの魔法陣のようだが
	ENDIF
	PRINTFORML 入りますか？
	CALL ASK_YN
	SIF RESULT == 1
		RETURN 2
ENDIF

IF FLAG:AUTOMODE
    PRINTFORML 突然%CALLNAME:1%%TACHI()%は強い光に包まれた！
    TWAIT FLAG:AUTOMODE * 400, 0
ELSE
    PRINTFORMW  突然%CALLNAME:1%%TACHI()%は強い光に包まれた！
ENDIF

CALL PRINT_MESSAGE(1, 201, 0, CALLNAME:1, "")
CALL PRINT_MESSAGE(FLAG:10, 201, 0, CALLNAME:1, "")
CALL PRINT_MESSAGE(FLAG:11, 201, 0, CALLNAME:1, "")

; テレポートと同時にPTメンバーが外れる
; FLAG:10から削除を行うとFLAG:11のキャラがFLAG:10にスライドしてくるのでFLAG:11から処理
CALL RANDOM_TELEPORT(1)
CALL RANDOM_TELEPORT(FLAG:11)
CALL RANDOM_TELEPORT(FLAG:10)

FLAG:5 = CFLAG:1:4
FLAG:(600 + FLAG:5) = 1

IF FLAG:AUTOMODE
    PRINTFORML 咄嗟に目を瞑った%CALLNAME:1%が恐る恐る目を開けると、全く別の部屋に転送されていた…
    TWAIT FLAG:AUTOMODE * 400, 0
ELSE
    PRINTFORMW  咄嗟に目を瞑った%CALLNAME:1%が恐る恐る目を開けると、全く別の部屋に転送されていた…
ENDIF

IF FLAG:10 && FLAG:11
	IF FLAG:AUTOMODE
	    PRINTFORML さっきまで一緒に居たはずの%CALLNAME:(FLAG:10)%達の姿も見えない。　どうやら別の部屋に転送されたようだ…
	    TWAIT FLAG:AUTOMODE * 400, 0
	ELSE
	    PRINTFORMW  さっきまで一緒に居たはずの%CALLNAME:(FLAG:10)%達の姿も見えない。　どうやら別の部屋に転送されたようだ…
	ENDIF

ELSEIF FLAG:10
	IF FLAG:AUTOMODE
	    PRINTFORML さっきまで一緒に居たはずの%CALLNAME:(FLAG:10)%の姿も見えない。　どうやら別の部屋に転送されたようだ…
	    TWAIT FLAG:AUTOMODE * 400, 0
	ELSE
	    PRINTFORMW  さっきまで一緒に居たはずの%CALLNAME:(FLAG:10)%の姿も見えない。　どうやら別の部屋に転送されたようだ…
	ENDIF

ENDIF
CALL PRINT_MESSAGE(1, 201, 1, CALLNAME:1, "")

RETURN 2


@RANDOM_TELEPORT(ARG:0)

IF ARG:0 == 0
	RETURN
ENDIF

LOCAL:1 = RAND:30 + 1

CFLAG:(ARG:0):4 = LOCAL:1
CFLAG:(ARG:0):13 = LOCAL:1
CFLAG:(ARG:0):14 = LOCAL:1

CALL MEMBER_EXIT_ID(ARG:0)


