;ゲームオーバー
;クリア時もここに処理をまとめる
;ARG:0	
@GAMEOVER(ARG:0)

;クイーン撃破
IF ARG:0 == 0
	IF CHARANUM <= 2
		;完全ソロ撃破
		CALL PRINT_MESSAGE(1, 212, 0, CALLNAME:1)
		FLAG:214 = 0
	ELSE
		LOCAL:1 = 0
		FOR LOCAL:0, 2, CHARANUM
			IF CFLAG:(LOCAL:0):2 == 0 && CFLAG:(LOCAL:0):3 == 0
				LOCAL:1 = 1
				BREAK
			ENDIF
		NEXT
		
		IF LOCAL:1
			;普通に撃破
			CALL PRINT_MESSAGE(1, 210, 0, CALLNAME:1)
			IF FLAG:214 == 0
				CALL PRINT_MESSAGE(FLAG:10, 211, 0, CALLNAME:1)
				CALL PRINT_MESSAGE(FLAG:11, 211, 0, CALLNAME:1)
			ELSEIF FLAG:214 >= 1
				DA:2:1 = 0
				DA:3:1 = 0
				RETURN
			ENDIF
		ELSE
			;ソロ撃破
			CALL PRINT_MESSAGE(1, 212, 0, CALLNAME:1)
			FLAG:214 = 0
		ENDIF
	ENDIF
	CALL HISTORY(0)
ENDIF

;ガーディアン撃破
IF ARG:0 == 1
	CALL PRINT_MESSAGE(1, 206, 0, CALLNAME:1)
	;門番撃破後パッチ、選択肢によって続行可能
	IF FLAG:213 == 0
		CALL PRINT_MESSAGE(FLAG:10, 207, 0, CALLNAME:1)
		CALL PRINT_MESSAGE(FLAG:11, 207, 0, CALLNAME:1)
		CALL HISTORY(1)
	ELSEIF FLAG:213 == 1 || FLAG:213 == 2
		RETURN
	ENDIF
ENDIF

;ガーディアンに味方２人が捕まった状態で逃げると囮エンド
IF ARG:0 == 2
	CALL PRINT_MESSAGE(1, 204, 0, CALLNAME:1)
	CALL PRINT_MESSAGE(FLAG:10, 205, 0, CALLNAME:1)
	CALL PRINT_MESSAGE(FLAG:11, 205, 0, CALLNAME:1)
	CALL HISTORY(2)
ENDIF


;石版エンド
IF ARG:0 == 3
	CALL PRINT_MESSAGE(1, 208, 0, CALLNAME:1)
	CALL PRINT_MESSAGE(FLAG:10, 209, 0, CALLNAME:1)
	CALL PRINT_MESSAGE(FLAG:11, 209, 0, CALLNAME:1)
	CALL HISTORY(3)
ENDIF

;敗北エンド
IF ARG:0 == 4
	LOCAL:1 = CFLAG:1:103
	CALL PRINT_MESSAGE(1, 157, LOCAL:1, CALLNAME:1, ENEMY_NAME(LOCAL:1))
	CALL HISTORY(4)
ENDIF

;淫らでニューゲーム
;クイーンやガーディアン撃破をした場合エンドレスモードがあるがそれとは別
$INPUT_LOOP
PRINTFORML 
PRINTFORML 開発度合いそのままに最初からやり直しますか？
PRINTFORML [ 0] はい
PRINTFORML [ 1] いいえ
PRINTFORML [ 2] 主人公のみ変更してやり直す
PRINTFORML [99] キャラのステータスを確認する
PRINTFORML 


INPUT
IF RESULT == 0
	FLAG:3 += 1
	FLAG:49 = 0
	CALL NEWGAME
ELSEIF RESULT == 1
	RESETDATA
	BEGIN TITLE
ELSEIF RESULT == 2
	CALL CHARA_SELECT
	FLAG:96 = RESULT
	CALL NEWGAME
ELSEIF RESULT == 99
	FLAG:49 = ARG:0 + 1
	CALL SHOW_FINALCHARADATA
	GOTO INPUT_LOOP
ELSE
	CLEARLINE 1
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF



@SHOW_FINALCHARADATA
#DIM LCOUNT


	$INPUT_LOOP_2
	DRAWLINE
	FOR LCOUNT, 1, MIN(CHARANUM,30)
		IF CFLAG:(LCOUNT):4 != 59
			PRINTFORM [%@"{LCOUNT}",3,RIGHT%] %CALLNAME:LCOUNT,14,LEFT% 
			
			PRINTFORM 現在{CFLAG:(LCOUNT):4}番の部屋で
				IF LCOUNT == 1 || LCOUNT == FLAG:10 || LCOUNT == FLAG:11
					IF FLAG:49 == 1
						PRINTFORML クイーンを倒し脱出した
					ELSEIF FLAG:49 == 2
						PRINTFORML ガーディアンを倒し脱出した
					ELSEIF FLAG:49 == 3
						IF LCOUNT == 1
							PRINTFORML ガーディアンを倒し脱出した
						ELSEIF LCOUNT == FLAG:10 || LCOUNT == FLAG:11
							PRINTFORML %CALLNAME:1%が脱出するための囮にされた…
						ENDIF
					ELSEIF FLAG:49 == 4
						PRINTFORML 石版の謎をとき脱出した
					ELSEIF FLAG:49 == 5
						IF LCOUNT == 1
							PRINTFORML %ENEMY_NAME(CFLAG:1:103)%に敗北した…
						ELSEIF LCOUNT == FLAG:10 || LCOUNT == FLAG:11
							PRINTFORML %CALLNAME:1%とともに%ENEMY_NAME(CFLAG:1:103)%に敗北した…
						ENDIF
					ELSE
						PRINTFORML うろうろしている
					ENDIF
				ELSEIF CFLAG:(LCOUNT):2
					IF CFLAG:(LCOUNT):58
						PRINTFORML %ENEMY_NAME(CFLAG:(LCOUNT):103)%に殺害され、物言わぬ屍と化している…
					;悪堕ち転生
					ELSEIF TALENT:(LCOUNT):73>0
						IF TALENT:(LCOUNT):73==101
							PRINTFORML 屈服、迷宮の主に忠誠を誓っている…
						;「〇〇化」だとエネミー名が不自然なので決め打ち
						ELSEIF TALENT:(LCOUNT):73==103
							PRINTFORML 敗北、アルラウネとして生まれ変わった…
						ELSEIF TALENT:(LCOUNT):73==104
							PRINTFORML 敗北、スライムガールとして生まれ変わった…
						ELSEIF TALENT:(LCOUNT):73==105
							PRINTFORML 敗北、ラミアとして生まれ変わった…
						ELSE
							PRINTFORML 完全敗北、%ENEMY_NAME(TALENT:(LCOUNT):73)%として生まれ変わった…
						ENDIF
					;通常ロスト
					ELSE
						PRINTFORML %ENEMY_NAME(CFLAG:(LCOUNT):103)%に敗北し良質な苗床と化している
					ENDIF
				ELSEIF CFLAG:(LCOUNT):10
					PRINTFORML %ENEMY_NAME(CFLAG:(LCOUNT):10)%に捕まっている
				ELSEIF CFLAG:(LCOUNT):29
					PRINTFORML 戦意を喪失している
				ELSE
					PRINTFORML うろうろしている
				ENDIF
		ENDIF
	NEXT
	PRINTL
	PRINTL [ 0] 戻る

	INPUT
	
	IF RESULT == 0
		REDRAW 1
		RETURN
	ELSEIF 1 <= RESULT && RESULT < MIN(CHARANUM,30)
		LOCAL:1 = RESULT
		PRINTFORML {LOCAL:1}
		CALL CHECK_STATUS(LOCAL:1)
		GOTO INPUT_LOOP_2
	ELSE
		REUSELASTLINE 入力が不正です！
		CLEARLINE 1
		GOTO INPUT_LOOP_2
	ENDIF

