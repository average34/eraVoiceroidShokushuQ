;Ver1.2に伴い、中身を書き換え。レートがゆるめに。それでも渋いけど。
;バグ指摘してくださった方ありがとうございました。
;ああしてテンキーに対応するとPRINTBUTTON使う意味が特にないので（かといってアレ以外の方法も特に思いつかないので、）、PRINTFORMLにしてます。
@PET_EXP_SHOP
#DIM LCOUNT      ; ループカウンタ
#DIM IN_CMD      ; 選んだサービス/アイテムの番号
#DIM CHARA_ID    ; サービスを受ける少女のID
#DIM COST, 100   ; 各サービス/アイテムに必要な栄養値。0なら選択不可
VARSET COST
DRAWLINE
PRINTFORML 栄養を消費し、アイテムやサービスを受けられます
PRINTFORML 何をしますか？ 栄養:{PET:21}
PRINTL
;----------------
;リスト描画処理
;----------------
;個別のもの
;栄養足りてないときに灰色で描く処理をいちいち作るのがめんどくさいのでFOR化
PRINTFORML ----サービス----
FOR LCOUNT, 1, 6
	SELECTCASE LCOUNT
		CASE 1
			COST:LCOUNT = 100
		CASE 2
			COST:LCOUNT = 100
		CASE 3
			SIF FLAG:520
				COST:LCOUNT = 100
		CASE 4
			COST:LCOUNT = 150
		CASE 5
			COST:LCOUNT = 50
	ENDSELECT
	COST:LCOUNT = COST:LCOUNT * (100 + PET_SUBMISSION_PT() / GET_MEMBER_SUM()) / 100
	SIF COST:LCOUNT && !PET_EXP_ENOUGH(COST:LCOUNT)
		SETCOLOR 0X333333
	SELECTCASE LCOUNT
		CASE 1
			PRINTFORML [ 1] - %"回復",15,LEFT% {COST:1}
		CASE 2
			PRINTFORML [ 2] - %"服を着る",15,LEFT% {COST:2}
		CASE 3
			SIF FLAG:520
				PRINTFORML [ 3] - %"武器入手",15,LEFT% {COST:3}
		CASE 4
			PRINTFORML [ 4] - %"腹を満たす",15,LEFT% {COST:4}
		CASE 5
			PRINTFORML [ 5] - %"目を覚ます",15,LEFT% {COST:5}
	ENDSELECT
	RESETCOLOR
NEXT
PRINTFORML
PRINTFORML ----生産----
;今後生産できるアイテムを追加する場合、まずそのIDのCASEを追加
;んでCOST:LCOUNTにそのレートを書き加えてください。
FOR LCOUNT, 50, 72
	SELECTCASE LCOUNT
		CASE 50
			COST:LCOUNT = 75
		CASE 51
			COST:LCOUNT = 150
		CASE 52
			COST:LCOUNT = 100
		CASE 53
			COST:LCOUNT = 200
		CASE 54
			COST:LCOUNT = 150
		CASE 55
			COST:LCOUNT = 50
		CASE 56
			COST:LCOUNT = 50
		CASE 57
			COST:LCOUNT = 150
		CASE 58
			COST:LCOUNT = 250
		CASE 59
			COST:LCOUNT = 50
		CASE 61
			COST:LCOUNT = 500
		CASE 62
			COST:LCOUNT = 500
		CASE 63
			COST:LCOUNT = 500
		CASE 70
			COST:LCOUNT = 70
		CASE 71
			COST:LCOUNT = 100
	ENDSELECT
	IF COST:LCOUNT
		COST:LCOUNT = COST:LCOUNT * (100 + PET_SUBMISSION_PT() / GET_MEMBER_SUM()) / 100
		IF GET_ITEM_MAX(LCOUNT) > -1 && GET_ITEM_MAX(LCOUNT) <= ITEM:LCOUNT
			SETCOLOR 0X333333
			PRINTFORML [{LCOUNT,2,RIGHT}] - %ITEMNAME:LCOUNT,15,LEFT%は既に十分持っている
			RESETCOLOR
		ELSEIF !PET_EXP_ENOUGH(COST:LCOUNT)
			SETCOLOR 0X333333
			PRINTFORML [{LCOUNT,2,RIGHT}] - %ITEMNAME:LCOUNT,15,LEFT% {COST:LCOUNT}
			RESETCOLOR
		ELSE
			PRINTFORML [{LCOUNT,2,RIGHT}] - %ITEMNAME:LCOUNT,15,LEFT% {COST:LCOUNT}
		ENDIF
	ENDIF
NEXT
PRINTL
PRINTFORML [ 0] - 戻る
;----------------
;リスト描画処理終わり
;----------------

$INPUT_LOOP
INPUT

;----------------
;各処理
;----------------

IN_CMD = RESULT

IF IN_CMD == 0
	RETURN
ELSEIF IN_CMD < 0 || IN_CMD > 99 || !COST:IN_CMD
	REUSELASTLINE 不正入力です。正しい値を入力してください。
	GOTO INPUT_LOOP
ELSEIF PET_EXP_ENOUGH(COST:IN_CMD) == 0
	REUSELASTLINE 栄養が足りていないようだ……
	GOTO INPUT_LOOP
ELSEIF GET_ITEM_MAX(IN_CMD) >= 0 && ITEM:IN_CMD >= GET_ITEM_MAX(IN_CMD)
	REUSELASTLINE %ITEMNAME:IN_CMD%は既に十分持っているようだ……
	GOTO INPUT_LOOP
ENDIF



IF IN_CMD == 1
	PRINTFORML 誰が回復しますか？
	CALL SELECT_PT_TARGET2(29)
	IF RESULT > 0
		CHARA_ID = RESULT
		SELECTCASE RAND:100
			CASE 0 TO 50
				LOCAL = 0 ; HP
			CASE 51 TO 80
				LOCAL = 2 ; EP
			CASE 81 TO 99
				LOCAL = 1 ; MP
		ENDSELECT
		PRINTFORM %CALLNAME:CHARA_ID%の%BASENAME:LOCAL%が、全快した！
		BASE:CHARA_ID:LOCAL = MAXBASE:CHARA_ID:LOCAL
	ELSE
		RESTART
	ENDIF
ELSEIF IN_CMD == 2
	PRINTFORML 誰が服を着ますか？
	CALL SELECT_PT_TARGET2(0)
	IF RESULT > 0
		CHARA_ID = RESULT
		CALL CLOTH_TYPE_1(CHARA_ID)
		BASE:CHARA_ID:3 = MAXBASE:CHARA_ID:3
		SIF CFLAG:CHARA_ID:501 == 15
			CFLAG:CHARA_ID:47 = 1
		CALL PRINT_MESSAGE(CHARA_ID, 36, 54, CALLNAME:CHARA_ID, "")
		CALL SYOKUSYU_HUKU(CHARA_ID)
	ELSE
		RESTART
	ENDIF
ELSEIF IN_CMD == 3 && FLAG:520
	PRINTFORML 誰に武器を与えますか？
	CALL SELECT_PT_TARGET2(-1)
	IF RESULT > 0
		CHARA_ID = RESULT
		PRINTFORM %CALLNAME:CHARA_ID%は服を脱ぐと、
		SIF ABL:CHARA_ID:射精中毒
			PRINTFORM 心なしか頬を上気させ、
		PRINTFORML 横たわった……
		PRINTFORML 無防備な%CALLNAME:CHARA_ID%に%PETNAME%が触手を殺到させる！
		FOR LCOUNT, 0, (RAND:5 + (2 - PET_STARVE()) + 3)
			TFLAG:63 = 2
			CALL PRINT_MESSAGE(CHARA_ID, 200, 148, CALLNAME:CHARA_ID, "", 1)
			LOCAL = CALC_SENSITIVITY(1, TFLAG:63, COST:3)
			CALL DAMAGE_COMMON(CHARA_ID, 2, LOCAL * 7 * (TALENT:CHARA_ID:103 + 2) * (TALENT:CHARA_ID:77 + 1), 500)
		NEXT
		PRINTFORML %CALLNAME:CHARA_ID%は【ふたなり】になってしまった！
		CALL PRINT_MESSAGE(CHARA_ID, 200, 148, CALLNAME:CHARA_ID, "", 3)
		CFLAG:CHARA_ID:42 += 1 + RAND:4 + RAND:3 + RAND:2
		IF RAND:4 == 0 && TALENT:CHARA_ID:103 <= 2
			PRINTFORMW %CALLNAME:CHARA_ID%の身体の様子がおかしい。どうやら、触手からの責めによって体質が書き換えられてしまったようだ…
			; 寸前でふたなり化しているので分岐の必要は無いと思うが、一応別の箇所と同一処理
			IF GET_PENIS(CHARA_ID)
				PRINTFORMW %CALLNAME:CHARA_ID%の陰茎が少し敏感になった
			ELSE
				PRINTFORMW %CALLNAME:CHARA_ID%のクリトリスが少し敏感になった
			ENDIF
			TALENT:CHARA_ID:103 += 1
		ENDIF
	ELSE
		RESTART
	ENDIF
ELSEIF IN_CMD == 4
	PRINTFORML 誰のお腹を満たしますか？
	CALL SELECT_PT_TARGET2(0)
	IF RESULT > 0
		CHARA_ID = RESULT
		CALL PRINT_MESSAGE(CHARA_ID, 200, 160, CALLNAME:1, "", 2, 0)
		FOR LCOUNT, 0, 20
			TFLAG:63 = 3
			CALL DAMAGE_COMMON(CHARA_ID, 2, 30*(RAND:5+10)* (TALENT:CHARA_ID:(101+TFLAG:63) + 2), 50)
		NEXT
		CALL PRINT_MESSAGE(CHARA_ID, 200, 160, CALLNAME:1, "", 2, 1)
		IF CFLAG:CHARA_ID:22 == 0
			CALL RANDOM_PARASITE(CHARA_ID, 1, 1)
		ENDIF
	ELSE
		RESTART
	ENDIF
ELSEIF IN_CMD == 5
	PRINTFORMW %PETNAME%が起こしてくれるというので、%CALLNAME:1%%TACHI()%は厚意に甘えることにした……
	FOR LCOUNT, 0, GET_MEMBER_SUM()
		CHARA_ID = GET_MEMBER(LCOUNT)
		SIF PET:0 == 98
			CFLAG:CHARA_ID:43 = 0
		CALL BODY_TO_PET_EXECUTE(CHARA_ID, 2)
	NEXT
	PRINTFORML %PETNAME%にさんざん犯され、%CALLNAME:1%%TACHI()%は目が覚めた！
	FLAG:7 = 0
	;淫乱系素質のランダム追加
	CHARA_ID = GET_MEMBER(RAND:(GET_MEMBER_SUM()))
	LOCAL = 74 + RAND:5
	IF RAND:100 > 70 && TALENT:CHARA_ID:LOCAL == 0
		PRINTFORML 同時に、%CALLNAME:CHARA_ID%が何かに目覚めてしまったようだ……
		PRINTFORML %CALLNAME:CHARA_ID%は[%TALENTNAME:LOCAL%]を得た
		TALENT:CHARA_ID:LOCAL = 1
	ENDIF
;アイテム生産汎用処理
ELSE
	CALL ADD_ITEM(IN_CMD, 1, 1)
ENDIF
PRINTW
PET:21 -= COST:IN_CMD
CALL LIMIT_PET_STATUS
RESTART
