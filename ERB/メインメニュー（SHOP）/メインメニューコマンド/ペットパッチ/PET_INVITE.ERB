@PET_INVITE
#DIM LCOUNT
#DIM 対象
VARSET LOCAL
;LOCAL　仲間になる確率
;LOCAL:1 - 10 仲間になる候補確定用

;すでにいる場合はﾅｰｼ
SIF PET:0
	RETURN

;-----------------
;起き上がり候補確定
;-----------------
FOR LCOUNT, 1, TFLAG:15 + 1
	SIF TFLAG:LCOUNT > 0
		LOCAL:LCOUNT = RAND:100
NEXT
MAX LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6, LOCAL:7, LOCAL:8, LOCAL:9, LOCAL:10
FOR LCOUNT, 1 , TFLAG:15 + 1
	SIF RESULT == LOCAL:LCOUNT
		対象 = TFLAG:LCOUNT
NEXT
;-----------------
;起き上がり候補確定おわり
;-----------------


;-----------------
;起き上がり確率判定おわり
;エネミー図鑑の人のおかげでここの処理がめっちゃ簡単になったよ！！
;-----------------
SELECTCASE DA:対象:86
	CASE 6, 7
		LOCAL = 10
	CASE 4, 5
		LOCAL = 15
	CASE 1, 2, 3
		LOCAL = 20
	CASE 0
		LOCAL = 15
ENDSELECT

;触手使役術
SIF TALENT:1:触手使役術 && CFLAG:1:29 == 0
	LOCAL += 15

SIF FLAG:10 && TALENT:(FLAG:10):触手使役術 && CFLAG:(FLAG:10):29 == 0
	LOCAL += 15

SIF FLAG:11 && TALENT:(FLAG:11):触手使役術 && CFLAG:(FLAG:11):29 == 0
	LOCAL += 15

;特別な処理
SELECTCASE DA:対象:0
	;ヒュプノ、マンボウは倒すの大変なので確定で起き上がる
	;というか、マンボウは倒せるものなのだろうか……？
	CASE 51, 98
		LOCAL = 100
	;追加、ペット専用敵の確率は100固定
	CASE 70
		LOCAL = 100
	;悪堕ちキャラは高い確率でペットに
	CASE 101, 102
		LOCAL = 50
	;転生悪堕ちキャラは必ずペットに
	CASE 103, 104, 105
		LOCAL = 100
	CASEELSE
		;簡易転生悪堕ちキャラも必ずペットに
		IF DA:対象:99>0
			LOCAL = 100
		ENDIF
ENDSELECT

;デバッグなら必ず仲間に
SIF FLAG:9
	LOCAL = 100000000

SIF DA:対象:88
	LOCAL = -1
;-----------------
;起き上がり確率判定おわり
;-----------------


;-----------------
;条件満たさなければ戻す
;-----------------
IF LOCAL < RAND:100
	;悪落ちの場合メッセージ
	IF DA:対象:99 >0
		PRINTFORML 何とか%SAVESTR:(対象)%を気絶させることに成功したものの、
		PRINTFORML 倒れたその体を肉々しい地面が飲み込んでそのままどこかへ隠してしまった。
		IF FLAG:AUTOMODE
			PRINTFORML もっと弱らせるか迷宮そのものを何とかしないと救出できそうにない…
			TWAIT FLAG:AUTOMODE * 400, 0
		ELSE
			PRINTFORMW もっと弱らせるか迷宮そのものを何とかしないと救出できそうにない…
		ENDIF
	ENDIF
	RETURN
ENDIF
;-----------------
;条件満たさなければ戻す処理終わり
;-----------------


;-----------------
;入力及び分岐
;-----------------
IF DA:対象:99 >0
	PRINTFORML ようやく大人しくなった%SAVESTR:対象%は起き上がってこちらを見ている。
	SELECTCASE DA:対象:99
		CASE 3
		PRINTFORML 敵意は無くなったようだが、ぼんやりとしたその瞳は正気に戻ったようには見えない……
		CASE 2
		PRINTFORML 敵意は無くなったようだが、造り変えられた身体の真意までは窺い知れない……
		CASEELSE
		PRINTFORML 虚ろな瞳は何も映すことなく、敵意が無くなったかどうかも定かではない……
	ENDSELECT
	PRINTFORML 連れて行きますか？
ELSE

	IF !TALENT:1:ペット
		PRINTFORML なんと%SAVESTR:対象%が起き上がってこちらをみている！
		PRINTFORML どうやら心を入れ替えたようだ……
		PRINTFORML 仲間にしますか？
	ELSE
		PRINTFORML %SAVESTR:対象%のたくましさに%CALLNAME:1%は魅了されてしまった……
		PRINTFORML ご主人様になってもらうのも悪くないかもしれない
		PRINTFORML なってもらいますか？
	ENDIF
ENDIF
PRINTFORML [1] - はい
PRINTFORML [0] - いいえ
$INPUT_LOOP
IF FLAG:AUTOMODE
	TINPUT FLAG:AUTOMODE * 400, MAX(RAND:30 - 28 ,0), 0, ""
ELSE
	INPUT
ENDIF

IF RESULT == 0
	IF FLAG:AUTOMODE
		PRINTFORML %SAVESTR:対象%は悲しそうに去っていった……
		TWAIT FLAG:AUTOMODE * 400, 0
	ELSE
		IF FLAG:AUTOMODE
			PRINTFORML %SAVESTR:対象%は悲しそうに去っていった……
			TWAIT FLAG:AUTOMODE * 400, 0
		ELSE
			PRINTFORMW %SAVESTR:対象%は悲しそうに去っていった……
		ENDIF
	ENDIF
	RETURN
ELSEIF RESULT != 1
	GOTO INPUT_LOOP
ENDIF
PRINTFORML %SAVESTR:対象%を仲間にした！
PRINTFORML 名前を何にしますか？
PRINTFORML 何も入力しないと種族名が入ります
IF FLAG:AUTOMODE
	TINPUTS FLAG:AUTOMODE * 3000, "", 0, ""
ELSE
	INPUTS
ENDIF
IF RESULTS == ""
	PETNAME = %SAVESTR:対象%
ELSE
	PETNAME = %RESULTS%
ENDIF
IF FLAG:AUTOMODE
	PRINTFORML %PETNAME%を仲間にした！
	TWAIT FLAG:AUTOMODE * 400, 0
ELSE
	PRINTFORMW %PETNAME%を仲間にした！
ENDIF
;悪堕ちキャラの場合元キャラを記憶
;入れるタイミングは地味にここしかない
IF DA:対象:99 >0
	PET:9 = DA:対象:46
ELSE
	PET:9 = 0
ENDIF


CALLFORM PRINT_MESSAGE(1, 514, 3, CALLNAME:1, PETNAME)
IF DA:対象:99 >0
	CALLFORM PRINT_MESSAGE(PET:9, 1514, 3, CALLNAME:1, PETNAME)
ENDIF
CALL PET_INVITE_2(DA:対象:0)

;悪堕ちキャラなど名前が可変な場合の対策
SAVESTR:0 = %PETNAME%

;-----------------
;入力終わり
;-----------------

