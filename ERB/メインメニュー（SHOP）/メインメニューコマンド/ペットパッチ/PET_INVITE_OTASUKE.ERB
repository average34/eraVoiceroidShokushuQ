@PET_INVITE_OTASUKE
#DIM LCOUNT
#DIM 確率,11
#DIM 対象
#DIM 才能レベル,5
#DIMS 才能コメント,5
VARSET LOCAL
;LOCAL　仲間になる確率
;LOCAL:1 - 10 仲間になる候補確定用


PRINTFORML 小悪魔を仲間にした！
PRINTFORML 名前を何にしますか？
PRINTFORML 何も入力しないと小悪魔と名づけます
INPUTS
IF RESULTS == ""
	PETNAME = 小悪魔
ELSE
	PETNAME = %RESULTS%
ENDIF
IF FLAG:AUTOMODE
	PRINTFORML %PETNAME%を仲間にした！
	TWAIT FLAG:AUTOMODE * 400, 0
ELSE
	PRINTFORMW %PETNAME%を仲間にした！
ENDIF
;-----------------
;入力終わり
;-----------------


;-----------------
;初期ステータス決定（固定部分）
;-----------------
;初期ステータスの設定
DA:0:0 = 70
TRYCALLFORM SET_ENEMY_DATA_70(0)
;種族は同じ
PET:0 = DA:0:0
;ランクも同じ
PET:1 = DA:0:86
;レベルは1から
PET:2 = 1
;戦闘素質と速度はﾀｶｲ
PET:10 = 30
PET:11 = 30
;腹具合と好感度は100から
PET:20 = 100
PET:22 = 100
;SET_PET_DATAしとく
TRYCCALLFORM SET_PET_DATA_{PET:0}
CATCH
	CALLFORM SET_PET_DATA_5
ENDCATCH
;-----------------
;初期ステータス決定（固定部分）終わり
;-----------------


;-----------------
;才覚分類
;-----------------
;才能分類
;才能レベルは0ランク 1戦闘　2速度　3やる気　4カッコ良さ

;ランクから
SELECTCASE PET:1
	;念のため！！
	CASE 0
		才能レベル:0 = 1
		才能コメント:0 = ふつう
	;低ランクは中ランクまでしか行かない
	CASE 1
		SELECTCASE RAND:100 + 1
			CASE 1 TO 80
				才能レベル:0 = 0
				才能コメント:0 = ふつう
			CASE 81 TO 87
				才能レベル:0 = 1
				才能コメント:0 = ちょっと
			CASE 88 TO 93
				才能レベル:0 = 2
				才能コメント:0 = なかなか
			CASE 94 TO 97
				才能レベル:0 = 3
				才能コメント:0 = かなり
			CASEELSE
				才能レベル:0 = 4
				才能コメント:0 = がち
		ENDSELECT
	CASE 2
		SELECTCASE RAND:100 + 1
			CASE 1 TO 60
				才能レベル:0 = 0
				才能コメント:0 = ふつう
			CASE 61 TO 85
				才能レベル:0 = -1
				才能コメント:0 = あんまり
			CASE 81 TO 87
				才能レベル:0 = 1
				才能コメント:0 = ちょっと
			CASE 88 TO 93
				才能レベル:0 = 2
				才能コメント:0 = なかなか
			CASEELSE
				才能レベル:0 = 3
				才能コメント:0 = かなり
		ENDSELECT
	CASE 3
		SELECTCASE RAND:100 + 1
			CASE 1 TO 50
				才能レベル:0 = 0
				才能コメント:0 = ふつう
			CASE 51 TO 65
				才能レベル:0 = -2
				才能コメント:0 = ぜんぜん
			CASE 66 TO 85
				才能レベル:0 = -1
				才能コメント:0 = あんまり
			CASE 86 TO 95
				才能レベル:0 = 1
				才能コメント:0 = ちょっと
			CASEELSE
				才能レベル:0 = 2
				才能コメント:0 = なかなか
		ENDSELECT
	;中ランクはランク6までしか行けない
	;かつ、高ランクになる確率は低い
	CASE 4
		SELECTCASE RAND:100
			CASE 1 TO 50
				才能レベル:0 = 0
				才能コメント:0 = ふつう
			CASE 51 TO 55
				 才能レベル:0 = -3
				 才能コメント:0 = だめだめ
			CASE 56 TO 65
				才能レベル:0 = -2
				才能コメント:0 = ぜんぜん
			CASE 66 TO 85
				才能レベル:0 = -1
				才能コメント:0 = あんまり
			CASE 86 TO 95
				才能レベル:0 = 1
				才能コメント:0 = ちょっと
			CASEELSE
				才能レベル:0 = 2
				才能コメント:0 = なかなか
		ENDSELECT
	CASE 5
		SELECTCASE RAND:100
			CASE 1 TO 50
				才能レベル:0 = 0
				才能コメント:0 = ふつう
			CASE 51 TO 55
				 才能レベル:0 = -4
				 才能コメント:0 = ぜつぼう
			CASE 56 TO 65
				才能レベル:0 = -3
				才能コメント:0 = だめだめ
			CASE 66 TO 85
				才能レベル:0 = -2
				才能コメント:0 = ぜんぜん
			CASE 86 TO 95
				才能レベル:0 = -1
				才能コメント:0 = あんまり
			CASEELSE
				才能レベル:0 = 1
				才能コメント:0 = ちょっと
		ENDSELECT
	;高ランクは下がるのみ
	CASE 6, 7
		SELECTCASE RAND:100
			CASE 1 TO 50
				才能レベル:0 = 0
				才能コメント:0 = ふつう
			CASE 51 TO 55
				 才能レベル:0 = -5
				 才能コメント:0 = かいむ
			CASE 56 TO 65
				才能レベル:0 = -4
				才能コメント:0 = ぜつぼう
			CASE 66 TO 85
				才能レベル:0 = -3
				才能コメント:0 = だめだめ
			CASE 86 TO 95
				才能レベル:0 = -2
				才能コメント:0 = ぜんぜん
			CASEELSE
				才能レベル:0 = -1
				才能コメント:0 = あんまり
		ENDSELECT
ENDSELECT
;ランク才能計算終わり

;先に加算OR減産
PET:1 = PET:1 + 才能レベル:0

;数値系才能の処理
FOR LCOUNT,1, 5
	SELECTCASE RAND:100
		CASE 1 TO 50
			才能レベル:LCOUNT = 0
			才能コメント:LCOUNT = ふつう
		CASE 51 TO 53
			才能レベル:LCOUNT = -5
			才能コメント:LCOUNT = かいむ
		CASE 54 TO 57
			才能レベル:LCOUNT = -4
			才能コメント:LCOUNT = ぜつぼう
		CASE 58 TO 62
			才能レベル:LCOUNT = -3
			才能コメント:LCOUNT = だめだめ
		CASE 63 TO 68
			才能レベル:LCOUNT = -2
			才能コメント:LCOUNT = ぜんぜん
		CASE 69 TO 75
			才能レベル:LCOUNT = -1
			才能コメント:LCOUNT = あんまり
		CASE 76 TO 82
			才能レベル:LCOUNT = 1
			才能コメント:LCOUNT = ちょっと
		CASE 83 TO 88
			才能レベル:LCOUNT = 2
			才能コメント:LCOUNT = なかなか
		CASE 89 TO 93
			才能レベル:LCOUNT = 3
			才能コメント:LCOUNT = かなり
		CASE 94 TO 97
			才能レベル:LCOUNT = 4
			才能コメント:LCOUNT = がち
		CASE 98 TO 100
			才能レベル:LCOUNT = 5
			才能コメント:LCOUNT = ばけもの
	ENDSELECT
	PET:(LCOUNT + 9) = PET:(LCOUNT + 9) + 才能レベル:LCOUNT * 7
NEXT
;処理おわり

;マイナスになった時用にリミットかける
CALL LIMIT_PET_STATUS

;-----------------
;才覚分類おわり
;-----------------


;-----------------
;HPボーナス
;-----------------
;HPに応じて能力にボーナス
;ver1.2から才能システム一新したので制限キツ目に。
LOCAL:11 = LIMIT(DA:0:2 / 500, 1, 30)
FOR LCOUNT, 0, LOCAL:11
	PET:(10 + RAND:4) ++
NEXT
;-----------------
;HPボーナスおわり
;-----------------


;-----------------
;才能、ボーナス表示
;-----------------
PRINTFORML %PETNAME%はこんな子だったようだ
PRINTFORML %"ランク",10,LEFT%  :  %才能コメント:0%
PRINTFORML %"戦闘素質",10,LEFT%  :  %才能コメント:1%
PRINTFORML %"速度",10,LEFT%  :  %才能コメント:2%
PRINTFORML %"やる気",10,LEFT%  :  %才能コメント:3%
PRINTFORML %"カッコ良さ",10,LEFT%  :  %才能コメント:4%
