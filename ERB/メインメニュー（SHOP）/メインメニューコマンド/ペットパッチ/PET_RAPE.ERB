;-----------------------------------
;ペットのレイプ用関数
;ARG:0　ターゲットが確定している場合、その番号
;これに値が入っている場合は、レイプチェックも飛ばされる
;-----------------------------------
@PET_RAPE(ARG:0)

;----対象未設定の場合、チェックと対象確定----
IF !ARG:0
	CALL CHECK_PET_RAPE
	SIF !RESULT
		RETURN 0
	;チェック通り抜けた場合、対象選択
	TRYCCALLFORM PET_RAPE_TARGET_RATE_{PET:0}
	CATCH
		CALLFORM PET_RAPE_TARGET_RATE_5
	ENDCATCH
	;メッセ表示
	SETCOLOR 0xEE1010
	CALLFORM PRINT_MESSAGE(PET:51, 514, 0, CALLNAME:(PET:51), PETNAME)
	IF PET:0 >= 101 && PET:0 <= 105
		CALLFORM PRINT_MESSAGE(PET:9, 1514, 0, CALLNAME:(PET:51), PETNAME)
	ENDIF
	RESETCOLOR
ELSE
	PET:51 = ARG:0
ENDIF

	

TRYCALLFORM SET_ENEMY_DATA{PET:0}(0)

;----技IDの確定----
;リストがない場合はリビングバイン
TRYCCALLFORM SELECT_PET_RAPE_ACTION_{PET:0}
CATCH
	CALLFORM SELECT_PET_RAPE_ACTION_5
ENDCATCH

;----一応転写----
DA:0:50 = PET:50
DA:0:20 = PET:51
DA:0:51 = PET:51
TFLAG:50 = PET:50

;----戦闘中フラグ----
;もしもこの関数を戦闘中に呼び出した場合、戦闘中に0になるのを防ぐ
TFLAG:13 += 1

;----技データ呼び出し----
TFLAG:57 = PET:10
TRYCALLFORM FIX_ENEMY_ACTION_{DA:0:50}(0)
;----必中させるための処理----
TFLAG:61 = 99999
CFLAG:(PET:51):24 = 1
;----戦闘中フラグ終了----
;もしも戦闘中に呼び出した場合、戦闘中に0になるのを防ぐ
TFLAG:13 -= 1

;----屈服度補正----
;技の種類に応じて補正具合が違います
SELECTCASE TFLAG:55
	CASE 1
		LOCAL = CFLAG:(PET:51):400
	CASE 2
		LOCAL = CFLAG:(PET:51):400 / 3
	CASE 3
		LOCAL = CFLAG:(PET:51):400
	CASE 4
		LOCAL = CFLAG:(PET:51):400 / 2
	CASE 5
		LOCAL = CFLAG:(PET:51):400 / 4
ENDSELECT

IF TALENT:(PET:51):ペット
	LOCAL = LOCAL * 3 / 2
ELSEIF TALENT:(PET:51):触手使役術
	LOCAL = LOCAL * 2 / 3
ENDIF

TFLAG:58 += LOCAL


;----実処理----
CALL FIX_BATTLE_ACTION
;----好感度加算----
SELECTCASE PET_LIKE()
	CASE 3
		PET:22 += 3
	CASE 2
		PET:22 += 5 + RAND:4
	CASE 1
		PET:22 += 5 + RAND:6
	CASE 0
		PET:22 += 8 + RAND:6
ENDSELECT

SIF TALENT:(PET:51):触手使役術
	PET:22 += 1 + RAND:3

;----栄養価の計算----
;受け手の各能力を合算
LOCAL = MAX((ABL:(PET:51):Ａ感覚 + ABL:(PET:51):Ｂ感覚 + ABL:(PET:51):Ｃ感覚 + ABL:(PET:51):Ｖ感覚 + ABL:(PET:51):自慰中毒 + ABL:(PET:51):触手中毒 + ABL:(PET:51):射精中毒 + ABL:(PET:51):レズ中毒 + ABL:(PET:51):精液中毒), 8)
;ランクボーナス
SELECTCASE PET:1
	CASE 4, 5
		LOCAL = LOCAL + 3 + RAND:3
	CASE 1, 2, 3
		LOCAL = LOCAL + 3 + RAND:5
ENDSELECT

;おねだりを使用していたらボーナス
SIF CFLAG:(PET:51):50 >= 54 && CFLAG:(PET:51):50 <= 58
	LOCAL = LOCAL * 12 / 10

CALL PET_ADD_EXP(LOCAL, CALLNAME:(PET:51) + "の身体")

;----満腹度加算----
SELECTCASE PET_STARVE()
	CASE 3
		LOCAL = RAND:3
	CASE 2
		LOCAL = RAND:3 + 2
	CASE 1
		LOCAL = RAND:4 + 3
	CASE 0
		LOCAL = RAND:6 + 4
ENDSELECT
PET:20 += LOCAL
SETCOLOR 0x10EEEE
PRINTFORML 満腹度が上昇した！
RESETCOLOR
PRINTFORML 現在の満腹度:%PET_STARVE_STR()%

;----屈服度加算----
;レイプの場合は大きく入る
;身体を捧げた場合、複数回加算されるので小さめ
IF ARG:0
	CFLAG:(PET:51):400 += RAND:5 + 3
ELSE
	CFLAG:(PET:51):400 += RAND:10 + 5
ENDIF
SIF TALENT:(PET:51):ペット
	CFLAG:(PET:51):400 += 3


;----中出しされた場合転写----
IF CFLAG:(PET:51):60
	CFLAG:(PET:51):401 += CFLAG:(PET:51):60
	CFLAG:(PET:51):57 = 0
	CFLAG:(PET:51):60 = 0
ENDIF

;----あとしまつ----
DA:0:50 = 0
DA:0:20 = 0
DA:0:51 = 0
VARSET TFLAG, 0, 50, 100
TRYCALLFORM SET_ENEMY_DATA{PET:0}(0)
CALL LIMIT_PET_STATUS
RETURN 1

;-----------------------------------
;レイプ判定用関数
;戦闘時、フェイズ終了時に呼び出される
;-----------------------------------
@CHECK_PET_RAPE
VARSET LOCAL
LOCAL = PET:33
;空腹値補正
LOCAL = LOCAL + (100 - PET:20) / 10

;好感度補正
SELECTCASE PET_LIKE()
	CASE 3
		LOCAL -= 3
	CASE 2
		LOCAL -= 2
	CASE 0
		LOCAL += 3
ENDSELECT

;屈服度補正
LOCAL += PET_SUBMISSION_PT() / GET_MEMBER_SUM() / 50
LOCAL:1 = PET_SUBMISSION_PT() / GET_MEMBER_SUM() / 100

;ペット持ちを調べる
SIF TALENT:1:ペット
	LOCAL:2 += 3
SIF FLAG:10 && TALENT:(FLAG:10):ペット
	LOCAL:2 += 3
SIF FLAG:11 && TALENT:(FLAG:11):ペット
	LOCAL:2 += 3

;ランクによる上限、下限
SELECTCASE PET:1
	CASE 6, 7
		LOCAL = LIMIT(LOCAL, 3, 15 + LOCAL:1)
		SIF LOCAL:2 > 0
			LOCAL += RAND:(LOCAL:2)
	CASE 4, 5
		LOCAL = MAX(LOCAL, 2, 12 + LOCAL:1)
		SIF LOCAL:2 > 0
			LOCAL += RAND:(LOCAL:2)
	CASE 1, 2, 3
		LOCAL = MAX(LOCAL, 1, 9 + LOCAL:1)
		SIF LOCAL:2 > 0
			LOCAL += RAND:(LOCAL:2)
ENDSELECT
SIF RAND:100 < LOCAL
	RETURN 1
RETURN 0


;-----------------------------------
;ペットに身体を捧げる用関数
;VER1.3より若干修正。SHOW_SIMPLE_STATUSを使用するようにし、気絶でも選択肢にはあがるように
;ただし対象に選ぶことはできない
;LOCAL:1 = 対象の番号
;LOCAL:2 = おねだり使用するか
;LOCAL:3 = 回数を放り込む
;LOCAL:4 = おねだりする場合、どの部位か
;LOCAL:5 = 全員かどうか
;-----------------------------------
@BODY_TO_PET
#DIM LCOUNT
VARSET LOCAL
DRAWLINE
PRINTFORML ペットに%CALLNAME:1%%TACHI()%の身体を捧げます
PRINTFORML 誰が捧げますか？
PRINTFORML 栄養:{PET:21}
PRINTFORML 満腹度:%PET_STARVE_STR()%
DRAWLINE
PRINTFORM [1] 
CALL SHOW_SIMPLE_STATUS(1)
IF FLAG:10
	PRINTFORM [2] 
	CALL SHOW_SIMPLE_STATUS(FLAG:10)
ENDIF
IF FLAG:11
	PRINTFORM [3] 
	CALL SHOW_SIMPLE_STATUS(FLAG:11)
ENDIF
IF FLAG:10
	IF TALENT:1:140 || TALENT:(FLAG:10):140 || (FLAG:11 && TALENT:(FLAG:11):140)
	PRINTFORML 
	PRINTFORML [4] 全員
	ENDIF
ENDIF
DRAWLINE
PRINTFORML [0] - 戻る
$INPUT_LOOP
INPUT
IF RESULT == 1
	LOCAL:1 = 1
ELSEIF RESULT == 2 && FLAG:10
	LOCAL:1 = FLAG:10
ELSEIF RESULT == 3 && FLAG:11
	LOCAL:1 = FLAG:11
ELSEIF RESULT == 4 && FLAG:10
	LOCAL:1 = 0
	LOCAL:5 = 1
ELSEIF RESULT == 0
	RETURN
ELSE
	GOTO INPUT_LOOP
ENDIF
IF CFLAG:(LOCAL:1):29 && LOCAL:1
	PRINTFORML %CALLNAME:(LOCAL:1)%は戦意を喪失している……
	GOTO INPUT_LOOP
ELSEIF LOCAL:5
	SIF CFLAG:1:29
		PRINTFORML %CALLNAME:1%は戦意を喪失している……
	SIF CFLAG:(FLAG:10):29
		PRINTFORML %CALLNAME:(FLAG:10)%は戦意を喪失している……
	SIF CFLAG:(FLAG:11):29
		PRINTFORML %CALLNAME:(FLAG:11)%は戦意を喪失している……
	IF CFLAG:1:29 || (FLAG:10 && CFLAG:(FLAG:10):29) || (FLAG:11 && CFLAG:(FLAG:11):29)
		LOCAL:5 = 0
		GOTO INPUT_LOOP
	ENDIF
ENDIF

IF ABL:(LOCAL:1):触手中毒 >= 1 || CFLAG:(LOCAL:1):400 > 30 && LOCAL:1
	PRINTFORML
	PRINTFORML おねだりしますか？
	PRINTFORML [1] - はい
	PRINTFORML [2] - いいえ
	$INPUT_LOOP2
	INPUT
	IF RESULT == 1
		LOCAL:2 = 1
	ELSEIF RESULT == 2
		LOCAL:2 = 0
	ELSE
		REUSELASTLINE 不正な入力です
		GOTO INPUT_LOOP2
	ENDIF
ELSE
	LOCAL:2 = 0
ENDIF

;メッセ表示
IF LOCAL:5
	SETCOLOR 0xEE1010
	CALLFORM PRINT_MESSAGE(1, 514, 1, CALLNAME:1, PETNAME)
	SIF FLAG:10
		CALLFORM PRINT_MESSAGE((FLAG:10), 514, 1, CALLNAME:(FLAG:10), PETNAME)
	SIF FLAG:11
		CALLFORM PRINT_MESSAGE((FLAG:11), 514, 1, CALLNAME:(FLAG:11), PETNAME)
	RESETCOLOR
	IF PET:0 >= 101 && PET:0 <= 105
		CALLFORM PRINT_MESSAGE(PET:9, 1514, 1, CALLNAME:1, PETNAME)
	ENDIF
ELSE
	SETCOLOR 0xEE1010
	CALLFORM PRINT_MESSAGE((LOCAL:1), 514, 1, CALLNAME:(LOCAL:1), PETNAME)
	RESETCOLOR
	IF PET:0 >= 101 && PET:0 <= 105
		CALLFORM PRINT_MESSAGE(PET:9, 1514, 1, CALLNAME:1, PETNAME)
	ENDIF
ENDIF

IF LOCAL:2 == 1
	IF RAND:10
		LOCAL:3 = RAND:(LIMIT((100 - PET:10) / 20, 1, 5)) + 1
		;満腹度が低いほど回数が増える
		FOR LCOUNT, 0, LOCAL:3
			LOCAL:4 = RAND:5
			TRYCALLFORM FIX_PT_ACTION_{54 + LOCAL:4}(LOCAL:1)
			CFLAG:(LOCAL:1):50 = 54 + LOCAL:4
			CALL PET_RAPE(LOCAL:1)
		NEXT
	ELSE
		;極希に大暴走
		SETCOLOR 255, 0, 0
		PRINTFORML 大暴走だ！！
		RESETCOLOR
		FOR LCOUNT, 0, 10
			LOCAL:4 = RAND:5
			TRYCALLFORM FIX_PT_ACTION_{54 + LOCAL:4}(LOCAL:1)
			CFLAG:(LOCAL:1):50 = 54 + LOCAL:4
			CALL PET_RAPE(LOCAL:1)
		NEXT
	ENDIF
ELSEIF LOCAL:5
	FOR LOCAL:0, 0, 3
		IF RAND:10
			LOCAL:3 = RAND:(LIMIT((100 - PET:10) / 20, 1, 5)) + 1
			;満腹度が低いほど回数が増える
			FOR LCOUNT, 0, LOCAL:3
				LOCAL:4 = RAND:5
				TRYCALLFORM FIX_PT_ACTION_{54 + LOCAL:4}(GET_MEMBER(LOCAL:0))
				CFLAG:(GET_MEMBER(LOCAL:0)):50 = 54 + LOCAL:4
				CALL PET_RAPE(GET_MEMBER(LOCAL:0))
			NEXT
		ELSE
			;極希に大暴走
			SETCOLOR 255, 0, 0
			PRINTFORML 大暴走だ！！
			RESETCOLOR
			FOR LCOUNT, 0, 10
				LOCAL:4 = RAND:5
				TRYCALLFORM FIX_PT_ACTION_{54 + LOCAL:4}(GET_MEMBER(LOCAL:0))
				CFLAG:(GET_MEMBER(LOCAL:0)):50 = 54 + LOCAL:4
				CALL PET_RAPE(GET_MEMBER(LOCAL:0))
			NEXT
		ENDIF
	NEXT
ELSE
	IF RAND:10
		LOCAL:3 = RAND:(LIMIT((100 - PET:10) / 20, 1, 5)) + 1
		;満腹度が低いほど回数が増える
		FOR LCOUNT, 0, LOCAL:3
			CALL PET_RAPE(LOCAL:1)
		NEXT
	ELSE
		;極希に大暴走
		SETCOLOR 255, 0, 0
		PRINTFORML 大暴走だ！！
		RESETCOLOR
		FOR LCOUNT, 0, 10
			CALL PET_RAPE(LOCAL:1)
		NEXT
	ENDIF
ENDIF