;-----------------------------------
;ペットのレイプ用関数
;ARG:0　ターゲットが確定している場合、その番号
;これに値が入っている場合は、レイプチェックも飛ばされる
;-----------------------------------
@PET_RAPE(ARG:0)

;----対象未設定の場合、チェックと対象確定----
IF !ARG:0
	CALL CHECK_PET_RAPE
	SIF !RESULT
		RETURN 0
	;チェック通り抜けた場合、対象選択
	TRYCCALLFORM PET_RAPE_TARGET_RATE_{PET:0}
	CATCH
		CALLFORM PET_RAPE_TARGET_RATE_5
	ENDCATCH
	;メッセ表示
	SETCOLOR 0xEE1010
	CALLFORM PRINT_MESSAGE(PET:51, 514, 0, CALLNAME:(PET:51), PETNAME)
	IF PET:0 >= 101 && PET:0 <= 105
		CALLFORM PRINT_MESSAGE(PET:9, 1514, 0, CALLNAME:(PET:51), PETNAME)
	ENDIF
	RESETCOLOR
ELSE
	PET:51 = ARG:0
ENDIF

	

TRYCALLFORM SET_ENEMY_DATA{PET:0}(0)

;----技IDの確定----
;リストがない場合はリビングバイン
TRYCCALLFORM SELECT_PET_RAPE_ACTION_{PET:0}
CATCH
	CALLFORM SELECT_PET_RAPE_ACTION_5
ENDCATCH

;----一応転写----
DA:0:50 = PET:50
DA:0:20 = PET:51
DA:0:51 = PET:51
TFLAG:50 = PET:50

;----戦闘中フラグ----
;もしもこの関数を戦闘中に呼び出した場合、戦闘中に0になるのを防ぐ
TFLAG:13 += 1

;----技データ呼び出し----
TFLAG:57 = PET:10
TRYCALLFORM FIX_ENEMY_ACTION_{DA:0:50}(0)
;----必中させるための処理----
TFLAG:61 = 99999
CFLAG:(PET:51):24 = 1
;----戦闘中フラグ終了----
;もしも戦闘中に呼び出した場合、戦闘中に0になるのを防ぐ
TFLAG:13 -= 1

;----屈服度補正----
;技の種類に応じて補正具合が違います
SELECTCASE TFLAG:55
	CASE 1
		LOCAL = CFLAG:(PET:51):400
	CASE 2
		LOCAL = CFLAG:(PET:51):400 / 3
	CASE 3
		LOCAL = CFLAG:(PET:51):400
	CASE 4
		LOCAL = CFLAG:(PET:51):400 / 2
	CASE 5
		LOCAL = CFLAG:(PET:51):400 / 4
ENDSELECT

IF TALENT:(PET:51):ペット
	LOCAL = LOCAL * 3 / 2
ELSEIF TALENT:(PET:51):触手使役術
	LOCAL = LOCAL * 2 / 3
ENDIF

TFLAG:58 += LOCAL


;----実処理----
CALL FIX_BATTLE_ACTION
;----好感度加算----
SELECTCASE PET_LIKE()
	CASE 3
		PET:22 += 3
	CASE 2
		PET:22 += 5 + RAND:4
	CASE 1
		PET:22 += 5 + RAND:6
	CASE 0
		PET:22 += 8 + RAND:6
ENDSELECT

SIF TALENT:(PET:51):触手使役術
	PET:22 += 1 + RAND:3

;----栄養価の計算----
;受け手の各能力を合算
LOCAL = MAX((ABL:(PET:51):Ａ感覚 + ABL:(PET:51):Ｂ感覚 + ABL:(PET:51):Ｃ感覚 + ABL:(PET:51):Ｖ感覚 + ABL:(PET:51):自慰中毒 + ABL:(PET:51):触手中毒 + ABL:(PET:51):射精中毒 + ABL:(PET:51):レズ中毒 + ABL:(PET:51):精液中毒), 8)
;ランクボーナス
SELECTCASE PET:1
	CASE 4, 5
		LOCAL = LOCAL + 3 + RAND:3
	CASE 1, 2, 3
		LOCAL = LOCAL + 3 + RAND:5
ENDSELECT

;おねだりを使用していたらボーナス
SIF CFLAG:(PET:51):50 >= 54 && CFLAG:(PET:51):50 <= 58
	LOCAL = LOCAL * 12 / 10

CALL PET_ADD_EXP(LOCAL, CALLNAME:(PET:51) + "の身体")

;----満腹度加算----
SELECTCASE PET_STARVE()
	CASE 3
		LOCAL = RAND:3
	CASE 2
		LOCAL = RAND:3 + 2
	CASE 1
		LOCAL = RAND:4 + 3
	CASE 0
		LOCAL = RAND:6 + 4
ENDSELECT
PET:20 += LOCAL
SETCOLOR 0x10EEEE
PRINTFORML 満腹度が上昇した！
RESETCOLOR
PRINTFORML 現在の満腹度:%PET_STARVE_STR()%

;----屈服度加算----
;レイプの場合は大きく入る
;身体を捧げた場合、複数回加算されるので小さめ
IF ARG:0
	CFLAG:(PET:51):400 += RAND:5 + 3
ELSE
	CFLAG:(PET:51):400 += RAND:10 + 5
ENDIF
SIF TALENT:(PET:51):ペット
	CFLAG:(PET:51):400 += 3


;----中出しされた場合転写----
IF CFLAG:(PET:51):60
	CFLAG:(PET:51):401 += CFLAG:(PET:51):60
	CFLAG:(PET:51):57 = 0
	CFLAG:(PET:51):60 = 0
ENDIF

;----あとしまつ----
DA:0:50 = 0
DA:0:20 = 0
DA:0:51 = 0
VARSET TFLAG, 0, 50, 100
TRYCALLFORM SET_ENEMY_DATA{PET:0}(0)
CALL LIMIT_PET_STATUS
RETURN 1

;-----------------------------------
;レイプ判定用関数
;戦闘時、フェイズ終了時に呼び出される
;-----------------------------------
@CHECK_PET_RAPE
VARSET LOCAL
LOCAL = PET:33
;空腹値補正
LOCAL = LOCAL + (100 - PET:20) / 10

;好感度補正
SELECTCASE PET_LIKE()
	CASE 3
		LOCAL -= 3
	CASE 2
		LOCAL -= 2
	CASE 0
		LOCAL += 3
ENDSELECT

;屈服度補正
LOCAL += PET_SUBMISSION_PT() / GET_MEMBER_SUM() / 50
LOCAL:1 = PET_SUBMISSION_PT() / GET_MEMBER_SUM() / 100

;ペット持ちを調べる
SIF TALENT:1:ペット
	LOCAL:2 += 3
SIF FLAG:10 && TALENT:(FLAG:10):ペット
	LOCAL:2 += 3
SIF FLAG:11 && TALENT:(FLAG:11):ペット
	LOCAL:2 += 3

;ランクによる上限、下限
SELECTCASE PET:1
	CASE 6, 7
		LOCAL = LIMIT(LOCAL, 3, 15 + LOCAL:1)
		SIF LOCAL:2 > 0
			LOCAL += RAND:(LOCAL:2)
	CASE 4, 5
		LOCAL = MAX(LOCAL, 2, 12 + LOCAL:1)
		SIF LOCAL:2 > 0
			LOCAL += RAND:(LOCAL:2)
	CASE 1, 2, 3
		LOCAL = MAX(LOCAL, 1, 9 + LOCAL:1)
		SIF LOCAL:2 > 0
			LOCAL += RAND:(LOCAL:2)
ENDSELECT
SIF RAND:100 < LOCAL
	RETURN 1
RETURN 0


;-----------------------------------
;ペットに身体を捧げる用関数
;VER1.3より若干修正。SHOW_SIMPLE_STATUSを使用するようにし、気絶でも選択肢にはあがるように
;ただし対象に選ぶことはできない
;ALL_ABLE = 「全員」選択可能か
;ONEDARI = おねだり使用するか
;PARTY_STATE = パーティー内の各キャラが奉仕するかどうか
;-----------------------------------
@BODY_TO_PET
#DIM LCOUNT
#DIM  TEMP_REDRAW
#DIM ALL_ABLE
#DIM ONEDARI
#DIM PARTY_STATE, 3

VARSET PARTY_STATE

ONEDARI = 0
SIF FLAG:10 && (TALENT:1:140 || TALENT:(FLAG:10):140 || (FLAG:11 && TALENT:(FLAG:11):140))
	ALL_ABLE = 1

;全員戦意喪失ならできない
IF CFLAG:1:29 && (FLAG:10 == 0 || CFLAG:(FLAG:10):29) && (FLAG:11 == 0 || CFLAG:(FLAG:11):29)
	PRINTFORML %CALLNAME:1%%TACHI()%は戦意を喪失している……
	RETURN
ENDIF

CURRENTREDRAW
TEMP_REDRAW = RESULT
REDRAW 0

DRAWLINE
PRINTFORML ペットに%CALLNAME:1%%TACHI()%の身体を捧げます
PRINTFORML 誰が捧げますか？
PRINTFORML 栄養:{PET:21}
PRINTFORML 満腹度:%PET_STARVE_STR()%
DRAWLINE
PRINTFORM [1] 
CALL SHOW_SIMPLE_STATUS(1)
IF FLAG:10
	PRINTFORM [2] 
	CALL SHOW_SIMPLE_STATUS(FLAG:10)
ENDIF
IF FLAG:11
	PRINTFORM [3] 
	CALL SHOW_SIMPLE_STATUS(FLAG:11)
ENDIF
IF ALL_ABLE
	PRINTFORML 
	PRINTFORML [4] 全員
ENDIF
DRAWLINE
PRINTFORML [0] - 戻る

REDRAW TEMP_REDRAW

$INPUT_LOOP
INPUT
IF RESULT >= 1 && RESULT <= GET_MEMBER_SUM()
	IF CFLAG:(GET_MEMBER(RESULT - 1)):29
		PRINTFORML %CALLNAME:(GET_MEMBER(RESULT - 1))%は戦意を喪失している……
		GOTO INPUT_LOOP
	ENDIF
	PARTY_STATE:(RESULT - 1) = 1
	IF ABL:(GET_MEMBER(RESULT - 1)):触手中毒 >= 1 || CFLAG:(GET_MEMBER(RESULT - 1)):400 > 30
		PRINTFORML
		PRINTFORML おねだりしますか？
		CALL ASK_YN
		IF RESULT == 0
			ONEDARI = 1
		ENDIF
	ENDIF
ELSEIF RESULT == 4 && ALL_ABLE
	; 全員の場合はおねだり確定
	ONEDARI = 1
	FOR LCOUNT, 0, GET_MEMBER_SUM()
		SIF CFLAG:(GET_MEMBER(LCOUNT)):29 == 0
			PARTY_STATE:LCOUNT = 1
	NEXT
ELSEIF RESULT == 0
	RETURN
ELSE
	REUSELASTLINE 入力値が不正です。再入力してください
	GOTO INPUT_LOOP
ENDIF

;メッセ表示
SETCOLOR 0xEE1010
FOR LCOUNT, 0, 3
	IF PARTY_STATE:LCOUNT
		CALLFORM PRINT_MESSAGE(GET_MEMBER(LCOUNT), 514, 1, GET_MEMBER_NAME(LCOUNT), PETNAME)
	ENDIF
NEXT
RESETCOLOR
IF PET:0 >= 101 && PET:0 <= 105
	CALLFORM PRINT_MESSAGE(PET:9, 1514, 1, CALLNAME:1, PETNAME)
ENDIF

FOR LCOUNT, 0, 3
	IF PARTY_STATE:LCOUNT
		CALL BODY_TO_PET_EXECUTE(GET_MEMBER(LCOUNT), ONEDARI)
	ENDIF
NEXT

;-----------------------------------
;ペットに身体を捧げるー実行部分
;CHARA_ID = 対象のキャラID(1/FLAG:10/FLAG:11)
;ONEDARI  = おねだりするか(0しない/1レイプ毎/2最初だけ)
;-----------------------------------
@BODY_TO_PET_EXECUTE(CHARA_ID, ONEDARI)
#DIM CHARA_ID
#DIM ONEDARI
#DIM LCOUNT
#DIM ONEDARI_TYPE
#DIM RAPE_NUM

;最初におねだり
IF ONEDARI == 2
	ONEDARI_TYPE = RAND:5
	TRYCALLFORM FIX_PT_ACTION_{54 + ONEDARI_TYPE}(CHARA_ID)
	CFLAG:CHARA_ID:50 = 54 + ONEDARI_TYPE
ENDIF

;レイプ回数指定
IF RAND:10
	;満腹度が低いほど回数が増える
	RAPE_NUM = RAND:(LIMIT((100 - PET:10) / 20, 1, 5)) + 1
ELSE
	;極希に大暴走
	SETCOLOR 255, 0, 0
	PRINTFORML 大暴走だ！！
	RESETCOLOR
	RAPE_NUM = 10
ENDIF

FOR LCOUNT, 0, RAPE_NUM
	;レイプ毎におねだり
	IF ONEDARI == 1
		ONEDARI_TYPE = RAND:5
		TRYCALLFORM FIX_PT_ACTION_{54 + ONEDARI_TYPE}(CHARA_ID)
		CFLAG:CHARA_ID:50 = 54 + ONEDARI_TYPE
	ENDIF
	CALL PET_RAPE(CHARA_ID)
NEXT
