@SHOP_PET
#DIM  TEMP_REDRAW
	
	CURRENTREDRAW
	TEMP_REDRAW = RESULT
	REDRAW 0

	DRAWLINE
	PRINTFORML ペットに何をしますか？
	DRAWLINE
	PRINTFORML %"PTの状態", 15, LEFT%:
	PRINTFORML %CALLNAME:1, 15, LEFT%:%PET_SUBMISSION_STR(1)%
	SIF FLAG:10
		PRINTFORML %CALLNAME:(FLAG:10), 15, LEFT%:%PET_SUBMISSION_STR(FLAG:10)%
	SIF FLAG:11
		PRINTFORML %CALLNAME:(FLAG:11), 15, LEFT%:%PET_SUBMISSION_STR(FLAG:11)%
	DRAWLINE
	PRINTFORML [1] - 餌やり
	PRINTFORML [2] - 身体を捧げる
	PRINTFORML [3] - 栄養を消費して色々する
	PRINTFORML [4] - ペットの名前を変更する
	IF TALENT:1:140 == 1 || TALENT:(FLAG:10):140 == 1 || TALENT:(FLAG:11):140 == 1
		PRINTFORML [5] - 下克上
	ENDIF
	PRINTFORML [999] - ペットとさよならする
	PRINTFORML [0] - 戻る
	
	REDRAW TEMP_REDRAW
	
	$INPUT_LOOP
	INPUT
	IF RESULT == 1
		CALL FOOD_TO_PET
	ELSEIF RESULT == 2
		CALL BODY_TO_PET
	ELSEIF RESULT == 3
		CALL PET_USE_EXP
	ELSEIF RESULT == 4
		CALL RENAME_PET
	ELSEIF RESULT == 5
		CALL CONTRACT_PET
	ELSEIF RESULT == 999
		JUMP BYEBYE_PET
	ELSEIF RESULT == 0
		RETURN
	ELSE
		GOTO INPUT_LOOP
	ENDIF
	RESTART

@PET_USE_EXP
#DIM  TEMP_REDRAW
	LOCAL = 100 + (PET_SUBMISSION_PT() / GET_MEMBER_SUM())
	LOCAL:1 = 100 + PET:1 * 50 + (PET_SUBMISSION_PT() / GET_MEMBER_SUM())
	
	CURRENTREDRAW
	TEMP_REDRAW = RESULT
	REDRAW 0
	
	DRAWLINE
	PRINTFORML 栄養を消費して様々なことができます
	PRINTFORML 何を行いますか？
	IF PET_EXP_ENOUGH(LOCAL)
		PRINTFORML [1] - レベルアップ　経験値{LOCAL}消費
	ELSE
		SETCOLOR 0x333333
		PRINTFORML [1] - レベルアップには栄養が足りない
		RESETCOLOR
	ENDIF
	IF PET_EXP_ENOUGH(LOCAL:1) && PET:1 <= 6
		PRINTFORML [2] - ランクアップ　経験値{LOCAL:1}消費
	ELSEIF PET:1 >= 7
		SETCOLOR 0x333333
		PRINTFORML [2] - %PETNAME%は既に最高ランクに達している
		RESETCOLOR
	ELSE
		SETCOLOR 0x333333
		PRINTFORML [2] - ランクアップには栄養が足りない
		RESETCOLOR
	ENDIF
	PRINTFORML [3] - アイテムを生産
	PRINTFORML [0] - 戻る
	
	REDRAW TEMP_REDRAW
	
	$INPUT_LOOP
	INPUT
	IF RESULT == 1 && PET_EXP_ENOUGH(LOCAL) && PET:1 != 9999
		CALL PET_LVUP(LOCAL)
	ELSEIF RESULT == 1 && !PET_EXP_ENOUGH(LOCAL)
		REUSELASTLINE レベルアップに必要な栄養を満たしていないようだ……
		GOTO INPUT_LOOP
	ELSEIF RESULT == 1 && PET_EXP_ENOUGH(LOCAL) && PET:1 >= 9999
		REUSELASTLINE レベルアップしようと思ったが、%PETNAME%はレベルの限界に達しているようだ……
		GOTO INPUT_LOOP

	ELSEIF RESULT == 2 && PET_EXP_ENOUGH(LOCAL:1) && PET:1 <= 6
		CALL PET_RANKUP(LOCAL:1)
	ELSEIF RESULT == 2 && !PET_EXP_ENOUGH(LOCAL:1) && PET:1 <= 6
		REUSELASTLINE ランクアップに必要な栄養を満たしていないようだ……
	ELSEIF RESULT == 2 && PET_EXP_ENOUGH(LOCAL:1) && PET:1 >= 7
		REUSELASTLINE ランクアップしようと思ったが、%PETNAME%はランクの限界に達しているようだ……

	ELSEIF RESULT == 3
		JUMP PET_EXP_SHOP

	ELSEIF RESULT == 0
		RETURN

	ELSE
		REUSELASTLINE 不正入力です。正しい値を入力してください。
		GOTO INPUT_LOOP
	ENDIF


;-----------------------------------
;レベルアップ用関数
;-----------------------------------
@PET_LVUP(ARG:0)
#DIM  TEMP_REDRAW

PET:2 ++
PET:21 -= ARG:0

CURRENTREDRAW
TEMP_REDRAW = RESULT
REDRAW 0

PRINTL
SETCOLOR 255, 255, 0
PRINTFORMW レ　ベ　ル　ア　ッ　プ　！　！
RESETCOLOR
PRINTFORML
PRINTFORMW おめでとう！　ボーナスを選んでください
PRINTFORML レベル:{PET:2 - 1, 4} →{PET:2, 4}
PRINTFORML [0] - 戦闘能力   現在:{PET:10, 4}
PRINTFORML [1] - 速さ       現在:{PET:11, 4}
PRINTFORML [2] - やる気     現在:{PET:12, 4}
PRINTFORML [3] - カッコよさ 現在:{PET:13, 4}

REDRAW TEMP_REDRAW

$INPUT_LOOP
INPUT
IF RESULT >= 0 && RESULT <= 3
	IF PET:(RESULT + 10) == 9999
		REUSELASTLINE 既にカンスト済みです
		GOTO INPUT_LOOP
	ELSE
		PRINTL
		SELECTCASE RESULT
			CASE 0
				PRINT 戦闘能力
			CASE 1
				PRINT 速さ
			CASE 2
				PRINT やる気
			CASE 3
				PRINT カッコよさ
		ENDSELECT
		PRINTFORMW がアップした！
		PET:(RESULT + 10) ++
	ENDIF
ELSE
	REUSELASTLINE 不正入力です。正しい値を入力してください。
	GOTO INPUT_LOOP
ENDIF
RETURN

;-----------------------------------
;ランクアップ用関数
;-----------------------------------
@PET_RANKUP(ARG:0)
#DIM  TEMP_REDRAW

VARSET LOCAL
PET:21 -= ARG:0
PET:1 ++

CURRENTREDRAW
TEMP_REDRAW = RESULT
REDRAW 0

PRINTL
SETCOLOR 255, 255, 0
PRINTFORMW ラ　ン　ク　ア　ッ　プ　！　！
RESETCOLOR
PRINTL
FOR LOCAL, 0, 5
	REDRAW 0
	PRINTFORML おめでとう！　ボーナスを選んでください
	PRINTFORML なお、ランクアップのボーナスは{5 - LOCAL}ポイントです
	PRINTFORML ランク:{PET:1 - 1, 4} →{PET:1, 4}
	PRINTFORML [0] - 戦闘能力   現在:{PET:10, 4}
	PRINTFORML [1] - 速さ       現在:{PET:11, 4}
	PRINTFORML [2] - やる気     現在:{PET:12, 4}
	PRINTFORML [3] - カッコよさ 現在:{PET:13, 4}
	LOCAL:1 = 0
	$INPUT_LOOP
	REDRAW TEMP_REDRAW
	INPUT
	IF RESULT >= 0 && RESULT <= 3
		IF PET:(RESULT + 10) == 9999
			REUSELASTLINE 既にカンスト済みです
			GOTO INPUT_LOOP
		ELSE
			PRINTL
			SELECTCASE RESULT
				CASE 0
					PRINT 戦闘能力
				CASE 1
					PRINT 速さ
				CASE 2
					PRINT やる気
				CASE 3
					PRINT カッコよさ
			ENDSELECT
			PRINTFORMW がアップした！
			PET:(RESULT + 10) ++
		ENDIF
	CLEARLINE 10 + LOCAL:1
	ELSE
		REUSELASTLINE 不正入力です。正しい値を入力してください。
		LOCAL:1 ++
		GOTO INPUT_LOOP
	ENDIF
NEXT
REDRAW TEMP_REDRAW
PRINTFORML おめでとう！　そしておめでとう！
RETURN

;-----------------------------------
;ペットの名前変更用関数
;-----------------------------------
@RENAME_PET
#DIM  TEMP_REDRAW

CURRENTREDRAW
TEMP_REDRAW = RESULT
REDRAW 0

DRAWLINE
PRINTFORML 現在のペットの名前は【%PETNAME%】です
PRINTFORML 名前を入力してください（キャンセルする場合はそのままEnterを押してください）

REDRAW TEMP_REDRAW

$INPUT_LOOP
INPUTS
IF RESULTS == ""
	RETURN
ELSE
	PETNAME = %RESULTS%
	PRINTFORMW ペットの名前を【%RESULTS%】に変更しました。
ENDIF

;-----------------------------------
;ペット消去用関数
;-----------------------------------
@BYEBYE_PET(ARG)
#DIM LCOUNT
#DIM  TEMP_REDRAW

	CURRENTREDRAW
	TEMP_REDRAW = RESULT
	REDRAW 0

;ARG	デバッグ経由なら1
	LOCAL:0 = ARG
	DRAWLINE
	PRINTFORML ペットと別れます
	PRINTFORML 二度と戻って来ません
	PRINTFORML 今までつぎ込んできた栄養やアイテムも全部パァです
	PRINTFORML 本当によろしいですか？
	PRINTFORML [1] - やる
	PRINTFORML [0] - やめる
	$INPUT_LOOP
	INPUT
	IF RESULT == 1
		IF TALENT:1:140 > 1 || (FLAG:10 > 0 && TALENT:(FLAG:10):140 > 1) || (FLAG:11 > 0 && TALENT:(FLAG:11):140 > 1)
			IF LOCAL:0 || FLAG:10 == 0
				IF PET_SUBMISSION_PT() / GET_MEMBER_SUM() > 60 && LOCAL:0 == 0
					CALLFORM PRINT_MESSAGE(1, 514, 4, CALLNAME:1, PETNAME, 1)
					GOTO INPUT_LOOP
				ENDIF
				CALLFORM PRINT_MESSAGE(1, 514, 4, CALLNAME:1, PETNAME, 0)

				VARSET PET
				VARSET PETNAME
				FOR LCOUNT, 0, CHARANUM
					CFLAG:LCOUNT:400 = 0
				NEXT
				TRYCALLFORM SET_ENEMY_DATA_99(0)
				RETURN
			ENDIF
			CALL LINK_FALL_2
			RETURN
		ELSE
			IF PET_SUBMISSION_PT() / GET_MEMBER_SUM() > 60 && LOCAL:0 == 0
				CALLFORM PRINT_MESSAGE(1, 514, 4, CALLNAME:1, PETNAME, 1)
				GOTO INPUT_LOOP
			ENDIF
			CALLFORM PRINT_MESSAGE(1, 514, 4, CALLNAME:1, PETNAME, 0)

			VARSET PET
			VARSET PETNAME
			FOR LCOUNT, 0, CHARANUM
				CFLAG:LCOUNT:400 = 0
			NEXT
			TRYCALLFORM SET_ENEMY_DATA_99(0)
			RETURN
		ENDIF
	ELSEIF RESULT == 0
		RETURN
	ENDIF
	
	REDRAW TEMP_REDRAW
	
