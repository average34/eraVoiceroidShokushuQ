;お店（という名の待機画面）処理

@EVENTSHOP
DRAWLINE

@SHOW_SHOP
#DIM LCOUNT,3	;ループ用
#DIM LINECOUNTBAK

;ゲームプレイ後にcsvでキャラ追加が行われると管理人数との齟齬が発生する為
;管理するキャラの人数設定を常時更新する
CALL GET_CSVCHARA

;最も多く表示されるSHOP画面でキャラクタの各種データを正規化する
;バグ対策・エラーデータを保持するセーブデータ対策も兼ねて
CALL NORMALIZE_CHARADATA

CALL STATUS_RENEW

;PRINTFORML SHOPでの表示テスト
;捕食されている場合は入力無しでループ
WHILE CFLAG:1:10
	PRINTFORML {DAY}日目：%CONVERT_TIME()%
	PRINTL  
	CFLAG:1:11 = 2
	CFLAG:1:13 = FLAG:5
	CFLAG:1:14 = FLAG:5

	CALL MAIN_PHASE
	IF RESULT == 1
		RETURN
	ENDIF
	PRINTL  
WEND

;眠気が限界の場合は強制睡眠
IF FLAG:7 >= 15
	IF FLAG:AUTOMODE
		PRINTFORML 眠気が限界に達した%CALLNAME:1%%TACHI()%は気絶してしまった！
		TWAIT FLAG:AUTOMODE * 800, 0
	ELSE
		PRINTFORMW 眠気が限界に達した%CALLNAME:1%%TACHI()%は気絶してしまった！
	ENDIF
	PRINTFORML 
	CFLAG:1:11 = 4
	CFLAG:1:13 = FLAG:5
	CFLAG:1:14 = FLAG:5
	FLAG:8 = 3
	CALL GET_MARK(1, 17)
	IF FLAG:10
		CALL GET_MARK(FLAG:10, 17)
	ENDIF
	IF FLAG:11
		CALL GET_MARK(FLAG:11, 17)
	ENDIF
	CALL MAIN_PHASE
	RESTART
ELSEIF FLAG:8 >= 3
	IF FLAG:AUTOMODE
		PRINTFORML %CALLNAME:1%%TACHI()%は気絶している…
		TWAIT FLAG:AUTOMODE * 800, 0
	ELSE
		PRINTFORMW %CALLNAME:1%%TACHI()%は気絶している…
	ENDIF
	PRINTFORML 
	CFLAG:1:11 = 4
	CFLAG:1:13 = FLAG:5
	CFLAG:1:14 = FLAG:5
	FLAG:8 -= 1
	CALL MAIN_PHASE
	RESTART
ELSEIF FLAG:8 == 2
	IF FLAG:AUTOMODE
		PRINTFORML %CALLNAME:1%%TACHI()%は目を覚ました！
		TWAIT FLAG:AUTOMODE * 800, 0
	ELSE
		PRINTFORMW %CALLNAME:1%%TACHI()%は目を覚ました！
	ENDIF
	PRINTFORML 
	FLAG:8 = 0
ENDIF

IF CFLAG:1:10 == 0 && CFLAG:1:29
	IF (!FLAG:10 || CFLAG:(FLAG:10):29) && (!FLAG:11 || CFLAG:(FLAG:11):29)
		CALL PRINT_MESSAGE(1, 158, 1, CALLNAME:1, "")
		CFLAG:1:29 = 0
		BASE:1:1 = 1
		
		IF (FLAG:11 && CFLAG:(FLAG:11):10)
			CALL MEMBER_EXIT(1)
		ENDIF
		IF (FLAG:10 && CFLAG:(FLAG:10):10)
			CALL MEMBER_EXIT(0)
		ENDIF
	ENDIF
ENDIF


;＝＝＝メイン画面描画処理ここから＝＝＝
REDRAW 0
DRAWLINEFORM =
CALL MAP_INFO_SIDE_ROOMREPORT(-1)

CALL PRINT_MAP_INFO

DRAWLINE

;PT簡易ステータス表示
CALL SHOW_PT_STATUS

DRAWLINE

;つぶやき表示
CALL PRINT_MESSAGE(1, 160, 0, CALLNAME:1, "")
CALL PRINT_MESSAGE(FLAG:10, 160, 0, CALLNAME:1, "")
CALL PRINT_MESSAGE(FLAG:11, 160, 0, CALLNAME:1, "")

;選択肢表示
CALL PRINT_LCL(1)
IF FLAG:AUTOMODE
	PRINTFORML オートモードを始めると基本的に自動でゲームが進みます。
	PRINTFORML マップが表示されるたびにオートセーブされています。
	PRINTFORML 停止する場合マップ表示がされた直後から次の文字が出るまでに0以外の数字を入力してください。
	PRINTFORML 
	PRINTFORML オート期間を設定する場合は数字でターン数を入力してください（4ターンで1日です）
	PRINTFORML 指定する場合は1～120までです。
	PRINTFORML 指定解除は0を入力してください。ターン数指定がない場合止まるまでオートし続けます。
	PRINTFORML 
	IF FLAG:AUTOTURN
		PRINTFORML {FLAG:AUTOTURN}ターン（{FLAG:AUTOTURN / 4}日）オート
	ELSE
		PRINTFORML クリアかゲームオーバーまでオート
	ENDIF
	CALL PRINT_LCL(0, "[180] - オートモードを始める")
ELSE
	IF FLAG:997 == 81
		CALL PRINT_LCL(0, "[100] - ミニゲームに戻る")
	ELSE
		CALL PRINT_LCL(0, "[100] - 周囲を探索")
	ENDIF

	CALL PRINT_LCL(0, "[101] - 行動する")
	IF FLAG:10
		CALL PRINT_LCL(0, "[102] - 仲間と別れる・話す")
	ELSE
		SETCOLOR 0x333333
		CALL PRINT_LCL(0, "[102] - 仲間と別れる・話す")
		RESETCOLOR
	ENDIF

	CALL PRINT_LCL(0, "[110] - 能力確認")
	CALL PRINT_LCL(0, "[111] - 装備変更")
	CALL PRINT_LCL(0, "[112] - アイテム確認・使用")


	IF FLAG:6 >= 100
		CALL PRINT_LCL(0, "[120] - この部屋を浄化")
	ELSE
		SETCOLOR 0x333333
		CALL PRINT_LCL(0, "[120] - この部屋を浄化")
		RESETCOLOR
	ENDIF
	CALL CHECK_OHANATSUMI
	IF RESULT != -1
		CALL PRINT_LCL(0, "[121] - お花を摘む")
	ELSE
		SETCOLOR 0x333333
		CALL PRINT_LCL(0, "[121] - お花を摘む")
		RESETCOLOR
	ENDIF
	IF FLAG:7 >= 3
		CALL PRINT_LCL(0, "[122] - この部屋で寝る")
	ELSE
		SETCOLOR 0x333333
		CALL PRINT_LCL(0, "[122] - この部屋で寝る")
		RESETCOLOR
	ENDIF
	;ペットパッチ分
	IF PET:0
		IF !TALENT:1:ペット
			CALL PRINT_LCL(0, "[123] - ペット")
		ELSE
			CALL PRINT_LCL(0, "[123] - ご主人様")
		ENDIF
	ENDIF

	IF TRAP:0 && TRAP:1 && TRAP:2 && TRAP:5 != FLAG:5 && TRAP:6 != FLAG:5 && TRAP:7 != FLAG:5
		SETCOLOR 0x333333
		CALL PRINT_LCL(0, "[124] - トラップ")
		RESETCOLOR
	ELSE
		CALL PRINT_LCL(0, "[124] - トラップ")
	ENDIF

	CALL PRINT_LCL(0, "[125] - 手書き地図")
ENDIF

CALL PRINT_LCL(2)

DRAWLINE
CALL PRINT_LCL(1)
CALL PRINT_LCL(0, "[150] - コンフィグ")
IF FLAG:9
	CALL PRINT_LCL(0, "[500] - デバッグコマンド")
ELSE
	CALL PRINT_LCL(0, "　")
ENDIF
CALL PRINT_LCL(0, "[200] - システム")
CALL PRINT_LCL(2)

REDRAW 1
;＝＝＝メイン画面描画ここまで＝＝＝


CALL AUTO_SAVE

CALL SHOW_TUTORIAL(4)
IF RESULT
	RESTART
ENDIF



IF FLAG:(FLAG:5 + 50) >= 100
	;汚染度MAXのチュートリアル
	CALL SHOW_TUTORIAL(7)
	IF RESULT
		RESTART
	ENDIF
ENDIF

@USERSHOP

IF RESULT == 180 && FLAG:AUTOMODE
	CALL PC_AUTO_ACTION
ELSEIF RESULT == 0 && FLAG:AUTOMODE
	FLAG:AUTOTURN = 0
	PRINTFORML オートモードのターン数を指定しません。
	RETURN
ELSEIF RESULT >= 1 && RESULT <= 120 && FLAG:AUTOMODE
	FLAG:AUTOTURN = RESULT
	PRINTFORML オートの度{FLAG:AUTOTURN}ターン（{FLAG:AUTOTURN / 4}日）経過します
	RETURN
; 手書き地図を直接編集
ELSEIF RESULT >= 1 && RESULT <= 30
	CALL HAND_MAP_MAIN(RESULT)
	RETURN
	
;探索
ELSEIF RESULT == 100
	IF FLAG:997 == 81
		JUMP MINIGAME_SHOP
	ENDIF
	CFLAG:1:11 = 3
	CFLAG:1:13 = FLAG:5
	CFLAG:1:14 = FLAG:5
	CALL PRINT_MESSAGE(1, 135, 0, CALLNAME:1, "")
	CALL PRINT_MESSAGE(FLAG:10, 135, 0, CALLNAME:1, "")
	CALL PRINT_MESSAGE(FLAG:11, 135, 0, CALLNAME:1, "")
;行動
ELSEIF RESULT == 101
	CALL MOVE_SELECT(FLAG:5)
	IF RESULT == 0
		RETURN
	ENDIF
	CFLAG:1:11 = RESULT:1
	CFLAG:1:13 = FLAG:5
	CFLAG:1:14 = RESULT
	;イベント未発生なら
	IF RESULT:2 == 0
		;突撃
		IF RESULT:1 == 0
			CALL PRINT_MESSAGE(1, 132, 0, CALLNAME:1, "")
			CALL PRINT_MESSAGE(FLAG:10, 132, 0, CALLNAME:1, "")
			CALL PRINT_MESSAGE(FLAG:11, 132, 0, CALLNAME:1, "")
		;慎重
		ELSEIF RESULT:1 == 1
			CALL PRINT_MESSAGE(1, 133, 0, CALLNAME:1, "")
			CALL PRINT_MESSAGE(FLAG:10, 133, 0, CALLNAME:1, "")
			CALL PRINT_MESSAGE(FLAG:11, 133, 0, CALLNAME:1, "")
		;待ち伏せ
		ELSEIF RESULT:1 == 2
			CALL PRINT_MESSAGE(1, 134, 0, CALLNAME:1, "")
			CALL PRINT_MESSAGE(FLAG:10, 134, 0, CALLNAME:1, "")
			CALL PRINT_MESSAGE(FLAG:11, 134, 0, CALLNAME:1, "")
		ENDIF
	ENDIF


;話しかける
ELSEIF RESULT == 102 && FLAG:10
;	CALL BREAK_UP_PT
;	CALL TALK_SELECT
	CALL PT_TALK
	RETURN

;------
;能力確認
ELSEIF RESULT == 110
	CALL CHECK_STATUS
	RETURN
;装備変更
ELSEIF RESULT == 111
	CALL CHANGE_EQUIP
	RETURN
;アイテム
ELSEIF RESULT == 112
	CALL SHOW_ITEM_LIST
	RETURN
;------
;部屋を浄化
ELSEIF RESULT == 120 && FLAG:6 >= 100
	CALL CLEAN
	RETURN
;お花摘み
ELSEIF RESULT == 121
	CALL CHECK_OHANATSUMI(1)
	IF RESULT != -1
		CALL OHANATSUMI
	ENDIF
	RETURN
;部屋で寝る
ELSEIF RESULT == 122 && FLAG:7 >= 3
	CALL SLEEP
	IF RESULT != 1
		RETURN
	ENDIF

ELSEIF RESULT == 123 && PET:0
	CALL SHOP_PET
	IF RESULT != 1
		RETURN
	ENDIF
ELSEIF RESULT == 124 && !(TRAP:0 && TRAP:1 && TRAP:2 && TRAP:5 != FLAG:5 && TRAP:6 != FLAG:5 && TRAP:7 != FLAG:5)
	CALL TRAP_SET
	IF RESULT != 1
		RETURN
	ENDIF
ELSEIF RESULT == 125
	CALL HAND_MAP_MAIN
	RETURN
;------
ELSEIF RESULT == 150
	CALL SET_CONFIGURE
	RETURN
ELSEIF RESULT == 500 && FLAG:9
	CALL DEBUG_COMMAND
	RETURN
ELSEIF RESULT == 200
	CALL SYSTEM_COMMAND
	RETURN
;------
ELSEIF RESULT == 999
	IF FLAG:9
		PRINTFORMW デバッグモードをオフにしました
		FLAG:9 = 0
	ELSE
		PRINTFORMW デバッグモードをオンにしました
		FLAG:9 = 1
	ENDIF
	RETURN

;------
ELSEIF RESULT == 1000
	CALL PRINT_MAP_INFO_SIDE(-1)
	RETURN
ELSEIF RESULT > 1000 && RESULT < 2000
	LOCAL = RESULT
	CALL PRINT_MAP_INFO_SIDE(-2)
	IF RESULT == 1
		CALL MAP_INFO_SIDE_ROOMREPORT(LOCAL * -1)
		IF RESULT < 0
			PRINTW 入力値が不正です！
		ENDIF
		RETURN
	ELSE
		PRINTW 入力値が不正です！
		RETURN
	ENDIF
ELSE
	PRINTW 入力値が不正です！
	RETURN
ENDIF
CALL PT_TALK(-1)

CALL MAP_INFO_SIDE_ROOMREPORT(-10)
CALL MAIN_PHASE

;=================================================
;システムコマンド
;=================================================
@SYSTEM_COMMAND

WHILE 1
	REDRAW 0
	DRAWLINE
	PRINTL
	PRINTL [ 1] セーブ
	PRINTL
	PRINTL [ 2] ロード
	PRINTL
	PRINTL [ 3] タイトルに戻る
	PRINTL
	PRINTL
	PRINTL [ 0] キャンセル
	REDRAW 1
	
	$INPUT_LOOP
	INPUT
	
	IF RESULT == 0
		RETURN
	ELSEIF RESULT == 1
		CALL SAVEGAME_EX
	ELSEIF RESULT == 2
		CALL LOADGAME_EX
	ELSEIF RESULT == 3
		PRINTL タイトルに戻りますか？※セーブしていない情報は失われてしまいます！
		PRINTL [ 1] はい
		PRINTL
		PRINTL [ 0] キャンセル
		$INPUT_LOOP2
		INPUT
		IF RESULT == 0
			
		ELSEIF RESULT == 1
			RESETDATA
			BEGIN TITLE
		ELSE
			GOTO INPUT_LOOP2
		ENDIF
	ELSE
		GOTO INPUT_LOOP
	ENDIF

WEND

;=================================================

